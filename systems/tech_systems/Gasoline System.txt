:GAS_SYS
thread 'GAS_SYS'

create_thread @GAS_SY2

GASOLINE_OFFED_FLAG = 0        
        
:GASOLINE_CHECK
wait 0

if and
Actor.Driving(PLAYER_ACTOR)   
$CARS_WORK_FLAG == 0
    then
    GASOLINE_OFFED_FLAG = 0
    if and
    $INF_GASOLINE == 0
    $END_SAVING == 0
        then
        if and
        80DD: not actor PLAYER_ACTOR driving_car_with_model #BMX
        80DD: not actor PLAYER_ACTOR driving_car_with_model #MTBIKE
        80DD: not actor PLAYER_ACTOR driving_car_with_model #BIKE
        80DD: not actor PLAYER_ACTOR driving_car_with_model 15489
        89AE: not actor PLAYER_ACTOR driving_train
            then   
            0@ = Actor.CurrentCar(PLAYER_ACTOR)
            0D5E: get_vehicle 0@ class_to 11@ subclass_to 22@
            if
            not 22@ == 10
            jf @GASOLINE_CHECK_9 
            32@ = 0  
            0A97: 11@ = car 0@ struct 
            11@ += 4 
            0A8D: 12@ = read_memory 11@ size 1 virtual_protect 0                        
            
            if
            88B7:   not test 12@ bit 1
            jf @GASOLINE_CHECK_9    
            32@ = 0
            08BD: set 12@ bit 1 
            0A8C: write_memory 11@ size 1 value 12@ virtual_protect 0 
            08C3: clear 12@ bit 1                   
            random_float $BENZIN_CAR $GASOLINE_MIN $GASOLINE_MAX
            jump @GASOLINE_MINUS
            end
        end
    end 
    
jump @GASOLINE_CHECK                             

:GASOLINE_CHECK_9 
32@ = 0        
0A97: 22@ = car 0@ struct 
0A8E: 23@ = 22@ + 5 // int 
0A8D: $BENZIN_CAR = read_memory 23@ size 4 virtual_protect 0     
if
not $BENZIN_CAR < 1.0
jf @GASOLINE_NONE
gosub @GASOLINE_CHECK_MAX_CAP
if
8026: not  $BENZIN_CAR < 23@  // (float)
jf @GASOLINE_1000
jump @GASOLINE_MINUS

:GASOLINE_1000
0088: $BENZIN_CAR = 23@ // (float)
jump @GASOLINE_MINUS

:GASOLINE_MINUS 
wait 0
if and
actor.InCar(PLAYER_ACTOR,0@)
$INF_GASOLINE == 0
jf @GASOLINE_CHECK_2

if
CAR_2P_UPDATE_GAS_INCAR == 1
then
0A97: 22@ = car 0@ struct 
0A8E: 23@ = 22@ + 5 // int 
0A8D: $BENZIN_CAR = read_memory 23@ size 4 virtual_protect 0
CAR_2P_UPDATE_GAS_INCAR = 0
end

///////////////////////////////////////////////////////////////

if and
84C8: not  actor PLAYER_ACTOR driving_flying_vehicle
84A7: not  actor PLAYER_ACTOR driving_boat
then

if
PERK_GOODRIVER == 0
then
28@ = 2000
else

if
PERK_GOODRIVER == 1
then
28@ = 2400
else
28@ = 2900
end

end

0093: 28@ = integer 28@ to_float
006F: 28@ *= GAS_WASTE_SPEED_DIVIDER // (float)
0092: 28@ = float 28@ to_integer

if
002D:   32@ >= 28@  // (int)
jf @GASOLINE_MINUS

else

if
04C8:   actor PLAYER_ACTOR driving_flying_vehicle
    then
    
    ////////////////////////
    
    if
    PERK_GOODRIVER == 0
    then
    28@ = 1500
    else
    
    if
    PERK_GOODRIVER == 1
    then
    28@ = 1800
    else
    28@ = 2175
    end
    
    end
    
    ////////////////////////
    
    0093: 28@ = integer 28@ to_float
    006F: 28@ *= GAS_WASTE_SPEED_DIVIDER // (float)
    0092: 28@ = float 28@ to_integer
    
    if
    002D:   32@ >= 28@  // (int)
    jf @GASOLINE_MINUS
    
    end

if
04A7:   actor PLAYER_ACTOR driving_boat
    then
    
    ////////////////////////
    
    if
    PERK_GOODRIVER == 0
    then
    28@ = 800
    else
    
    if
    PERK_GOODRIVER == 1
    then
    28@ = 960
    else
    28@ = 1160
    end
    
    end
    
    ////////////////////////
    
    0093: 28@ = integer 28@ to_float
    006F: 28@ *= GAS_WASTE_SPEED_DIVIDER // (float)
    0092: 28@ = float 28@ to_integer
    
    if
    002D:   32@ >= 28@  // (int)
    jf @GASOLINE_MINUS   
    
    end

end

///////////////////////////////////////////////////////////////

if
not $BENZIN_CAR < 1.0 
jf @GASOLINE_NONE
if
not $BENZIN_CAR == 0
jf @GASOLINE_MINUS
32@ = 0
0A97: 10@ = car 0@ struct
0A8E: 2@ = 10@ + 0x49C 
0A8D: 3@ = read_memory 2@ size 4 virtual_protect 1
0092: 4@ = float 3@ to_integer
0092: 20@ = float 20@ to_integer

if and
047A:   actor PLAYER_ACTOR driving_bike
not 4@ == 1
then
4@ = 0
end

if and
84C8: not  actor PLAYER_ACTOR driving_flying_vehicle
not actor.DrivingVehicleType(PLAYER_ACTOR,#VORTEX)
then
if or
4@ == 1   
4@ == -1   
jf @GASOLINE_MINUS
else
if or
00E1:   player 0 pressed_key 16
00E1:   player 0 pressed_key 14   
jf @GASOLINE_MINUS
end

if or
car.Model(0@) == #KART
car.Model(0@) == #MOWER
car.Model(0@) == #TRACTOR
car.Model(0@) == #CADDY
car.Model(0@) == #TUG
car.Model(0@) == #SWEEPER
car.Model(0@) == #BAGGAGE
car.Model(0@) == #VORTEX
then
$BENZIN_CAR -= 0.075  
$BENZIN_LOST += 0.075
else
$BENZIN_CAR -= 0.15  
$BENZIN_LOST += 0.15
end

if and
0969:   car 0@ is_big
not PLAYER_CAR_BONUS_FLAG == 1
then
$BENZIN_CAR -= 0.05  
$BENZIN_LOST += 0.05
end

if
actor.DrivingVehicleType(PLAYER_ACTOR,ARMY_HUNTER_MODEL_NUMBER)
then
if
SURVIVAL_CUSTOM_WEAPON_NABOR == 6
    then
    $BENZIN_CAR -= 0.08  
    $BENZIN_LOST += 0.08
    else
    $BENZIN_CAR -= 0.20  
    $BENZIN_LOST += 0.20
    end
end

if
actor.DrivingVehicleType(PLAYER_ACTOR,#RHINO)
then
if
SURVIVAL_CUSTOM_WEAPON_NABOR == 6
    then
    $BENZIN_CAR -= 0.18  
    $BENZIN_LOST += 0.18
    else
    $BENZIN_CAR -= 0.45  
    $BENZIN_LOST += 0.45
    end
end

if
$BENZIN_CAR < 0.15
then
$BENZIN_CAR = 0.0
end

if
not $BENZIN_CAR < 1.0
jf @GASOLINE_NONE
jump @GASOLINE_MINUS  

:GASOLINE_NONE

if
84C8: not  actor PLAYER_ACTOR driving_flying_vehicle
then

if
GASOLINE_OFFED_FLAG == 0
then
GASOLINE_OFFED_FLAG = 1

if
047A:   actor PLAYER_ACTOR driving_bike
then
02D4: car 0@ turn_off_engine
else
0918: set_car 0@ engine_operation 0  
end

text.ShowTextBox('INV362')
create_thread @GAS_CAR 0@ 0 0 0 PLAYER_ACTOR
end

else

if
04AB:   actor PLAYER_ACTOR driving_plane
then
0ABF: set_vehicle 0@ engine_state_to false
if
GASOLINE_OFFED_FLAG == 0
    then
    GASOLINE_OFFED_FLAG = 1
    text.ShowTextBox('INV362')
    create_thread @GAS_CAR 0@ 0 0 0 PLAYER_ACTOR
    end
else

if
GASOLINE_OFFED_FLAG == 0
then
//07D5: set_car 0@ velocity_in_direction_XYZ 0.0 0.0 -0.01 rotation_velocitiesXY 0.0 0.0 unk 0.0
//0622: AS_actor PLAYER_ACTOR bail_car 0@
GASOLINE_OFFED_FLAG = 1
create_thread @GAS_HEL 0@ 0 0 0 PLAYER_ACTOR
text.ShowTextBox('INV362')
end

end

end

jump @GASOLINE_MINUS

:GASOLINE_CHECK_2
if
056E:   car 0@ defined
jf @GASOLINE_CHECK
0A97: 22@ = car 0@ struct 
0A8E: 23@ = 22@ + 5 // int 
0A8C: write_memory 23@ size 4 value $BENZIN_CAR virtual_protect 0 
jump @GASOLINE_CHECK

:GASOLINE_CHECK_MAX_CAP

22@ = car.Model(0@)
23@ = 100.0

if
0820:   model 22@ heli
then
23@ = 200.0
end

if
081F:  model 22@ plane 
then
23@ = 300.0
end

if and
881E: not model 22@ boat
881F: not model 22@ plane
8820: not model 22@ heli
8A01: not model 22@ car
then
23@ = 50.0
end

if
car.Model(0@) == ARMY_HUNTER_MODEL_NUMBER
then
23@ = 1420.0
end

if or
car.Model(0@) == #MAVERICK
car.Model(0@) == #POLMAV
car.Model(0@) == #VCNMAV
then
23@ = 574.0
end

if
car.Model(0@) == #ANDROM
then
23@ = 178610.0
end

if
car.Model(0@) == #BEAGLE
then
23@ = 1628.0
end

if
car.Model(0@) == #CARGOBOB
then
23@ = 3914.0
end

if
car.Model(0@) == #CROPDUST
then
23@ = 160.0
end

if
car.Model(0@) == #LEVIATHN
then
23@ = 6360.0
end

if
car.Model(0@) == #RAINDANC
then
23@ = 2720.0
end

if
car.Model(0@) == #NEVADA
then
23@ = 4700.0
end

if
car.Model(0@) == #RUSTLER
then
23@ = 1392.0
end

if or
car.Model(0@) == #SPARROW
car.Model(0@) == #SEASPAR
then
23@ = 900.0
end

if
car.Model(0@) == #SHAMAL
then
23@ = 6068.0
end

if
car.Model(0@) == #AT400
then
23@ = 32546.0
end

if
car.Model(0@) == #HYDRA
then
23@ = 2800.0
end

if
car.Model(0@) == #STUNT
then
23@ = 336.0
end

if
car.Model(0@) == #RHINO
then
23@ = 3800.0
end

if or
car.Model(0@) == #DODO
car.Model(0@) == #SKIMMER
then
23@ = 170.0
end

return

:GAS_HEL
thread 'GAS_HEL'

0819: 3@ = actor 4@ distance_from_ground

0A97: 1@ = car 0@ struct
1@ += 0x84C //HELI ROTOR POWER
0A8D: 1@ = read_memory 1@ size 4 virtual_protect 0

while 056E:   car 0@ defined
wait 0

1@ -= 0.001
if
1@ < 0.0
    then
    1@ = 0.0
    break
    end

if
056E:   car 0@ defined
then
0A97: 2@ = car 0@ struct
2@ += 0x84C //HELI ROTOR POWER
0A8C: write_memory 2@ size 4 value 1@ virtual_protect 0
0E65: get_car_collision_intensity 0@ store_to 7@
if or
7@ > 0.0
8E93: not is_car_really_in_air 0@
    then
    break
    end
else
end_thread
end

end

while 056E:   car 0@ defined
wait 0

if
056E:   car 0@ defined
then
0A97: 2@ = car 0@ struct
2@ += 0x84C //HELI ROTOR POWER
0A8C: write_memory 2@ size 4 value 0.0 virtual_protect 0
0E65: get_car_collision_intensity 0@ store_to 7@
if
02BF:   car 0@ sunk
    then
    7@ = 69.0
    8@ = 1
    end 

if and
7@ > 0.0
5@ == 0
    then
    5@ = 1
    if or
    3@ < 50.0
    8@ == 1
        then
        
        if and
        8@ == 1
        3@ > 200.0
        then
        jump to @GAS_HEL_DO_BOOM
        else
                
        0092: 6@ = float 3@ to_integer
        if
        8@ == 0
            then
            6@ *= 2
            end
        
        if
        actor.InCar(4@,0@)
            then
            0851: set_actor 4@ decrease_health_by 6@ affect_armour 0
            0622: AS_actor 4@ bail_car 0@
            end
        
        end
        
        end_thread
        else
        :GAS_HEL_DO_BOOM
        020B: explode_car 0@
        end_thread
        end
    end

else
end_thread
end

end

end_thread        

:GAS_CAR
thread 'GAS_CAR'

while 056E:   car 0@ defined
wait 0

if
056E:   car 0@ defined
then
if
actor.InCar(4@,0@)
    then
    0EFD: get_car_pedals 0@ gas_to 2@ break_to 3@ 
    00E2: get_player 0 key 0 state_to 3@
    if and
    2@ > 0.0
    3@ == 0
    8E93: not is_car_really_in_air 0@
        then    
        02E3: 1@ = car 0@ speed
//        0AD1: show_formatted_text_highpriority "%f" time 2000 1@
        
        if
        0E4D: random_percent 80
        then
        1@ *= 1.19
        car.SetSpeedInstantly(0@,1@)
        end
            
        end
    else
    end_thread
    end    
else
end_thread
end

end

end_thread        