const
ARTIST_HANDLE = 19@
end

:ARTIST
thread 'ARTIST'

create_thread @ELY_SIM

:ARTIST_START

///////////////////////////////////

while true
wait 500

if and
0EEA: locate_char_distance_to_coordinates PLAYER_ACTOR pos 1726.9001 -1636.8458 20.2241 radius 90.0
$SAVESTATUS == 0
$ONMISSION == 0
    then
    0A40: 0@ = create_entrance_marker_at 1726.9336 -1636.4836 20.7241 color 14
    break
    end

end

///////////////////////////////////

while true
wait 0

if or
8EEA: not locate_char_distance_to_coordinates PLAYER_ACTOR pos 1726.9001 -1636.8458 20.2241 radius 90.0
$SAVESTATUS == 1
$ONMISSION == 1
    then
    0A41: destroy_entrance_marker 0@
    jump to @ARTIST_START
    end

if and
0EEA: locate_char_distance_to_coordinates PLAYER_ACTOR pos 1726.9001 -1636.8458 20.2241 radius 1.0
$SAVESTATUS == 0
$ONMISSION == 0
    then
    0A41: destroy_entrance_marker 0@
    break
    end

end

///////////////////////////////////

player.CanMove(PLAYER_CHAR,false)
$HUD_REMOVE = 1
fade 0 500
wait 500

$SAVESTATUS = 1
INT_ZOMBIE_OFF_FADING_AND_SHIT_FLAG = 1
$INT_ZMB_NOT = 1

select_interior 18
0860: link_actor PLAYER_ACTOR to_interior 18
04F9: set_sky_color 1 fade 0
actor.PutAtNoOffset(PLAYER_ACTOR,1726.9486, -1638.8882, 20.2235)
actor.Angle(PLAYER_ACTOR) = 176.9909
camera.Restore_WithJumpCut()
create_thread @REFR_3D

wait 500

008B: 18@ = $ALLOW_TANK_THROW_OBJ // (int)
$ALLOW_TANK_THROW_OBJ = 0

$SAVESTATUS = 0

$TANK_INTERIOR_SPAWN = 1
$WITCH_INTERIOR_SPAWN = 1
$TANKID_FLAG = 14733
$WITCHID_FLAG = 14734

$SPAWN_TANK_ON_COORD = 1  
$TANK_X = 1719.0131
$TANK_Y = -1668.317
$TANK_Z = 20.2256

$SPAWN_WITCH_ON_COORD = 1  
$WITCH_X = 1721.7488
$WITCH_Y = -1666.8229
$WITCH_Z = 20.2263

SPEC_INF_ONLY_ONE_INSTANSE_SPAWN_FLAG = 1
$SPAWN_TANK_FLAG = 1
$SPAWN_WITCH_FLAG = 1

wait 1000

$ZOMBIE_DONT_ATTACK_PLAYER = 1

repeat
wait 0
$WITCH_INSTANT_AGR = 1

if and
056D:   actor $TANK[0] defined
056D:   actor $WITCH[0] defined
    then
    break
    end

until false

player.CanMove(PLAYER_CHAR,true)
$HUD_REMOVE = 0
fade 1 500
wait 500

$ZOMBIE_DONT_ATTACK_PLAYER = 0

///////////////////////////////////

while not model.Available(14884)
wait 0
model.Load(14884)
mload
end
    
ARTIST_HANDLE = actor.Create(26,14884,1720.6465, -1672.5062, 27.2058)
077A: set_actor ARTIST_HANDLE acquaintance 0 to_actors_pedtype 0 // see ped.dat
0E24: fix_char ARTIST_HANDLE ground 0 brightness DO_ACTORS_BRIGHT_CORR and_fade_in 1
actor.Angle(ARTIST_HANDLE) = 357.2811     
actor.SetWalkStyle(ARTIST_HANDLE,"GANG2")
0446: set_actor ARTIST_HANDLE dismemberment_possible 0
060B: set_actor ARTIST_HANDLE decision_maker_to $DEC_PEOPLE
actor.Health(ARTIST_HANDLE) = 1000
02A9: set_actor ARTIST_HANDLE immune_to_nonplayer 1
07DD: set_actor ARTIST_HANDLE attack_rate 100 // previously known as temper_to
087E: set_actor ARTIST_HANDLE weapon_droppable 0

while not animation.Available("ON_LOOKERS")
wait 0
animation.Load("ON_LOOKERS")
end

0812: AS_actor ARTIST_HANDLE perform_animation "panic_cower" IFP "ON_LOOKERS" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 9999999  // versionB

20@ = 0
21@ = 0
22@ = 0
23@ = 0

RANDOM_MEETINGS_COUNTER += 1
create_thread @MTG_ACH

while true
wait 0

if and
actor.Dead($WITCH[0])
21@ == 0
    then
    
    23@ = 0
    
    21@ = 1
    
    v$PHRASE3 = "PAINWD1"
    
    v$2NAME3 = "GAME840"
                            
    09FD: get_gxt_string v$PHRASE3 width_to 29@
    0093: 29@ = integer 29@ to_float
    29@ *= 8.0
    0092: 29@ = float 29@ to_integer
    
    32@ = 0                    
                                                                   
    00BC: show_text_highpriority GXT v$PHRASE3 time 29@ 1
    wait 29@  
    
    end

if and
actor.Dead($TANK[0])
20@ == 0
    then
    
    23@ = 0
    
    20@ = 1
    
    v$PHRASE3 = "PAINTD1"
    
    v$2NAME3 = "GAME840"
                            
    09FD: get_gxt_string v$PHRASE3 width_to 29@
    0093: 29@ = integer 29@ to_float
    29@ *= 8.0
    0092: 29@ = float 29@ to_integer
    
    32@ = 0                    
                                                                   
    00BC: show_text_highpriority GXT v$PHRASE3 time 29@ 1
    wait 29@  
    
    end

if
0EEA: locate_char_distance_to_coordinates PLAYER_ACTOR pos 1713.6493 -1673.147 27.2055 radius 3.0
    then
    23@ = 1
    jump to @ARTIST_ACTION
    end
    
if and
20@ == 1
21@ == 1
22@ == 0
    then    
    
    v$PHRASE3 = "PAINAG0"
    
    v$2NAME3 = "GAME840"
                            
    09FD: get_gxt_string v$PHRASE3 width_to 29@
    0093: 29@ = integer 29@ to_float
    29@ *= 8.0
    0092: 29@ = float 29@ to_integer
    
    32@ = 0                    
                                                                   
    00BC: show_text_highpriority GXT v$PHRASE3 time 29@ 1
    wait 29@
    
    :ARTIST_ACTION
    22@ = 1  
    
    0792: disembark_instantly_actor ARTIST_HANDLE
    
    0781: get_weapon_with_ID 41 model_to 0@

    while not model.Available(0@)
    wait 0
    model.Load(0@)
    mload
    end
    
    actor.GiveWeaponAndAmmo(ARTIST_HANDLE,41,99999)
    
    0615: define_AS_pack_begin 0@  
    05D3: AS_actor -1 goto_point 1720.7098 -1671.7579 27.2021 mode 7 time 99999 ms // versionA
    05D4: AS_actor -1 rotate_angle 331.9175
    05BC: AS_actor -1 jump 1
    05E2: AS_actor -1 kill_actor PLAYER_ACTOR
    0616: define_AS_pack_end 0@
    0618: assign_actor ARTIST_HANDLE to_AS_pack 0@
    061B: remove_references_to_AS_pack 0@
    054E: clear_actor ARTIST_HANDLE damage
    
    end    

if and
0E45: frame_mod 90
20@ == 0
21@ == 0
22@ == 0
    then
    
    gosub @RANDOM_DIALOG_ARTIST1
                
    v$2NAME3 = "GAME840"
                            
    09FD: get_gxt_string v$PHRASE3 width_to 29@
    0093: 29@ = integer 29@ to_float
    29@ *= 8.0
    0092: 29@ = float 29@ to_integer
    
    32@ = 0                    
                                                                   
    00BC: show_text_highpriority GXT v$PHRASE3 time 29@ 1                                    
    
    end
    
if and
0E45: frame_mod 90
22@ == 1
not 23@ == 1
    then
    
    gosub @RANDOM_DIALOG_ARTIST2
                
    v$2NAME3 = "GAME840"
                            
    09FD: get_gxt_string v$PHRASE3 width_to 29@
    0093: 29@ = integer 29@ to_float
    29@ *= 8.0
    0092: 29@ = float 29@ to_integer
    
    32@ = 0                    
                                                                   
    00BC: show_text_highpriority GXT v$PHRASE3 time 29@ 1                                    
    
    end          

if and
0E45: frame_mod 30
22@ == 0
    then
    05BF: AS_actor ARTIST_HANDLE look_at_actor PLAYER_ACTOR 5000 ms
    end        

if or
$SAVESTATUS == 1
$ONMISSION == 1
    then
    actor.DestroyInstantly(ARTIST_HANDLE)
    $TANKID_FLAG = 0
    $WITCHID_FLAG = 0
    jump to @ARTIST_START
    end     
    
if
actor.Dead(ARTIST_HANDLE)
    then
    
    $TEMP_XP = 5000
    008D: $TEMP_XP = integer $TEMP_XP to_float
    0069: $TEMP_XP *= $XP_COEFF // (float)
    if
    PERK_CHARISMA == 2
    then
    $TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
    end
    008C: $TEMP_XP = float $TEMP_XP to_integer
    0058: $PLAYER_XP += $TEMP_XP // (int)
    
    actor.RemoveReferences(ARTIST_HANDLE)
    
    create_thread @ACH142
    
    v$PHRASE3 = "PDEAD1"
    
    v$2NAME3 = "GAME840"
                            
    09FD: get_gxt_string v$PHRASE3 width_to 29@
    0093: 29@ = integer 29@ to_float
    29@ *= 8.0
    0092: 29@ = float 29@ to_integer
    
    32@ = 0                    
                                                                   
    00BC: show_text_highpriority GXT v$PHRASE3 time 29@ 1
    
    repeat
    wait 0
    
    if and
    actor.Dead($TANK[0])
    actor.Dead($WITCH[0])
        then
        break
        end
    
    until false
    
    $TANK_INTERIOR_SPAWN = 0
    $WITCH_INTERIOR_SPAWN = 0    
    $SPAWN_TANK_ON_COORD = 0      
    $SPAWN_WITCH_ON_COORD = 0
    $TANKID_FLAG = 0
    $WITCHID_FLAG = 0      
    SPEC_INF_ONLY_ONE_INSTANSE_SPAWN_FLAG = 0
    008A: $ALLOW_TANK_THROW_OBJ = 18@ // (int)
    $WITCH_INSTANT_AGR = 0
    
    0A40: 0@ = create_entrance_marker_at 1726.9178 -1638.3378 20.7232 color 14
    
    while true
    wait 0
    
    if
    0EEA: locate_char_distance_to_coordinates PLAYER_ACTOR pos 1726.9178 -1638.3378 20.2232 radius 1.0
        then
        
        0A41: destroy_entrance_marker 0@
        
        player.CanMove(PLAYER_CHAR,false)
        $HUD_REMOVE = 1
        fade 0 500
        wait 500
        
        $SAVESTATUS = 1
        
        select_interior 0
        0860: link_actor PLAYER_ACTOR to_interior 0
        04FA: reset_sky_colors_with_fade 0
        actor.PutAtNoOffset(PLAYER_ACTOR,1726.9896, -1635.4597, 20.2241)
        actor.Angle(PLAYER_ACTOR) = 0.6733
        camera.Restore_WithJumpCut()
        create_thread @REFR_3D
        
        wait 500
        
        $SAVESTATUS = 0
        INT_ZOMBIE_OFF_FADING_AND_SHIT_FLAG = 0
        $INT_ZMB_NOT = 0        
        
        player.CanMove(PLAYER_CHAR,true)
        $HUD_REMOVE = 0
        fade 1 500
        wait 500
        
        08E7: disable_entrance_markers 0
        
        end_thread
        
        end
    
    end
    
    end_thread
    end        

end

end_thread

:RANDOM_DIALOG_ARTIST1
    
for 1@ = 0 to 200

v$PHRASE1 = "PAINT"
0AD3: v$PHRASE2 = format "%d" 1@
098B: v$PHRASE3 = v$PHRASE1 + v$PHRASE2

09FD: get_gxt_string v$PHRASE3 width_to 31@

if
not 31@ == 0
    then    
    1@ += 1
    random_int 30@ 0 1@
    end

end  

v$PHRASE1 = "PAINT"
0AD3: v$PHRASE2 = format "%d" 30@
098B: v$PHRASE3 = v$PHRASE1 + v$PHRASE2

return

:RANDOM_DIALOG_ARTIST2
    
for 1@ = 0 to 200

v$PHRASE1 = "PAINAG"
0AD3: v$PHRASE2 = format "%d" 1@
098B: v$PHRASE3 = v$PHRASE1 + v$PHRASE2

09FD: get_gxt_string v$PHRASE3 width_to 31@

if
not 31@ == 0
    then    
    1@ += 1
    random_int 30@ 0 1@
    end

end  

v$PHRASE1 = "PAINAG"
0AD3: v$PHRASE2 = format "%d" 30@
098B: v$PHRASE3 = v$PHRASE1 + v$PHRASE2

return