:C_DRDM
thread 'C_DRDM'

32@ = 0
33@ = 0

create_thread @CAR_BST

while true

:CAR_RANDOM_DRIVING_DAMAGE
wait 0  

if and
actor.Driving(PLAYER_ACTOR)
not actor.Dead(PLAYER_ACTOR)
847A: not  actor PLAYER_ACTOR driving_bike
84C8: not  actor PLAYER_ACTOR driving_flying_vehicle
84A7: not  actor PLAYER_ACTOR driving_boat
not actor.DrivingVehicleType(PLAYER_ACTOR,RHINO_MODEL_NUMBER)
$ZOMBIE_DERBY_FLAG == 0
not $NOTSPAWN_CARS == 2
    then   
        
    if
    UNITED_MAP == 0
    then
    if or
    actor.DrivingVehicleType(PLAYER_ACTOR,15426)
    actor.DrivingVehicleType(PLAYER_ACTOR,15410)
    actor.DrivingVehicleType(PLAYER_ACTOR,15488)
        then
        jump to @CAR_RANDOM_DRIVING_DAMAGE_SKIP
        end
    end
    
    if and
    not actor.DrivingVehicleType(PLAYER_ACTOR,#QUAD)    
    not actor.DrivingVehicleType(PLAYER_ACTOR,15558)
    not ALLOW_CAR_CRASH_DMG == 0    
        then    
        
        :CAR_DAMAGE_TURRET_JUMP
        
        if
        8A32: not actor PLAYER_ACTOR on_turret_of_car
        then
        0@ = actor.CurrentCar(PLAYER_ACTOR)      
        else
        008B: 0@ = DRIVEBY_CAR // (int)
        end  
    
        02E3: 3@ = car 0@ speed    
    
        0227: 1@ = car 0@ health
        
        32@ = 0
        repeat
        wait 0
        
        if
        $DRIVEBY_MODE == 1
            then
            if
            32@ > 100
                then
                break
                end
            end
        
        until 32@ > 300        
    //    wait 300
        
        if and
        0A32: actor PLAYER_ACTOR on_turret_of_car
        056E:   car 0@ defined
        then
        jump to @CAR_DAMAGE_TURRET_JUMP2
        end
        
        if and
        actor.Driving(PLAYER_ACTOR)
        056E:   car 0@ defined
            then
            :CAR_DAMAGE_TURRET_JUMP2
            0227: 2@ = car 0@ health
            
            0085: 10@ = 1@ // (int)
            0085: 11@ = 2@ // (int)
            
            0062: 10@ -= 11@  // (int)
            
            0093: 10@ = integer 10@ to_float    
            10@ *= 0.3    
            0092: 10@ = float 10@ to_integer
    
            0092: 3@ = float 3@ to_integer
            
            if and
            056D:   actor $TANK[0] defined
            056E:   car 0@ defined 
                then
                if
                0202:   actor $TANK[0] near_car 0@ radius 7.0 7.0 sphere 0 
                    then
                    10@ = 0    
                    end
                end
                
            if
            856E: not  car 0@ defined
            then
            jump to @CAR_RANDOM_DRIVING_DAMAGE
            end
            
            if
            0185:   car 0@ health >= 1001
                then
                if
                3@ < 40
                    then
                    jump to @CAR_RANDOM_DRIVING_DAMAGE
                    end
                end          
            
            if and      
            803B: not  1@ == 2@  // (int)
            851C: not  car 0@ damaged_by_actor -1
            10@ > 10
            3@ > 20
                then                               
                
                0085: 19@ = 10@ // (int)
                0085: 20@ = 10@ // (int)
                20@ *= 5
                19@ *= 3            
                camera.Shake(19@)
                0093: 19@ = integer 19@ to_float
                0093: 20@ = integer 20@ to_float
                19@ *= 0.025
                099C: jiggle_camera type 1 timelimit 20@ intensity 19@                      
                
                if and
                3@ > 20
                3@ < 35
                then 
                3@ *= 2    
                else              
                3@ *= 3     
                end
                
                if
                PERK_CARPILLOW == 1
                then
                0093: 3@ = integer 3@ to_float
                3@ *= 0.75
                0092: 3@ = float 3@ to_integer
                end
                
                if
                CUSTOM_MODE_ONE_HIT_KO == 1
                then
                05BE: AS_actor PLAYER_ACTOR die
                094E: set_actor PLAYER_ACTOR mute_pain_audio 1
                09F1: play_audio_at_actor PLAYER_ACTOR event 1189
                end
                
                if
                0185:   car 0@ health >= 1001
                then
                0093: 3@ = integer 3@ to_float
                3@ *= 0.60
                0092: 3@ = float 3@ to_integer
                end     
                    
                2@ = actor.Health(PLAYER_ACTOR)  
                0062: 2@ -= 3@  // (int)     
                actor.Health(PLAYER_ACTOR) = 2@
                
                if
                CUSTOM_MODE_ONE_HIT_KO == 1
                then
                3@ = 5000
                end
                
                if
                3@ < 25
                then
                
                if $ONMISSION == false
                then 0A1A: actor PLAYER_ACTOR perform_walk_animation "CAR_crash_pro" IFP "camera" framedelta 30.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
                else 0605: actor PLAYER_ACTOR perform_animation "CAR_crash_pro" IFP "camera" framedelta 30.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
                end
                
                else
                
                if $ONMISSION == false
                then 0A1A: actor PLAYER_ACTOR perform_walk_animation "CAR_crash_weak" IFP "camera" framedelta 30.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
                else 0605: actor PLAYER_ACTOR perform_animation "CAR_crash_weak" IFP "camera" framedelta 30.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
                end
                
                end                           
                
                3@ *= 3            
        
                //////////////////////////////////////////////////////////////////////////////////
                
                01EA: 25@ = car 0@ max_passengers
                if
                25@ > 0
                then
                
                25@ -= 1
                for 26@ = 0 to 25@
                if
                8431: not  car 0@ passenger_seat_free 26@
                    then                          
                    0432: 27@ = get_actor_handle_from_car 0@ passenger_seat 26@                
                    if
                    056D:   actor 27@ defined
                        then
                        
                        if
                        003A:   27@ == COOP_ACTOR // (int)
                        then
                        3@ /= 6
                        end
                        
                        2@ = actor.Health(27@)  
                        0062: 2@ -= 3@  // (int)     
                        actor.Health(27@) = 2@
                        
                        if
                        07D6:   27@ == COOP_ACTOR // @ == $ (int)
                        then
                        3@ *= 6
                        end                                                
                        
                        if
                        2@ < 1
                            then
                            09F1: play_audio_at_actor 27@ event 1130
                            05BE: AS_actor 27@ die
                            039E: set_actor 27@ locked 0 while_in_car
                            0982: set_actor 27@ stay_in_car_when_dead 0
                            09C9: disembark_actor 27@ from_car 0@ and_freeze_actor_position
                            else
                            
                            if $ONMISSION == false
                            then 0A1A: actor 27@ perform_walk_animation "CAR_crash_sit" IFP "camera" framedelta 30.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
                            else 0605: actor 27@ perform_animation "CAR_crash_sit" IFP "camera" framedelta 30.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
                            end
                            
                            end
                        
                        end
                    end
                end
                
                end
                
                //////////////////////////////////////////////////////////////////////////////////                                                                
                
                09F1: play_audio_at_actor PLAYER_ACTOR event 1130 
                
                2@ = actor.Health(PLAYER_ACTOR)
                if
                2@ < 1
                then
                05BE: AS_actor PLAYER_ACTOR die
                0982: set_actor PLAYER_ACTOR stay_in_car_when_dead 1
//                0A1A: actor PLAYER_ACTOR perform_walk_animation "ped" IFP "CAR_dead_LHS" framedelta 4.0 loopA 0 lockX 0 lockY 0 lockF 0 30000 ms
                0829: actor PLAYER_ACTOR perform_animation "CAR_dead_LHS" IFP "ped" rate 4.0 time 0 and_dies
                
                0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT76_GOT"                
                    
                if and
                0@ == 0
                $CHEATED_FLAG == 0
                    then
                    v$ACHIEVEMNT = "ACH76"
                    $ACHIEVEMENT_GOT_FLAG = 1                    
                    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT76_GOT"                    
                    end
                    
                0AF0: 0@ = get_int_from_ini_file "saves\ZACM.set" section "SCR" key "ACHIEVEMENT9_GOT"                            
                if and
                0@ == 0 
                $CHEATED_FLAG == 0
                    then
                    wait 2000
                    v$ACHIEVEMNT = "ACH9"
                    $ACHIEVEMENT_GOT_FLAG = 1                
                    0AF1: write_int 1 to_ini_file "saves\ZACM.set" section "SCR" key "ACHIEVEMENT9_GOT"                
                    end     
                            
                end
                
                end
            end
        end
    else
    
    repeat
    wait 0
    
    if
    $DRIVEBY_MODE == 1
        then
        if
        32@ > 100
            then
            break
            end
        end
    
    until 32@ > 250
    32@ = 0
//    wait 250
    
    if and
    0A32:  actor PLAYER_ACTOR on_turret_of_car
    056E:   car DRIVEBY_CAR defined
    not ALLOW_CAR_CRASH_DMG == 0
        then
        0@ = car.Model(DRIVEBY_CAR)
        
        if
        UNITED_MAP == 0
        then
        if or
        0@ == 15426
        0@ == 15410
        0@ == 15488
            then
            jump to @CAR_RANDOM_DRIVING_DAMAGE_SKIP
            end
        end
        
        if and
        not 0@ == #QUAD   
        not 0@ == 15558    
            then        
            jump to @CAR_DAMAGE_TURRET_JUMP
            end
        end    
    end
    
:CAR_RANDOM_DRIVING_DAMAGE_SKIP    

end

end_thread