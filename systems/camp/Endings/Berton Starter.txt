const
BERTON_HANDLE = $ARMY69_2[8]
end

:BERTONC
thread 'BERTONC'

create_thread @BOMB_EN

repeat
wait 0

if or
OOS_CURRENT_PROGRESS == 10 //OOS провалена, субъект заперт
OOS_CURRENT_PROGRESS == 8 //OOS на хорошую концовку, мы остались в штате, субъект в канаве
then
end_thread
end

if and
MAIN_QUEST1_COMPLETED_FLAG == 1
MAIN_QUEST8_COMPLETED_FLAG == 1
MAIN_QUEST13_COMPLETED_FLAG == 1
MAIN_QUEST6_COMPLETED_FLAG == 1
    then
    break
    end
    
if and
MAIN_QUEST1_COMPLETED_FLAG == 1
MAIN_QUEST8_COMPLETED_FLAG == 1
MAIN_QUEST6_COMPLETED_FLAG == 1
    then
    BERTON_STARTED_MEETED_FLAG = -1
    end
    
if
BERTON_STARTED_MEETED_FLAG == -2
    then
    BERTON_STARTED_MEETED_FLAG = 0
    0AF1: write_int 0 to_ini_file "data\survival.set" section "SRV" key "OTQ5GT"
    $GQST_SLCTD_RGIN = 8
    $GQEST_SLCTD_QST = 5
    0@ = marker.CreateIconAndSphere(108,-2160.3066, 2702.7483, 161.4145)
    jump to @BERTON_STARTER_BEGIN
    end        

until false

32@ = 0

while true
wait 0

if or
OOS_CURRENT_PROGRESS == 10 //OOS провалена, субъект заперт
OOS_CURRENT_PROGRESS == 8 //OOS на хорошую концовку, мы остались в штате, субъект в канаве
then
end_thread
end

if and
32@ > 5000
$ONMISSION == 0
$SAVESTATUS == 0
AFTER_ON_MISSION_TIMER == -1
not $PANEL_OPEN == 1
not $PANEL_OPEN2 == 1
not SURVIVAL_CUSTOM_WEAPON_NABOR == 8
$ENTERED_CAMP_NUMBER == -1
    then    
    
    ////////////////////////////////////////////////    
    
    if and
    MAIN_QUEST1_COMPLETED_FLAG == 1
    MAIN_QUEST8_COMPLETED_FLAG == 1
    MAIN_QUEST13_COMPLETED_FLAG == 1
    MAIN_QUEST6_COMPLETED_FLAG == 1
    not PLAYER_NEAR_CAMP_ZMB_SPWN == 1
        then
        
        $QUEST_TYPE = 999
        $ONMISSION = 1
        PLAYER_CANT_SPEAK_WITH_LEADER_FLAG = 1
        LADDER_ON_MARKER_FLAG = 1
        
        ///////////////////////////////////////////////////////////////////

        $RADIO_SOUND = Audiostream.Load("AUDIO\SOUNDS\Radio.mp3") 
        Audiostream.Loop($RADIO_SOUND, 1)  
        Audiostream.Volume($RADIO_SOUND) = 0.0
        Audiostream.PerformAction($RADIO_SOUND, PLAY)
        
        Audiostream.Loop($RADIO_ON_SOUND,0)
        Audiostream.PerformAction($RADIO_ON_SOUND, PLAY)
        Audiostream.Volume($RADIO_ON_SOUND) = $SOUNDS_VOLUME
        
        /////////////////////Плавное нарастание/////////////////////
        
        $MUSIC_FADE_NUMBER += 1
        if
        $MUSIC_FADE_NUMBER > 4
            then
            $MUSIC_FADE_NUMBER = 0 
            end    
        
        0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $RADIO_SOUND // (int) 
        0086: $MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = $SOUNDS_VOLUME // (float) 
        
        $MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 500
        $MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 1  
        $MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
        create_thread @MUS_FAD
        
        wait 1 
        
        ////////////////////////////////////////////////////////
        
        v$ST_DIALOG[0] = "END1"
        v$ST_DIALOG[1] = "END2"
        v$ST_DIALOG[2] = "END3"
        v$ST_DIALOG[3] = "END4"
        v$ST_DIALOG[4] = "END5"
        $DIALOG_COUNT = 4
        $DIALOG_NOT_SKIP = 1                 
        create_thread @DG_SYS2
        
        text.ClearAll()
        
        repeat
        wait 0                        
        
        if or
        $SAVESTATUS_TRUE == 1
        $SAVESTATUS == 1
            then
            break
            end
        
        until $DIALOG_END == 1
        
        $QUEST_TYPE = -1
        $NOTSPAWN_ETC = 0
        $SURVIVORS_DONT_SPAWN_FLAG = 0
        PLAYER_CANT_SPEAK_WITH_LEADER_FLAG = 0
        LADDER_ON_MARKER_FLAG = 0
        
        /////////////////////Плавное затухание/////////////////////
        
        $MUSIC_FADE_NUMBER += 1
        if
        $MUSIC_FADE_NUMBER > 4
            then
            $MUSIC_FADE_NUMBER = 0 
            end    
        
        0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $RADIO_SOUND // (int)
        $MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = 0.0
        
        $MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 150
        $MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 0  
        $MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
        create_thread @MUS_FAD
        
        wait 1
        
        Audiostream.Loop($RADIO_OFF_SOUND,0)
        Audiostream.PerformAction($RADIO_OFF_SOUND, PLAY)
        Audiostream.Volume($RADIO_OFF_SOUND) = $SOUNDS_VOLUME
        
        0@ = marker.CreateIconAndSphere(33,-2160.3066, 2702.7483, 161.4145)
        $ONMISSION = 0
        
        v$ACHIEVEMNT = "OTQ5B"
        $GQUEST_GOT_FLAG = 1                             
        0AF1: write_int 0 to_ini_file "data\survival.set" section "SRV" key "OTQ5GT"
        $GQST_SLCTD_RGIN = 8
        $GQEST_SLCTD_QST = 5
        BERTON_STARTED_MEETED_FLAG = -1
        
        $HORDE_OFF_TIMER_COUNTER += 999999999
        
        jump to @BERTON_STARTER_BEGIN
        end
    
    end
    
    ///////////////////////////////////////////////

end

end_thread

/////////////////////////////////////////////////

:BERTON_STARTER_BEGIN

while true
wait 0

if or
OOS_CURRENT_PROGRESS == 10 //OOS провалена, субъект заперт
OOS_CURRENT_PROGRESS == 8 //OOS на хорошую концовку, мы остались в штате, субъект в канаве
then

if
075C:   marker 0@ enabled
then
marker.Disable(0@)
end

0AF0: 1@ = get_int_from_ini_file "data\survival.set" section "SRV" key "OTQ5GT"
if
1@ == 0
then
0AF1: write_int 3 to_ini_file "data\survival.set" section "SRV" key "OTQ5GT"
end

0AF0: 1@ = get_int_from_ini_file "data\survival.set" section "SRV" key "OTQ6GT"
if
1@ == 0
then
0AF1: write_int 3 to_ini_file "data\survival.set" section "SRV" key "OTQ6GT"
end

0AF0: 1@ = get_int_from_ini_file "data\survival.set" section "SRV" key "OTQ7GT"
if
1@ == 0
then
0AF1: write_int 3 to_ini_file "data\survival.set" section "SRV" key "OTQ7GT"
end

0AF0: 1@ = get_int_from_ini_file "data\survival.set" section "SRV" key "OTQ8GT"
if
1@ == 0
then
0AF1: write_int 3 to_ini_file "data\survival.set" section "SRV" key "OTQ8GT"
end
    
end_thread
end

if and
875C: not  marker 0@ enabled
BERTON_STARTED_MEETED_FLAG < 3
then
0@ = marker.CreateIconAndSphere(33,-2160.3066, 2702.7483, 161.4145)
end

if and
0EEA: locate_char_distance_to_coordinates PLAYER_ACTOR pos 213.717 1879.4949 13.1406 radius 3.0
02A0:   actor PLAYER_ACTOR stopped
044B:   actor PLAYER_ACTOR on_foot
BERTON_STARTED_MEETED_FLAG >= 2
$ZONE69_CAPTURED == 1
    then
    
    marker.Disable(0@)
    
    player.CanMove(PLAYER_CHAR,false)
    $HUD_REMOVE = 1
    
    fade 0 500
    wait 500
    
    $SAVESTATUS = 1
    wait 1000
    
    start_mission 98
    end_thread    
    end

if and
0EEA: locate_char_distance_to_coordinates PLAYER_ACTOR pos -2160.3066 2702.7483 161.4145 radius 3.0
02A0:   actor PLAYER_ACTOR stopped
044B:   actor PLAYER_ACTOR on_foot
BERTON_STARTED_MEETED_FLAG < 2
    then
    
    $QUEST_TYPE = 666
    
    marker.Disable(0@)
    
    player.CanMove(PLAYER_CHAR,false)
    $HUD_REMOVE = 1
    
    fade 0 500
    wait 500
    
    $SAVESTATUS = 1
    wait 1000
    
    actor.PutAtNoOffset(PLAYER_ACTOR,-2161.5986, 2705.8538, 161.8002)
    actor.Angle(PLAYER_ACTOR) = 341.0957
    
    cinema.Widescreen(1)
    BLACK_POLOSKI_ON = 1
    01B9: set_actor PLAYER_ACTOR armed_weapon_to 0
    
    ////////////////////////////////////////////////////////

    MAIN_QUEST_MUSIC_FLAG = 1
    if or
    BERTON_STARTED_MEETED_FLAG == 0
    BERTON_STARTED_MEETED_FLAG == -1
    then
    $MUSIC_ASSHOLE = Audiostream.Load("AUDIO\SOUNDS\QUESTS\BURTON\A1.wav")
    Audiostream.PerformAction($MUSIC_ASSHOLE, PLAY)
    Audiostream.Loop($MUSIC_ASSHOLE, 1)
    Audiostream.Volume($MUSIC_ASSHOLE) = 0.0
    0AF1: write_int 1 to_ini_file "data\survival.set" section "SRV" key "OTQ5GT"
    $GQST_SLCTD_RGIN = 8
    $GQEST_SLCTD_QST = 5
    else
    $MUSIC_ASSHOLE = Audiostream.Load("AUDIO\SOUNDS\QUESTS\BURTON\A2.wav")
    Audiostream.PerformAction($MUSIC_ASSHOLE, PLAY)
    Audiostream.Loop($MUSIC_ASSHOLE, 1)
    Audiostream.Volume($MUSIC_ASSHOLE) = 0.0
    end
    
    /////////////////////Плавное нарастание/////////////////////
    
    $MUSIC_FADE_NUMBER += 1
    if
    $MUSIC_FADE_NUMBER > 4
        then
        $MUSIC_FADE_NUMBER = 0 
        end    
    
    0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $MUSIC_ASSHOLE // (int) 
    0086: $MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = $MUSIC_VOLUME // (float) 
    
    $MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 1000
    $MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 1  
    $MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
    create_thread @MUS_FAD
    
    wait 1
    
    wait 1000 
    
    ////////////////////////////////////////////////////////
    
    :CAR_CREATE_BURTON1
    if
    model.Available(#PCJ600)
    then
    
    20@ = Car.Create(#PCJ600, 0.0,0.0,0.0)
    
    if
    model.Available(#PCJ600)
    then
    //model.Destroy(#PCJ600)
    end
    
    else
    model.Load(#PCJ600)
    mload
    wait 0
    jump to @CAR_CREATE_BURTON1
    end
    
    repeat
    wait 0
    07C0: load_path 59
    until 07C1:   path 59 available    
    05EB: assign_car 20@ to_path 59
    
    /////////////////////////////////////////
    
    while not model.Available(14731)
    wait 0
    model.Load(14731)
    mload
    end
        
    BERTON_HANDLE = actor.CreateAsDriver(26,14731,20@)
    08C6: set_actor BERTON_HANDLE stay_on_bike 1    
    actor.SetWalkStyle(BERTON_HANDLE,"GANG2")
    0446: set_actor BERTON_HANDLE dismemberment_possible 0
    060B: set_actor BERTON_HANDLE decision_maker_to $DEC_PEOPLE
    actor.Health(BERTON_HANDLE) = 1000 
    actor.WeaponAccuracy(BERTON_HANDLE) = 85    
    create_thread @PED_BKP BERTON_HANDLE 371
    
    0936: set_camera -2166.494385 2697.793457 164.540421 position_to -2161.301514 2701.302979 163.158554 time 18000 smooth_transition 1
    0920: point_camera -2165.757324 2698.466309 164.476746 transverse_to -2161.189209 2702.278320 162.968750 time 18000 smooth_transition 1
    
    05BF: AS_actor PLAYER_ACTOR look_at_actor BERTON_HANDLE 900000 ms
    
    fade 1 500
    wait 500
    
    repeat
    wait 0
    
    02E3: 0@ = car 20@ speed    
    if and
    060E:   car 20@ assigned_to_path
    0@ < 1.0
        then
        05EC: release_car 20@ from_path
        break
        end
    
    until false
    
    fade 0 100
    wait 100
    
    0936: set_camera -2166.494385 2697.793457 164.540421 position_to -2161.301514 2701.302979 163.158554 time 1 smooth_transition 1
    0920: point_camera -2165.757324 2698.466309 164.476746 transverse_to -2161.189209 2702.278320 162.968750 time 1 smooth_transition 1
    
    wait 1
    
    0936: set_camera -2151.517822 2719.622070 161.171570 position_to -2155.670654 2714.832275 161.374130 time 180000 smooth_transition 1
    0920: point_camera -2152.179932 2718.873291 161.203384 transverse_to -2156.277832 2714.038330 161.405945 time 180000 smooth_transition 1
    
    0615: define_AS_pack_begin 0@
    05CD: AS_actor -1 exit_car 20@
    05D9: AS_actor -1 run_to_actor PLAYER_ACTOR timelimit 5000 stop_within_radius 3.0
    0616: define_AS_pack_end 0@
    0618: assign_actor BERTON_HANDLE to_AS_pack 0@
    061B: remove_references_to_AS_pack 0@
    
    fade 1 100
    wait 100
    
    ///////////////////////////////////////////////////////////
    
    32@ = 0
    
    repeat
    wait 0
    
    if and
    32@ > 5000
    02A0:   actor BERTON_HANDLE stopped
        then
        break
        end
    
    if and
    02A0:   actor BERTON_HANDLE stopped
    00F2:   actor BERTON_HANDLE near_actor PLAYER_ACTOR radius 3.0 3.0 sphere 0
        then
        break
        end
    
    until false
    
    /////////////////////////////////////////////////////////// 
    
    if
    BERTON_STARTED_MEETED_FLAG < 1
    then
    
    0084: $DIALOG_ACTOR[0] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[1] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[2] = PLAYER_ACTOR // (int)
    0084: $DIALOG_ACTOR[3] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[4] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[5] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[6] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[7] = PLAYER_ACTOR // (int)
    0084: $DIALOG_ACTOR[8] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[9] = PLAYER_ACTOR // (int)
    0084: $DIALOG_ACTOR[10] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[11] = PLAYER_ACTOR // (int)
    0084: $DIALOG_ACTOR[12] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[13] = BERTON_HANDLE // (int)
    
    if
    not OOS_CURRENT_PROGRESS == 9
    then
    v$ST_DIALOG[0] = "END6"
    v$ST_DIALOG[1] = "END7"
    v$ST_DIALOG[2] = "END8"
    v$ST_DIALOG[3] = "END9"
    v$ST_DIALOG[4] = "END10"
    v$ST_DIALOG[5] = "END11"
    v$ST_DIALOG[6] = "END12"
    v$ST_DIALOG[7] = "END13"
    v$ST_DIALOG[8] = "END14"
    v$ST_DIALOG[9] = "END15"
    v$ST_DIALOG[10] = "END16"
    v$ST_DIALOG[11] = "END17"
    v$ST_DIALOG[12] = "END17Q"
    v$ST_DIALOG[13] = "END18Q" 
    else
    v$ST_DIALOG[0] = "EOD6"
    v$ST_DIALOG[1] = "EOD7"
    v$ST_DIALOG[2] = "END8"
    v$ST_DIALOG[3] = "EOD9"
    v$ST_DIALOG[4] = "EOD10"
    v$ST_DIALOG[5] = "EOD11"
    v$ST_DIALOG[6] = "EOD12"
    v$ST_DIALOG[7] = "END13"
    v$ST_DIALOG[8] = "EOD14"
    v$ST_DIALOG[9] = "END15"
    v$ST_DIALOG[10] = "EOD16"
    v$ST_DIALOG[11] = "END17"
    v$ST_DIALOG[12] = "EOD17Q"
    v$ST_DIALOG[13] = "EOD18Q" 
    end
        
    $DIALOG_COUNT = 13
    create_thread @DG_SYS
    
    repeat
    wait 0
    until $DIALOG_END == 1
        
    end
    
    ///////////////////////////////////////////////////////////        
    
    if
    NESTS_CLEARED_TOTAL < 17
        then
        
        0084: $DIALOG_ACTOR[0] = BERTON_HANDLE // (int)
        0084: $DIALOG_ACTOR[1] = BERTON_HANDLE // (int)
        0084: $DIALOG_ACTOR[2] = BERTON_HANDLE // (int)
        if
        not OOS_CURRENT_PROGRESS == 9
        then
        v$ST_DIALOG[0] = "END18"
        v$ST_DIALOG[1] = "END19"
        v$ST_DIALOG[2] = "END20"
        else
        v$ST_DIALOG[0] = "EOD18"
        v$ST_DIALOG[1] = "EOD19"
        v$ST_DIALOG[2] = "EOD20"
        end 
        $DIALOG_COUNT = 2
        create_thread @DG_SYS
        
        repeat
        wait 0
        until $DIALOG_END == 1
        
        BERTON_STARTED_MEETED_FLAG = 1
        
        :BERTON_DRIVE_AWAY
        
        05CB: AS_actor BERTON_HANDLE enter_car 20@ as_driver 10000 ms
        
        repeat
        wait 0
        until actor.InCar(BERTON_HANDLE,20@)
        
        0873: release_path 59
        
        repeat
        wait 0
        07C0: load_path 60
        until 07C1:   path 60 available    
        05EB: assign_car 20@ to_path 60
        
        wait 2000
        
        fade 0 500
        wait 500
        
        actor.DestroyInstantly(BERTON_HANDLE)
        car.Destroy(20@)
        
        0873: release_path 60
        
        0936: set_camera -2166.494385 2697.793457 164.540421 position_to -2161.301514 2701.302979 163.158554 time 1 smooth_transition 1
        0920: point_camera -2165.757324 2698.466309 164.476746 transverse_to -2161.189209 2702.278320 162.968750 time 1 smooth_transition 1
        
        wait 1
        
        /////////////////////Плавное затухание/////////////////////

        $MUSIC_FADE_NUMBER += 1
        if
        $MUSIC_FADE_NUMBER > 4
            then
            $MUSIC_FADE_NUMBER = 0 
            end    
            
        0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $MUSIC_ASSHOLE // (int)
        $MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = 0.0
            
        $MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 1000
        $MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 0  
        $MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
        create_thread @MUS_FAD
            
        wait 1
            
        while $MUSIC_FADE_ENDED[$MUSIC_FADE_NUMBER] == 0
        wait 0
        end
        
        MAIN_QUEST_MUSIC_FLAG = 0
        
        cinema.Widescreen(0)
        BLACK_POLOSKI_ON = 0
        player.CanMove(PLAYER_CHAR,true)
        camera.Restore_WithJumpCut()
        
        $QUEST_TYPE = -1
        $NOTSPAWN_ETC = 0
        $SURVIVORS_DONT_SPAWN_FLAG = 0
        $SAVESTATUS = 0
        
        fade 1 500
        $HUD_REMOVE = 0
        wait 500
        
        if
        BERTON_STARTED_MEETED_FLAG == 1
        then
        
        v$ACHIEVEMNT = "OTQ6B"
        $GQUEST_GOT_FLAG = 1                             
        0AF1: write_int 0 to_ini_file "data\survival.set" section "SRV" key "OTQ6GT"
        $GQST_SLCTD_RGIN = 8
        $GQEST_SLCTD_QST = 6
        
        repeat
        wait 0
        until NESTS_CLEARED_TOTAL >= 17
        
        v$ACHIEVEMNT = "OTQ6C"
        $GQUEST_GOT_FLAG = 1
        0AF1: write_int 1 to_ini_file "data\survival.set" section "SRV" key "OTQ6GT"
        $GQST_SLCTD_RGIN = 8
        $GQEST_SLCTD_QST = 6
        
        end
        
        if
        BERTON_STARTED_MEETED_FLAG == 2
        then
        v$ACHIEVEMNT = "OTQ7B"
        $GQUEST_GOT_FLAG = 1                             
        0AF1: write_int 0 to_ini_file "data\survival.set" section "SRV" key "OTQ7GT"
        $GQST_SLCTD_RGIN = 8
        $GQEST_SLCTD_QST = 7
        end
        
        if
        BERTON_STARTED_MEETED_FLAG < 2
        then
        0@ = marker.CreateIconAndSphere(108,-2160.3066, 2702.7483, 161.4145)
        else
        0@ = marker.CreateIconAndSphere(108,213.717, 1879.4949, 13.1406)
        jump to @BERTON_STARTER_BEGIN
        end
        
        jump to @BERTON_STARTER_BEGIN
        
        end
        
    ///////////////////////////////////////////////////////////    
        
    if
    not BERTON_STARTED_MEETED_FLAG > 1
    then    
        
    0084: $DIALOG_ACTOR[0] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[1] = BERTON_HANDLE // (int)
    0084: $DIALOG_ACTOR[2] = PLAYER_ACTOR // (int)
    
    if
    not OOS_CURRENT_PROGRESS == 9
    then
    v$ST_DIALOG[0] = "END21"
    v$ST_DIALOG[1] = "END22"
    v$ST_DIALOG[2] = "END23"
    else
    v$ST_DIALOG[0] = "EOD21"
    v$ST_DIALOG[1] = "EOD22"
    v$ST_DIALOG[2] = "END23"
    end 
    $DIALOG_COUNT = 2
    create_thread @DG_SYS
        
    repeat
    wait 0
    until $DIALOG_END == 1
    
    end
    
    ///////////////////////////////////////////////////////////    
        
    if
    $ZONE69_CAPTURED == 0
        then
        
        0084: $DIALOG_ACTOR[0] = BERTON_HANDLE // (int)
        0084: $DIALOG_ACTOR[1] = BERTON_HANDLE // (int)
        0084: $DIALOG_ACTOR[2] = PLAYER_ACTOR // (int)
        0084: $DIALOG_ACTOR[3] = BERTON_HANDLE // (int)
        if
        not OOS_CURRENT_PROGRESS == 9
        then
        v$ST_DIALOG[0] = "END24"
        v$ST_DIALOG[1] = "END25"
        v$ST_DIALOG[2] = "END26"
        v$ST_DIALOG[3] = "END27"
        $DIALOG_COUNT = 3
        else
        v$ST_DIALOG[0] = "EOD24"
        v$ST_DIALOG[1] = "EOD25"
        v$ST_DIALOG[2] = "EOD26"
        $DIALOG_COUNT = 2
        end         
        create_thread @DG_SYS
        
        repeat
        wait 0
        until $DIALOG_END == 1                
        
        BERTON_STARTED_MEETED_FLAG = 2                
        
        jump @BERTON_DRIVE_AWAY
        
        else
        
        0084: $DIALOG_ACTOR[0] = BERTON_HANDLE // (int)
        0084: $DIALOG_ACTOR[1] = BERTON_HANDLE // (int)
        0084: $DIALOG_ACTOR[2] = PLAYER_ACTOR // (int)
        0084: $DIALOG_ACTOR[3] = BERTON_HANDLE // (int)
        if
        not OOS_CURRENT_PROGRESS == 9
        then
        v$ST_DIALOG[0] = "END24Q"
        v$ST_DIALOG[1] = "END25"
        v$ST_DIALOG[2] = "END26"
        v$ST_DIALOG[3] = "END27"
        $DIALOG_COUNT = 3
        else
        v$ST_DIALOG[0] = "EOD24Q"
        v$ST_DIALOG[1] = "EOD25"
        v$ST_DIALOG[2] = "EOD26"
        $DIALOG_COUNT = 2
        end         
        create_thread @DG_SYS
        
        repeat
        wait 0
        until $DIALOG_END == 1
        
        if
        not BERTON_STARTED_MEETED_FLAG == 3
        then
        v$ACHIEVEMNT = "OTQ8B"
        $GQUEST_GOT_FLAG = 1                             
        0AF1: write_int 0 to_ini_file "data\survival.set" section "SRV" key "OTQ8GT"
        $GQST_SLCTD_RGIN = 8
        $GQEST_SLCTD_QST = 8
        end
        
        BERTON_STARTED_MEETED_FLAG = 3
        
        jump @BERTON_DRIVE_AWAY
        
        end
        
    ///////////////////////////////////////////////////////////        
                    
    end
    
end

end_thread