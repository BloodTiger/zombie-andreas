//-------------Mission 6---------------
// Название: Прогулка по улице

:MISS6
thread 'MISS6'
gosub @KOD_MISSII6 
if 
wasted_or_busted 
jf @PROPUSK_KODA6 
gosub @KOD_PROVALA6 

:PROPUSK_KODA6
gosub @UDALENIE_KODA6

HARDCORE_DANGER_FLAG_SCRIPTED = 0
if
GAME_OVER_FLAG == 1
then
HARDCORE_DANGER_ANTIFLAG_SCRIPTED = 1
end

if
GAME_OVER_FLAG == 1
then
repeat
wait 0
until BLACK_POLOSKI_ON == 1
end

{$INCLUDE checkpoints_write.txt}
 
end_thread 

:KOD_MISSII6
increment_mission_attempts

cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
0A48: enable_menu_access_in_widescreen_mode 0  
$SOUND_OFF_1 = 1
0629: change_integer_stat 258 to 6

MAIN_QUEST_MUSIC_FLAG = 1
INT_ZOMBIE_OFF_FADING_AND_SHIT_FLAG = 1
MAIN_QUEST_TEAMMATES_REMOVE_FLAG = 1

create_thread @MCS_ASS_CREATE 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 2

wait 1000

while true
wait 0
$END_FADING_MUSIC_FAST = 1

if and
$MUSIC_FADE_ENDED[0] == 1
$MUSIC_FADE_ENDED[1] == 1
$MUSIC_FADE_ENDED[2] == 1
$MUSIC_FADE_ENDED[3] == 1
$MUSIC_FADE_ENDED[4] == 1
then
$END_FADING_MUSIC_FAST = 0
break
end

end

$ONMISSION = 1
$QUEST_TYPE = 5
CAR_SEARCH_DO_NOT_GIVE = 1
fade 0 0

wait 500

01B9: set_actor PLAYER_ACTOR armed_weapon_to 0 

$NOTSPAWN = 1
$SAVESTATUS = 1
$INT_ZMB_NOT = 1

$SPECIAL_INFECTED_NOT_SPAWN_FLAG = 1

$NOTSPAWN_ETC = 0 

$HEAL += 3

ENEX_OFF = 1

DONT_DO_BURGLARY = 1
select_interior 7 
0860: link_actor PLAYER_ACTOR to_interior 7
04F9: set_sky_color 1 fade 0
0776: create_objects_in_object_group "mike2" 

PLAYER_DONT_HAVE_BACKPACK = 1

02A3: enable_widescreen 1
BLACK_POLOSKI_ON = 1

$HUD_REMOVE = 1

while not model.Available(#OMYST)
wait 0
model.Load(#OMYST)
mload
end  

09C7: change_player PLAYER_CHAR model_to #OMYST

if
MIKE_SHIZA_FLAG == 1
then

32@ = 0
 repeat
wait 0
fade 0 0 
$SAVESTATUS = 1
03F0: enable_text_draw 1
038E: draw_box_position 320.0 224.0 size 640.0 448.0 RGBA 0 0 0 255
 until 32@ > 3100
 
fade 0 0 

fade 0 0
wait 1

///////////////////////////////////////////////

if
MIKE_CLASSIC_STORY_VOICES_ON == 1
then

{$INCLUDE noise2_on.txt}

VOICE_SKIP_LONG_SET = 0          

for $ACHIV_NUMBER = 195 to 199

create_thread @VOI_SHO
          
repeat
wait 0

if
VOICE_SKIP_LONG_SET > VOICE_SKIP_LONG_SKIP_SET
    then
    break
    end

until $ACHIV_SHOW_ENDED == 1

end

{$INCLUDE noise2_off.txt} 

end

///////////////////////////////////////////////

end  

if
CLASSIC_MUSIC == 0
then
$MUSIC_MISSION = Audiostream.Load(MUSIC_7A)
else
$MUSIC_MISSION = Audiostream.Load(CLASSIC_MUSIC_7A)
end

Audiostream.PerformAction($MUSIC_MISSION, PLAY)
Audiostream.Loop($MUSIC_MISSION, 1)
Audiostream.Volume($MUSIC_MISSION) = 0.0  

/////////////////////Плавное нарастание/////////////////////

$MUSIC_FADE_NUMBER += 1
if
$MUSIC_FADE_NUMBER > 4
    then
    $MUSIC_FADE_NUMBER = 0 
    end    

0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $MUSIC_MISSION // (int) 
0086: $MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = $MUSIC_VOLUME // (float) 

$MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 1000
$MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 1  
$MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
create_thread @MUS_FAD

wait 1

//////////////////////////////////////////////////////////  
        
Player.CanMove(PLAYER_CHAR) = False

if
MIKE_SHIZA_FLAG == 1
then

//////////////////////////////////////////////////////////

 repeat
wait 0
$SAVESTATUS = 1
03F0: enable_text_draw 1
038E: draw_box_position 320.0 224.0 size 640.0 448.0 RGBA 0 0 0 255
 until 32@ > 3000

fade 0 0
wait 1           

0A0E: disable_text_with_style 1

0391: release_textures
0390: load_txd_dictionary 'miss4' 
wait 1
038F: load_texture "chapter3" as 1 // Load dictionary with 0390 first 
wait 1

1@ = 0

 repeat 
wait 0
1@ += 5
03F0: enable_text_draw 1
038D: draw_texture 1 position 320.0 220.0 size 600.0 400.0 RGBA 255 255 255 1@
03E3: set_texture_to_be_drawn_antialiased 1 
 until 1@ == 255

32@ = 0 

 repeat 
wait 0
03F0: enable_text_draw 1
038D: draw_texture 1 position 320.0 220.0 size 600.0 400.0 RGBA 255 255 255 1@
03E3: set_texture_to_be_drawn_antialiased 1 

if
32@ > 3500
    then
    break
    end
    
 until 00E1: key_pressed 0 17

 repeat
wait 0
1@ -= 5
03F0: enable_text_draw 1
038D: draw_texture 1 position 320.0 220.0 size 600.0 400.0 RGBA 255 255 255 1@
03E3: set_texture_to_be_drawn_antialiased 1 
 until 1@ == 0
 
wait 1
03F0: enable_text_draw 0
wait 1
0391: release_textures 

0A0E: disable_text_with_style 0 

0A0E: disable_text_with_style 1

0391: release_textures
0390: load_txd_dictionary 'miss1' 
wait 1
038F: load_texture "mission6" as 1 // Load dictionary with 0390 first
wait 1

1@ = 0

 repeat 
wait 0
1@ += 5
03F0: enable_text_draw 1
038D: draw_texture 1 position 320.0 220.0 size 600.0 400.0 RGBA 255 255 255 1@
03E3: set_texture_to_be_drawn_antialiased 1 
 until 1@ == 255

32@ = 0
$SPEED_08_FLAG = 1 

 repeat 
wait 0
03F0: enable_text_draw 1
038D: draw_texture 1 position 320.0 220.0 size 600.0 400.0 RGBA 255 255 255 1@
03E3: set_texture_to_be_drawn_antialiased 1 
 until 00E1: key_pressed 0 17
 
$SPEED_08_FLAG = 0
015D: set_gamespeed 1.0 

 repeat
wait 0
1@ -= 5
03F0: enable_text_draw 1
038D: draw_texture 1 position 320.0 220.0 size 600.0 400.0 RGBA 255 255 255 1@
03E3: set_texture_to_be_drawn_antialiased 1 
 until 1@ == 0

wait 1
03F0: enable_text_draw 0
wait 1
0391: release_textures 

0A0E: disable_text_with_style 0

end

while not model.Available(#HMYRI)
wait 0
model.Load(#HMYRI)
mload
end

$SURVIVER[0] = Actor.Create(26, #HMYRI, 238.9281, 1018.2363, 1084.0273)
actor.Angle($SURVIVER[0]) = 10.6984
$SURVIVOR_MELEE_ID[0] = -1

Actor.Angle($SURVIVER[0]) = 2.747

actor.SetWalkStyle($SURVIVER[0],"SURV1")
0446: set_actor $SURVIVER[0] dismemberment_possible 0
060B: set_actor $SURVIVER[0] decision_maker_to $DEC_PEOPLE
actor.Health($SURVIVER[0]) = $FRIEND_HEALTH
actor.WeaponAccuracy($SURVIVER[0]) = 100
07DD: set_actor $SURVIVER[0] attack_rate 999 // previously known as temper_to      

actor.LockInCurrentPosition(PLAYER_ACTOR) = false

if
MIKE_SHIZA_FLAG == 1
then
Actor.PutAtNoOffset(PLAYER_ACTOR, 230.4269, 1048.209, 1084.0131)
Actor.Angle(PLAYER_ACTOR) = 181.0618
0615: define_AS_pack_begin 9@ 
05D3: AS_actor -1 goto_point 230.5683 1038.1494 1084.0128 mode 4 time 90000 ms // versionA  
0616: define_AS_pack_end 9@ 
0618: assign_actor PLAYER_ACTOR to_AS_pack 9@ 
061B: remove_references_to_AS_pack 9@
end

0936: set_camera 229.900070 1038.973389 1083.391602 position_to 229.937149 1039.337524 1083.403809 time 9000 smooth_transition 1
0920: point_camera 230.078613 1039.956543 1083.432251 transverse_to 230.224945 1040.294556 1083.439941 time 9000 smooth_transition 1
        
03E6: remove_text_box

wait 500

0A48: enable_menu_access_in_widescreen_mode 1  
$SOUND_OFF_1 = 0
wait 0
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1

if
MIKE_SHIZA_FLAG == 1
then

fade 1 2000
text.ShowH('MSN74',2000,1)

05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 999999 ms 
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 999999 ms

wait 2000
0707: start_scene_skip_to @MISSION6_SKIP

repeat
wait 0
until 02A0:   actor PLAYER_ACTOR stopped

0701: end_scene_skip

fade 0 1000
wait 1000
end

Actor.PutAtNoOffset(PLAYER_ACTOR, 238.5491, 1020.1424, 1084.0286)
Actor.Angle(PLAYER_ACTOR) = 189.0211

0A1D: AS_actor PLAYER_ACTOR rotate_to_and_look_at_actor $SURVIVER[0]
0A1D: AS_actor $SURVIVER[0] rotate_to_and_look_at_actor PLAYER_ACTOR

wait 1000            

0936: set_camera 240.311050 1027.216309 1084.651611 position_to 240.040909 1025.585571 1084.630005 time 70000 smooth_transition 1
0920: point_camera 240.111160 1026.249878 1084.490479 transverse_to 239.711456 1024.646973 1084.528076 time 70000 smooth_transition 1 

create_thread @REFR_3D
fade 1 3000 

if
MIKE_SHIZA_FLAG == 1
then
0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[5] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[7] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[8] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[9] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[10] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[11] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[12] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[13] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[14] = $SURVIVER[0] // (int)
v$ST_DIALOG[0] = "2NDG20"
v$ST_DIALOG[1] = "2NDG21"
v$ST_DIALOG[2] = "2NDG22"
v$ST_DIALOG[3] = "2NDG23"
v$ST_DIALOG[4] = "2NDG24"
v$ST_DIALOG[5] = "2NDG25"
v$ST_DIALOG[6] = "2NDG26"
v$ST_DIALOG[7] = "2NDG27"
v$ST_DIALOG[8] = "2NDG28"
v$ST_DIALOG[9] = "2NDG29"
v$ST_DIALOG[10] = "2NDG30"
v$ST_DIALOG[11] = "2NDG31"
v$ST_DIALOG[12] = "2NDG32" 
v$ST_DIALOG[13] = "2NDG33" 
v$ST_DIALOG[14] = "2NDG34" 
$DIALOG_COUNT = 14
create_thread @DG_SYS
else
0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[5] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[7] = $SURVIVER[0] // (int)
v$ST_DIALOG[0] = "2NDG20"
v$ST_DIALOG[1] = "2NDG21"
v$ST_DIALOG[2] = "2NDG22"
v$ST_DIALOG[3] = "2NDG23"
v$ST_DIALOG[4] = "2NDG24"
v$ST_DIALOG[5] = "2NDG25"
v$ST_DIALOG[6] = "2NDG26"
$DIALOG_COUNT = 6
create_thread @DG_SYS
end

repeat
wait 0
until $DIALOG_END == 1

$DIALOG_END = 1

:MISSION6_SKIP
0701: end_scene_skip

fade 0 1000
wait 1000

0936: set_camera 228.283 1033.828 1084.016 position_to 228.4718 1039.2811 1085.012 time 1 smooth_transition 1
0920: point_camera 230.5715 1037.527 1084.0133 transverse_to 230.5715 1037.527 1085.0133 time 1 smooth_transition 1 

0792: disembark_instantly_actor PLAYER_ACTOR

$SAVESTATUS = 1

00BE: text_clear_all

select_interior 0 
0860: link_actor PLAYER_ACTOR to_interior 0
04FA: reset_sky_colors_with_fade 0 

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0

Actor.PutAt(PLAYER_ACTOR, 1135.6245, -742.4295, 97.1622)
Actor.Angle(PLAYER_ACTOR) = 1.1358

camera.Restore_WithJumpCut()

04E4: refresh_game_renderer_at 1135.6245 -742.4295 
03CB: set_rendering_origin_at 1135.6245 -742.4295 97.1622
create_thread @REFR_3D

Actor.PutAt($SURVIVER[0], 1136.0311, -745.473, 97.1622)
Actor.Angle($SURVIVER[0]) = 2.6242
camera.Restore_WithJumpCut()

0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP

if
COOP_ACTIVE_FLAG == 1
then
0EA0: set_actor_second_player $SURVIVER[0] enable_camera 1 separate_cars 0
0084: COOP_ACTOR = $SURVIVER[0] // (int)

0781: get_weapon_with_ID 3 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo($SURVIVER[0], 3, 1)
else

0781: get_weapon_with_ID 3 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo($SURVIVER[0], 3, 1)

$SURVIVOR_MELEE_ID[0] = 3
$SURVIVOR_WEP_ID[0] = 25
create_thread @MEL_SRV $SURVIVER[0] 0
end 

actor.Health($SURVIVER[0]) = $FRIEND_HEALTH

create_thread @PED_BKP $SURVIVER[0] 371

0781: get_weapon_with_ID 25 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

Actor.GiveWeaponAndAmmo($SURVIVER[0], 25, 99999)

0781: get_weapon_with_ID 46 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

$SAVE_PREPARE_OFF = 0
PLAYER_DONT_HAVE_BACKPACK = 0
create_thread @PED_BKP PLAYER_ACTOR 371
$FP_BACKPACK_HAD = 1

if
MIKE_SHIZA_FLAG == 1
then
048F: actor PLAYER_ACTOR remove_weapons
end 

//////////////////////////////////////////////

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 2 weapon 1@ ammo 2@ model 3@
if
2@ < 1
then

0781: get_weapon_with_ID 3 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 3, 1)
else
//ass
end

//////////////////////////////////////////////

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 4 weapon 1@ ammo 2@ model 3@
if
2@ < 1
then

0781: get_weapon_with_ID 25 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

if
MIKE_SHIZA_FLAG == 1
then 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 25, 60)
else
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 25, 20)
end

else

if
MIKE_SHIZA_FLAG == 1
then
2@ += 60
else
2@ += 20
end

017B: set_actor PLAYER_ACTOR weapon 1@ ammo_to 2@
01B9: set_actor PLAYER_ACTOR armed_weapon_to 1@
end

//////////////////////////////////////////////

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 3 weapon 1@ ammo 2@ model 3@
if
2@ < 1
then

0781: get_weapon_with_ID 22 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

if
MIKE_SHIZA_FLAG == 1
then 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 22, 120)
else
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 22, 40)
end

else

if
MIKE_SHIZA_FLAG == 1
then
2@ += 120
else
2@ += 40
end

017B: set_actor PLAYER_ACTOR weapon 1@ ammo_to 2@
01B9: set_actor PLAYER_ACTOR armed_weapon_to 1@
end

//////////////////////////////////////////////

if
MIKE_SHIZA_FLAG == 1
then

:CAR_CREATE_MISSION6
if
model.Available(#FORTUNE)
then
             
MIKE_CAR = car.Create(#FORTUNE,1110.2242, -732.8909, 99.8528)

if
model.Available(#FORTUNE)
then
model.Destroy(#FORTUNE)
end

else
model.Load(#FORTUNE)
mload
wait 0
jump to @CAR_CREATE_MISSION6
end

car.Angle(MIKE_CAR) = 89.9781
car.Health(MIKE_CAR) = CAR_HEALTH_MAX
0A97: 11@ = car MIKE_CAR struct 
0229: set_car MIKE_CAR primary_color_to 17 secondary_color_to 17
11@ += 9
0A8D: 12@ = read_memory 11@ size 1 virtual_protect 0
08BD: set 12@ bit 2
0A8C: write_memory 11@ size 1 value 12@ virtual_protect 0 
/////////////////////
0A97: 11@ = car MIKE_CAR struct 
11@ += 4 
0A8D: 12@ = read_memory 11@ size 1 virtual_protect 0
08BD: set 12@ bit 1 
0A8C: write_memory 11@ size 1 value 12@ virtual_protect 0 
random_float $BENZIN 30.0 50.0
0A97: 22@ = car MIKE_CAR struct 
0A8E: 23@ = 22@ + 5 // int 
0A8C: write_memory 23@ size 4 value $BENZIN virtual_protect 0 
08C3: clear 12@ bit 1
08C3: clear 12@ bit 2 
0A97: 11@ = car MIKE_CAR struct 
11@ += 18
0A8D: 12@ = read_memory 11@ size 1 virtual_protect 0
08BD: set 12@ bit 2
0A8C: write_memory 11@ size 1 value 12@ virtual_protect 0 
08C3: clear 12@ bit 2   

end

////////////////////////////////////////////// 

$SAVESTATUS = 0
$NOTSPAWN_ETC = 0
$ZOMBIES_COUNT = 20

actor.Health($SURVIVER[0]) = $FRIEND_HEALTH

wait 2000

actor.Health($SURVIVER[0]) = $FRIEND_HEALTH

Player.CanMove(PLAYER_CHAR) = True

create_thread @REFR_3D
fade 1 2000 
$HUD_REMOVE = 0 
 
wait 2000 

v$OBJECTIVE = "MSN43"   

008B: 301@ = CAR_OBISKENO // (int)
0084: $CAR_OBISKENO2 = CAR_OBISKENO // (int)
$CAR_OBISKENO2 += 20
0084: MISSION_CAR2 = CAR_OBISKENO // (int)
0066: MISSION_CAR2 -= 301@  // (int)

008B: 300@ = $ZOMBIE_KILLED // (int)
300@ += 20

04F7: vagina_text MISSION_CAR2 type 0 line 1 GXT 'GAME525' // global_variable

32@ = 0
200@ = 0

 repeat
wait 0   
$SPECIAL_INFECTED_NOT_SPAWN_FLAG = 1
$ZOMBIES_COUNT = 20

0084: MISSION_CAR2 = CAR_OBISKENO // (int)
0066: MISSION_CAR2 -= 301@  // (int)

if and
002E:   $ZOMBIE_KILLED >= 300@  // (int)
200@ == 0
then
200@ = 1
v$ST_DIALOG[0] = "NDG01"
v$ST_DIALOG[1] = "NDG02"
v$ST_DIALOG[2] = "NDG03"
v$ST_DIALOG[3] = "NDG04"
v$ST_DIALOG[4] = "NDG05"
v$ST_DIALOG[5] = "NDG06"
v$ST_DIALOG[6] = "NDG07"
v$ST_DIALOG[7] = "NDG08"
$DIALOG_COUNT = 7
$DIALOG_NOT_SKIP = 1
create_thread @DG_SYS2 
end

if
actor.Dead(PLAYER_ACTOR)
    then
    jump to @KOD_PROVALA6
    end     

if 
   not Actor.Dead($SURVIVER[0])
jf @MISSION6_1902_DGO 

if
80F2: not  actor $SURVIVER[0] near_actor PLAYER_ACTOR radius 60.0 60.0 0  
    then 
       
    marker.Disable(MISSION_CHECKPOINT)
    $DIALOG_PAUSE = 1
    
    MISSION_MARKER = marker.CreateAboveActor($SURVIVER[0])
    07E0: set_marker MISSION_MARKER type_to 1
    
    v$OBJECTIVE = "MSN46"
    
    while 80F2: not  actor $SURVIVER[0] near_actor PLAYER_ACTOR radius 60.0 60.0 0  
    wait 0
    
    0084: MISSION_CAR2 = CAR_OBISKENO // (int)
    0066: MISSION_CAR2 -= 301@  // (int)  
    
    if
    actor.Dead(PLAYER_ACTOR)
        then
        jump to @KOD_PROVALA6
        end    
        
    if 
    not actor.Dead($SURVIVER[0]) 
    jf @MISSION6_1902_DGO      
        
    end
    
    marker.Disable(MISSION_MARKER) 
                 
    v$OBJECTIVE = "MSN43"     
    $DIALOG_PAUSE = 0 
       
    end
    
//lootskip
if and
$CHEATS_ON == 1
$DEBUG_MENU_ON == 1
0ADC:   test_cheat "SKIP"
    then
    break
    end         

 until 002C: CAR_OBISKENO >= $CAR_OBISKENO2  // (int)

0151: remove_status_text MISSION_CAR2
00BC: show_text_highpriority GXT 'MSN92' time 3000 flag 1 
wait 3000 

018A: MISSION_CHECKPOINT = create_checkpoint_at 1105.1465 -732.4737 101.0109

v$OBJECTIVE = "MSN52"

repeat
wait 0 
$SPECIAL_INFECTED_NOT_SPAWN_FLAG = 1
$ZOMBIES_COUNT = 20

if
actor.Dead(PLAYER_ACTOR)
    then
    jump to @KOD_PROVALA6
    end  

if 
   not Actor.Dead($SURVIVER[0])
jf @MISSION6_1902_DGO

if
80F2: not  actor $SURVIVER[0] near_actor PLAYER_ACTOR radius 60.0 60.0 0  
    then 
       
    marker.Disable(MISSION_CHECKPOINT)
    $DIALOG_PAUSE = 1
    
    MISSION_MARKER = marker.CreateAboveActor($SURVIVER[0])
    07E0: set_marker MISSION_MARKER type_to 1
    
    v$OBJECTIVE = "MSN46"
    
    while 80F2: not  actor $SURVIVER[0] near_actor PLAYER_ACTOR radius 60.0 60.0 0  
    wait 0   
    
    if
    actor.Dead(PLAYER_ACTOR)
        then
        jump to @KOD_PROVALA6
        end       
        
    if 
    not actor.Dead($SURVIVER[0]) 
    jf @MISSION6_1902_DGO      
        
    end
    
    marker.Disable(MISSION_MARKER) 
           
    v$OBJECTIVE = "MSN52" 
    $DIALOG_PAUSE = 0
    
    018A: MISSION_CHECKPOINT = create_checkpoint_at 1105.1465 -732.4737 101.0109
       
    end 

until 00EC:   actor PLAYER_ACTOR sphere 1 near_point 1105.1465 -732.4737 radius 2.0 2.0 

$END_DIALOG_INST = 1

Marker.Disable(MISSION_CHECKPOINT)

player.CanMove(PLAYER_CHAR) = false
0221: set_player PLAYER_CHAR apply_brakes_to_car 1

v$OBJECTIVE = "DUMMY" 

fade 0 1000 
$HUD_REMOVE = 1
wait 1000

0221: set_player PLAYER_CHAR apply_brakes_to_car 0   

$SAVESTATUS = 1 

0555: remove_weapon 46 from_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1 
06AB: set_actor $SURVIVER[0] all_weapons_hidden 1 

01B9: set_actor PLAYER_ACTOR armed_weapon_to 0 

02A3: enable_widescreen 1
BLACK_POLOSKI_ON = 1

Player.CanMove(PLAYER_CHAR) = False

if
COOP_ACTIVE_FLAG == 1
    then
    Player.CanMove(1) = False
    end

06C9: remove_actor $SURVIVER[0] from_group

0362: remove_actor PLAYER_ACTOR from_car_and_place_at 1135.6852 -741.7117 97.1622 
wait 2000

DONT_DO_BURGLARY = 1
select_interior 7 
0860: link_actor PLAYER_ACTOR to_interior 7
04F9: set_sky_color 1 fade 0
0776: create_objects_in_object_group "mike2"  

0362: remove_actor PLAYER_ACTOR from_car_and_place_at 225.2043 1028.157 1084.013
0362: remove_actor $SURVIVER[0] from_car_and_place_at 225.2043 1027.157 1084.013

Actor.PutAt(PLAYER_ACTOR, 227.6747, 1028.4093, 1084.0132)
Actor.Angle(PLAYER_ACTOR) = 3.0632

Actor.PutAt($SURVIVER[0], 227.7293, 1030.8259, 1084.0161)
Actor.Angle($SURVIVER[0]) = 174.9556

0936: set_camera 225.888641 1026.628662 1084.264771 position_to 226.047546 1027.076172 1084.208374 time 40000 smooth_transition 1
0920: point_camera 226.385101 1027.438110 1084.2 transverse_to 226.583252 1027.915283 1084.3 time 40000 smooth_transition 1

0337: set_actor PLAYER_ACTOR visibility 1

wait 100 

01B9: set_actor PLAYER_ACTOR armed_weapon_to 0 

00C0: set_current_time 20 0

wait 2000
create_thread @REFR_3D
 
fade 1 1000

05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 999999 ms 
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 999999 ms

wait 1000

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = PLAYER_ACTOR // (int)
v$ST_DIALOG[0] = "MSN93"
v$ST_DIALOG[1] = "MSN94"
v$ST_DIALOG[2] = "MSN95"
v$ST_DIALOG[3] = "MSN98"
v$ST_DIALOG[4] = "MSN96"
$DIALOG_COUNT = 4                 
create_thread @DG_SYS

repeat
wait 0
until $DIALOG_END == 1

$DIALOG_END = 0 

fade 0 1000 
wait 1000

$SAVE_PREPARE_OFF = 0

gosub @MUSIC_MISSION6_OFF

0936: set_camera 225.086 1027.2009 1084.0117 position_to 228.1281 1026.7927 1085.0137 time 1 smooth_transition 1
0920: point_camera 228.4042 1028.457 1084.016 transverse_to 228.4042 1028.457 1085.016 time 1 smooth_transition 1 

0792: disembark_instantly_actor PLAYER_ACTOR

camera.Restore_WithJumpCut()

05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 100 ms 
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 100 ms

00BE: text_clear_all

02A3: enable_widescreen 0
BLACK_POLOSKI_ON = 0

Player.CanMove(PLAYER_CHAR) = True

if
COOP_ACTIVE_FLAG == 1
    then
    Player.CanMove(1) = true
    end

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0 

if
COOP_ACTIVE_FLAG == 1
then
0EA1: disable_second_player_restore_camera 1
COOP_ACTOR = -69
end

Actor.DestroyInstantly($SURVIVER[0])       

if
MIKE_SHIZA_FLAG == 1
then
0318: set_latest_mission_passed 'GME1198'
else
0318: set_latest_mission_passed 'MSN97B'
end 

$QUEST_TYPE = -1
CAR_SEARCH_DO_NOT_GIVE = 0

if
not MIKE_SHIZA_FLAG == 1
then
$X_START = 1003.123 
$Y_START = -646.1412 
$Z_START = 121.3081 
$MISSION_START = 7
$MISSION_ICON = 61 
06D1: v$MISSION_NAME = "MSN131" // v$ = string
create_thread @MSN_STR
$BANKA += 2
$HEAL += 1
$BINT += 3
else
$MISSION_START_NUMBER = 7 
$SAVE_MISSION_START_FLAG = 1 
create_thread @MS_STAR
gosub @PROPUSK_KODA6 
return 
end

Cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
Camera.Restore_WithJumpCut
Player.CanMove(PLAYER_CHAR) = True
create_thread @PED_BKP PLAYER_ACTOR 371
wait 1000 
$SAVESTATUS = 0
create_thread @REFR_3D 
fade 1 1000
$HUD_REMOVE = 0 
wait 1000

MAIN_QUEST_MUSIC_FLAG = 0

$TEMP_XP = 1250
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

INT_ZOMBIE_OFF_FADING_AND_SHIT_FLAG = 0
MAIN_QUEST_TEAMMATES_REMOVE_FLAG = 2
COOP_TP_TEAMMATES_FLAG = 1
  
gosub @PROPUSK_KODA6 
return 

:MISSION6_1902_DGO
text.ClearAll()
v$FAILED_TEXT = "MS_FL1"
jump @KOD_PROVALA6

:KOD_PROVALA6
GAME_OVER_FLAG = 1
0151: remove_status_text CAR_OBISKENO
create_thread @GMV_OVR
$ONMISSION = 0 
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0  
0923: enable_air_traffic 0 
ENEX_OFF = 0     
return 

:UDALENIE_KODA6
$ONMISSION = 0      
$NOTSPAWN = 0
Marker.Disable(MISSION_MARKER)
Marker.Disable(MISSION_CHECKPOINT)
mission_cleanup 
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0  
0923: enable_air_traffic 0 
ENEX_OFF = 0    
return 

:MUSIC_MISSION6_OFF

/////////////////////Плавное затухание/////////////////////

$MUSIC_FADE_NUMBER += 1
if
$MUSIC_FADE_NUMBER > 4
    then
    $MUSIC_FADE_NUMBER = 0 
    end    

0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $MUSIC_MISSION // (int)
$MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = 0.0

$MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 700
$MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 0  
$MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
create_thread @MUS_FAD

wait 1

while $MUSIC_FADE_ENDED[$MUSIC_FADE_NUMBER] == 0
wait 0
end

//////////////////////////////////////////////////////////

return