//-------------Mission 3---------------
// Название: Нападение на дом

:MISS3
thread 'MISS3'
gosub @KOD_MISSII3 
if 
wasted_or_busted 
jf @PROPUSK_KODA3 
gosub @KOD_PROVALA3 

:PROPUSK_KODA3
gosub @UDALENIE_KODA3

HARDCORE_DANGER_FLAG_SCRIPTED = 0
if
GAME_OVER_FLAG == 1
then
HARDCORE_DANGER_ANTIFLAG_SCRIPTED = 1
end

if
GAME_OVER_FLAG == 1
then
repeat
wait 0
until BLACK_POLOSKI_ON == 1
end

{$INCLUDE checkpoints_write.txt}

end_thread

:KOD_MISSII3
increment_mission_attempts

cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
0A48: enable_menu_access_in_widescreen_mode 0
0629: change_integer_stat 258 to 3
0B04: copy_file "modloader\MODLOADER GLOBAL FILES\ZA Some Files\ZA Skins\STORY\labig1int2b.txd" to "modloader\ZA COMPLETE USER MODS FOLDER\labig1int2.txd" //IF and SET

MAIN_QUEST_MUSIC_FLAG = 1

create_thread @MCS_ASS_CREATE 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 1

32@ = 0
repeat
wait 0
fade 0 0
$SAVESTATUS = 1
03F0: enable_text_draw 1
038E: draw_box_position 320.0 224.0 size 640.0 448.0 RGBA 0 0 0 255
until 32@ > 3100

while true
wait 0
$END_FADING_MUSIC_FAST = 1

if and
$MUSIC_FADE_ENDED[0] == 1
$MUSIC_FADE_ENDED[1] == 1
$MUSIC_FADE_ENDED[2] == 1
$MUSIC_FADE_ENDED[3] == 1
$MUSIC_FADE_ENDED[4] == 1
then
$END_FADING_MUSIC_FAST = 0
break
end

end

$DONT_CHANGE_MONTH_FLAG = 1
wait 100
0A8C: write_memory 0x00B70154 size 1 value 26 virtual_protect 0 //Day
0A8C: write_memory 0x00B70155 size 1 value 9 virtual_protect 0 //Month
wait 100
$DONT_CHANGE_MONTH_FLAG = 0

while not model.Available(#MALE01)
wait 0
model.Load(#MALE01)
mload 
end

400@ = actor.Create(26,#MALE01,226.2994, 1114.2823, 1080.9929)
actor.LockInCurrentPosition(400@,true)
0619: enable_actor 400@ collision_detection 0
0337: set_actor 400@ visibility 0

401@ = actor.Create(26,#MALE01,226.2994, 1114.2823, 1080.9929)
actor.LockInCurrentPosition(401@,true)
0619: enable_actor 401@ collision_detection 0
0337: set_actor 401@ visibility 0

402@ = actor.Create(26,#MALE01,226.2994, 1114.2823, 1080.9929)
actor.LockInCurrentPosition(402@,true)
0619: enable_actor 402@ collision_detection 0
0337: set_actor 402@ visibility 0

300@ = Audiostream.Load3D("audio\sounds\quests\DoorBreak1.mp3")
Audiostream.LinkToActor(300@,400@)
301@ = Audiostream.Load3D("audio\sounds\quests\GlassBreak1.mp3")
Audiostream.LinkToActor(301@,401@)
302@ = Audiostream.Load3D("audio\sounds\quests\GlassBreak2.mp3")
Audiostream.LinkToActor(302@,402@)

00C0: set_current_time 20 0      

$SOUND_OFF_1 = 1
$SAVESTATUS = 1
$INT_ZMB_NOT = 1
ENEX_OFF = 1
$ZOMBIES_ALWAYS_VISIBLE = 1

$ONMISSION = 1
fade 0 0

////////////////////////////////////////////////////////////

repeat
wait 0 
Animation.Load("INT_HOUSE")
until Animation.Available("INT_HOUSE")
 
0619: enable_actor PLAYER_ACTOR collision_detection 0 
Cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
$HUD_REMOVE = 1

DONT_DO_BURGLARY = 1 
select_interior 5 
0860: link_actor PLAYER_ACTOR to_interior 5
04F9: set_sky_color 1 fade 0 
Actor.PutAt(PLAYER_ACTOR, 247.4238, 1109.0, 1079.8)
Actor.Angle(PLAYER_ACTOR) = 94.4616

Camera.SetPosition(245.5386, 1109.971, 1080.992, 0.0, 0.0, 0.0)

Actor.StorePos(PLAYER_ACTOR, 6@, 7@, 8@)
Camera.PointAt(6@, 7@, 8@, 2)

Player.CanMove(PLAYER_CHAR) = False
PLAYER_DONT_HAVE_BACKPACK = 1
 
wait 500
0605: actor PLAYER_ACTOR perform_animation "BED_LOOP_L" IFP "INT_HOUSE" framedelta 4.0 loop 1 lockX 0 lockY 0 lockF 0 time 20000
wait 500

PLAYER_DONT_HAVE_BACKPACK = 0

0A0E: disable_text_with_style 1
 
wait 500 
0936: set_camera 243.8298 1113.934 1080.992 position_to 245.8856 1110.222 1080.992 time 30000 smooth_transition 1 
0920: point_camera 244.7778 1112.623 1080.992 transverse_to 246.7539 1108.845 1080.995 time 30000 smooth_transition 1 
wait 500
 
$ZOMBIES_HORDE = 1  
0707: start_scene_skip_to @MISSION3_SKIP

00C0: set_current_time 23 0
$WEATHER_SYNC_FLAG = 1

0A48: enable_menu_access_in_widescreen_mode 1  
$SOUND_OFF_1 = 0
wait 0
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
create_thread @REFR_3D
  
fade 1 1000 
$MUSIC_MISSION = Audiostream.Load(CLASSIC_MUSIC_3A)
Audiostream.Loop($MUSIC_MISSION, 1)
Audiostream.PerformAction($MUSIC_MISSION, PLAY)
Audiostream.Volume($MUSIC_MISSION) = 0.0

/////////////////////Плавное нарастание/////////////////////

$MUSIC_FADE_NUMBER += 1
if
$MUSIC_FADE_NUMBER > 4
    then
    $MUSIC_FADE_NUMBER = 0 
    end    

0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $MUSIC_MISSION // (int) 
0086: $MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = $MUSIC_VOLUME // (float) 

$MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 1000
$MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 1  
$MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
create_thread @MUS_FAD

wait 1

//////////////////////////////////////////////////////////

$SIREN_X = 244.904
$SIREN_Y = 1108.96
$SIREN_Z = 1080.43 
$SIREN_VOLUME_MULTIPER = 10.0

$SOUND_EFFECT1 = Audiostream.Load("AUDIO\SOUNDS\Radio.mp3")

Audiostream.PerformAction($SOUND_EFFECT1, PLAY)
Audiostream.Volume($SOUND_EFFECT1) = 0.0
Audiostream.Loop($SOUND_EFFECT1, 1)

//////////////////////////////////////////////////////////  

//////////////////////////////////////////////////////////

32@ = 0
while 32@ < 1000
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end

//wait 1000

0@ = Audiostream.Load("AUDIO\SOUNDS\knock.wav")
Audiostream.Volume(0@) = $MUSIC_VOLUME
Audiostream.Loop(0@, 0)
Audiostream.PerformAction(0@, PLAY)
//wait 7000

32@ = 0
while 32@ < 7000
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end
 
0A0E: disable_text_with_style 0 
Audiostream.Volume(0@) = $MUSIC_VOLUME
Audiostream.Loop(0@, 0)
Audiostream.PerformAction(0@, PLAY)
//wait 2000

32@ = 0
while 32@ < 2000
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end
 
Text.ShowL('MSN6', 2000, 1)
//wait 2000 

32@ = 0
while 32@ < 2000
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end

Audiostream.Release(0@)

$SAVESTATUS = 0 
$ZOMBIES_COUNT = 20
$Z_DONT_CLIMB_FLAG = 1
$ZOMBIES_HORDE = 1
$ZOMBIE_SPAWN_ON_COORD = 1
ZOMBIE_INTERIOR_ALLOW_SPAWN = 1
ZOMBIE_INTERIOR_ALLOW_SPAWN2 = 1
$ZOMBIE_COORD_MAX = 1      
$ZOMBIE_X[0] = 228.0166
$ZOMBIE_Y[0] = 1114.2371
$ZOMBIE_Z[0] = 1080.9922 

Audiostream.Volume(300@) = $SOUNDS_VOLUME
Audiostream.Loop(300@, 0)
Audiostream.PerformAction(300@, PLAY)
  
Text.ClearAll
//wait 500 

32@ = 0
while 32@ < 500
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end

//Text.ShowL('MSN7', 1500, 1)
//wait 1500
//
//32@ = 0
//while 32@ < 1500
//wait 0
//gosub @SIREN_VOLUME_MISSION2
//end

0701: end_scene_skip
:MISSION3_SKIP_JUMP 
 
fade 0 500 
//wait 500

32@ = 0
while 32@ < 500
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end

$SAVESTATUS = 1
//wait 1000
32@ = 0
while 32@ < 1000
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end
$SAVESTATUS = 0

//////////////////////////////////////////////

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 3 weapon 1@ ammo 2@ model 3@
if
2@ < 1
then

0781: get_weapon_with_ID 22 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 22, 34)
else
2@ += 34
017B: set_actor PLAYER_ACTOR weapon 1@ ammo_to 2@
01B9: set_actor PLAYER_ACTOR armed_weapon_to 1@
end

//////////////////////////////////////////////

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 2 weapon 1@ ammo 2@ model 3@
if
2@ < 1
then

0781: get_weapon_with_ID 3 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 3, 1)
else
//ass
end

//////////////////////////////////////////////

$ZOMBIES_COUNT = 10    
$ZOMBIE_X[0] = 244.9636
$ZOMBIE_Y[0] = 1117.4175
$ZOMBIE_Z[0] = 1080.9995

Audiostream.Volume(301@) = $SOUNDS_VOLUME
Audiostream.Loop(301@, 0)
Audiostream.PerformAction(301@, PLAY) 
 
0936: set_camera 243.8298 1113.934 1080.992 position_to 245.8856 1110.222 1080.992 time 1 smooth_transition 1 
0920: point_camera 244.7778 1112.623 1080.992 transverse_to 246.7539 1108.845 1080.995 time 1 smooth_transition 1 
Cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
0619: enable_actor PLAYER_ACTOR collision_detection 1 
Actor.PutAtNoOffset(PLAYER_ACTOR, 246.1084, 1108.837, 1080.992)
Actor.Angle(PLAYER_ACTOR) = 91.8946
Camera.SetBehindPlayer
Camera.Restore_WithJumpCut
28@ = Pickup.Create(6549, 3, 239.7427, 1116.892, 1084.9922)
Player.CanMove(PLAYER_CHAR) = True
//wait 1000
32@ = 0
while 32@ < 1000
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end
create_thread @REFR_3D 
fade 1 1000
$HUD_REMOVE = 0
Text.ShowStyled("MSN5", 3000, 2) 
//wait 1000
32@ = 0
while 32@ < 1000
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
end  
09A6: enable_interior_radar_blips 1 
Text.ShowL('MSN7', 3000, 1)
$ZOMBIES_COUNT = 10
32@ = 0

repeat
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3

gosub @SIREN_VOLUME_MISSION2
if or
32@ > 3000
00EC:   actor PLAYER_ACTOR sphere 0 near_point 249.7787 1115.1106 radius 1.0 1.0
    then
    break
    end

until false
 
v$OBJECTIVE = "MSN17"
03DC: MISSION_MARKER = create_marker_above_pickup 28@

$ZOMBIES_COUNT = 20    
$ZOMBIE_X[0] = 248.3868
$ZOMBIE_Y[0] = 1112.098
$ZOMBIE_Z[0] = 1084.9922

repeat
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2
until 00EC:   actor PLAYER_ACTOR sphere 0 near_point 249.7787 1115.1106 radius 1.0 1.0 

$ZOMBIES_COUNT = 0

repeat
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2 
until Pickup.Picked_up(28@)

$PREPARING_TEXT_OFF = 0
create_thread @PED_BKP PLAYER_ACTOR 371
Marker.Disable(MISSION_MARKER)
v$OBJECTIVE = "MSN9"
018A: MISSION_CHECKPOINT = create_checkpoint_at 241.5381 1107.118 1085.039 
03BC: MISSION_SPHERE = create_sphere_at 241.5381 1107.118 1085.039 radius 1.0

$ZOMBIES_COUNT = 20      
$ZOMBIE_X[0] = 235.9527
$ZOMBIE_Y[0] = 1118.3109
$ZOMBIE_Z[0] = 1084.9922

Audiostream.Volume(302@) = $SOUNDS_VOLUME
Audiostream.Loop(302@, 0)
Audiostream.PerformAction(302@, PLAY) 

repeat
wait 0
if
not actor.Dead(PLAYER_ACTOR)
jf @KOD_PROVALA3
gosub @SIREN_VOLUME_MISSION2  
until 00EC:   actor PLAYER_ACTOR sphere 0 near_point 241.5381 1107.118 radius 1.0 1.0

v$OBJECTIVE = "DUMMY"
  
Player.CanMove(PLAYER_CHAR) = False
Marker.Disable(MISSION_CHECKPOINT)
03BD: destroy_sphere MISSION_SPHERE 
05BC: AS_actor PLAYER_ACTOR jump 1 
fade 0 500
$HUD_REMOVE = 1 
wait 500  
$SAVESTATUS = 1
Actor.Health(PLAYER_ACTOR) = 1000 
$ZOMBIES_HORDE = 0   
$SAVESTATUS = 0 
$ZOMBIE_SPAWN_ON_COORD = 0
ZOMBIE_INTERIOR_ALLOW_SPAWN = 0
ZOMBIE_INTERIOR_ALLOW_SPAWN2 = 0 
$ZOMBIES_ALWAYS_VISIBLE = 0 
09A6: enable_interior_radar_blips 0 
09BA: show_entered_zone_name 0
INT_ZOMBIE_OFF_FADING_AND_SHIT_FLAG = 1 
select_interior 0 
0860: link_actor PLAYER_ACTOR to_interior 0
04FA: reset_sky_colors_with_fade 0 
renderer 1448.785 -922.7123 
Camera.SetAtPos(1448.785, -922.7123, 45.8916)
Actor.PutAtNoOffset(PLAYER_ACTOR, 1448.785, -922.7123, 45.8916)
Actor.Angle(PLAYER_ACTOR) = 261.6791
Actor.LockInCurrentPosition(PLAYER_ACTOR) = True
Camera.Restore_WithJumpCut
Player.CanMove(PLAYER_CHAR) = True   

/////////////////////Плавное затухание/////////////////////

$MUSIC_FADE_NUMBER += 1
if
$MUSIC_FADE_NUMBER > 4
    then
    $MUSIC_FADE_NUMBER = 0 
    end    

0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $MUSIC_MISSION // (int)
$MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = 0.0

$MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 500
$MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 0  
$MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
create_thread @MUS_FAD

wait 1

while $MUSIC_FADE_ENDED[$MUSIC_FADE_NUMBER] == 0
wait 0
end

//////////////////////////////////////////////////////////

$MISSION_START_NUMBER = 3 
$SAVE_MISSION_START_FLAG = 0
$SAVE_MISSION_NOT_SAVE_FLAG = 1 
create_thread @MS_STAR

$TEMP_XP = 750
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)  

jump @UDACHNO3

:MISSION3_SKIP
0701: end_scene_skip
$SAVESTATUS = 0
$ZOMBIES_COUNT = 20
$Z_DONT_CLIMB_FLAG = 1
$ZOMBIES_HORDE = 1
$ZOMBIE_SPAWN_ON_COORD = 1
ZOMBIE_INTERIOR_ALLOW_SPAWN = 1
ZOMBIE_INTERIOR_ALLOW_SPAWN2 = 1
$ZOMBIE_COORD_MAX = 1
$ZOMBIE_X[0] = 237.3304
$ZOMBIE_Y[0] = 1118.2245
$ZOMBIE_Z[0] = 1080.9922
jump to @MISSION3_SKIP_JUMP 

:UDACHNO3  
0318: set_latest_mission_passed 'MSN5B'     
gosub @PROPUSK_KODA3 
return 

:KOD_PROVALA3
if
$SIREN_X == 244.904
then
Audiostream.PerformAction($SOUND_EFFECT1, STOP)
Audiostream.Release($SOUND_EFFECT1)
end
GAME_OVER_FLAG = 1
create_thread @GMV_OVR  
marker.Disable(MISSION_CHECKPOINT)
marker.Disable(MISSION_MARKER)
03BD: destroy_sphere MISSION_SPHERE
$ONMISSION = 0 
mission_cleanup 
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0  
0923: enable_air_traffic 0     
return 

:UDALENIE_KODA3
if
$SIREN_X == 244.904
then
Audiostream.PerformAction($SOUND_EFFECT1, STOP)
Audiostream.Release($SOUND_EFFECT1)
end
$ONMISSION = 0 
$Z_DONT_CLIMB_FLAG = 0

if and
GAME_OVER_FLAG == 0
$ACTOR_DEAD == 0
then
fade 0 0
end

mission_cleanup

if and
GAME_OVER_FLAG == 0
$ACTOR_DEAD == 0
then
fade 0 0
end

marker.Disable(MISSION_CHECKPOINT)
marker.Disable(MISSION_MARKER)

03BD: destroy_sphere MISSION_SPHERE

06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0  
0923: enable_air_traffic 0 

return

:SIREN_VOLUME_MISSION2

actor.StorePos(PLAYER_ACTOR,25@,26@,27@)
050A: 100@ = distance_between_XYZ 25@ 26@ 27@ and_XYZ $SIREN_X $SIREN_Y $SIREN_Z

0077: 100@ /= $SIREN_VOLUME_MULTIPER // (float)

2@ = 1.0

if
100@ > 0.9
    then
    100@ = 0.9
    end
    
0063: 2@ -= 100@  // (float)                   

0086: $SOUNDS_VOLUME22 = $SOUNDS_VOLUME // (float)
006D: $SOUNDS_VOLUME22 *= 2@ // (float)

if
$SIMULATING_FLAG == 1
then
$SOUNDS_VOLUME22 = 0.0
end

Audiostream.Volume($SOUND_EFFECT1) = $SOUNDS_VOLUME22          
                                                                 
return