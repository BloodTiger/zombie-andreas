//-------------Additioanl Mission 10---------------
// Название: Наглая сука

:DL_AD10
thread 'DL_AD10'
gosub @ADD_MSN_KOD_MISSI10 
if 
wasted_or_busted
Jf @ADD_MSN_PROPUSK_KOD10 
gosub @ADD_MSN_KOD_PROVAL10 

:ADD_MSN_PROPUSK_KOD10 
gosub @ADD_MSN_UDALENIE_KOD10
$QUEST_TYPE = -1
end_thread

:ADD_MSN_KOD_MISSI10 
increment_mission_attempts
0629: change_integer_stat 258 to 74

v$OBJECTIVE = "DUMMY"
$ONMISSION = 1
$SAVESTATUS = 1
$HUD_REMOVE = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
$QUEST_TYPE = 2048

$WITCH_REMOVE_FLAG = 1
$ZOMBIE_WITCH_SPAWN_TIMER_RESTART = 0
$SPECIAL_INFECTED_NOT_SPAWN_FLAG = 0
$WITCH_NOTSPAWN_FLAG = 0
$SOUND_EFFECT1_FLAG = 0
$SALVATION_FLAG = 0
$SAVEPLACE_ON = 0

while not model.Available(19)
wait 0
model.Load(19)
mload
end

03CB: set_rendering_origin_at -2189.3169 -2262.3821 30.6395

$SURVIVER[0] = actor.Create(26,19,-2189.3169, -2262.3821, 30.6395)
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 142.6159
actor.SetWalkStyle($SURVIVER[0],"GANG1")

actor.PutAtNoOffset(PLAYER_ACTOR,-2190.7395, -2264.1541, 30.625)
actor.Angle(PLAYER_ACTOR) = 318.8077

camera.SetPosition(-2193.9253, -2264.9741, 30.625,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)

v$ST_DIALOG[0] = "DLC532"
v$ST_DIALOG[1] = "DLC533"
v$ST_DIALOG[2] = "DLC534"
v$ST_DIALOG[3] = "DLC535"
v$ST_DIALOG[4] = "DLC536"
v$ST_DIALOG[5] = "DLC537"
v$ST_DIALOG[6] = "DLC538"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS

:DL_ADM10_CHECK1
wait 0
if
$DIALOG_END == 1
jf @DL_ADM10_CHECK1

fade 0 1000
wait 1000
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0

actor.PutAtNoOffset(PLAYER_ACTOR,-2192.9373, -2264.7529, 30.625)
actor.Angle(PLAYER_ACTOR) = 322.0418
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()
  
0446: set_actor $SURVIVER[0] dismemberment_possible 0
077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype 0 // see ped.dat
060B: set_actor $SURVIVER[0] decision_maker_to $DEC_PEOPLE
actor.Health($SURVIVER[0]) = $FRIEND_HEALTH
actor.WeaponAccuracy($SURVIVER[0]) = 100
07DD: set_actor $SURVIVER[0] attack_rate 999 // previously known as temper_to

0781: get_weapon_with_ID 5 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo($SURVIVER[0], 5, 1)

$SURVIVOR_MELEE_ID[0] = 5
$SURVIVOR_WEP_ID[0] = 23
create_thread @MEL_SRV $SURVIVER[0] 0

0781: get_weapon_with_ID 23 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

actor.GiveWeaponAndAmmo($SURVIVER[0],23,99999)

$SAVESTATUS = 0
0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP

wait 1000
$WITCH_REMOVE_FLAG = 0

repeat
wait 0
WITCH_CRY_FLAG = 1
SPEC_INF_ONLY_ONE_INSTANSE_SPAWN_FLAG = 1
$WITCH_X[0] = -2199.9963
$WITCH_Y[0] = -2243.8008
$WITCH_Z[0] = 33.3203
$SPAWN_WITCH_ON_COORD = 1
$SPAWN_WITCH_FLAG = 1
until 056D:   actor $WITCH[0] defined

wait 200
0812: AS_actor $WITCH[0] perform_animation "witch_crying" IFP "PED" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time -1  // versionB

fade 1 1000
$HUD_REMOVE = 0    
wait 1000
v$OBJECTIVE = "DLC539"

$SPAWN_WITCH_ON_COORD = 0
MISSION_MARKER = marker.CreateAboveActor($WITCH[0])
0168: set_marker MISSION_MARKER size 2
actor.PutAtNoOffset($WITCH[0],-2200.2358, -2243.1123, 30.7828)

:DL_ADM10_CHECK2
wait 0
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL10_PEOPLE_DEAD
if
actor.Dead($WITCH[0])
jf @DL_ADM10_CHECK2

v$OBJECTIVE = "DUMMY"          
marker.Disable(MISSION_MARKER)
text.ShowH('DLC540',1000,1)
player.CanMove(PLAYER_CHAR) = false

fade 0 1000
$HUD_REMOVE = 1
wait 1000
$SAVESTATUS = 1
WITCH_CRY_FLAG = 0
SPEC_INF_ONLY_ONE_INSTANSE_SPAWN_FLAG = 0

06C9: remove_actor $SURVIVER[0] from_group
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1

actor.PutAtNoOffset($SURVIVER[0],-2199.3989, -2244.228, 30.7768)
actor.Angle($SURVIVER[0]) = 52.9498

actor.PutAtNoOffset(PLAYER_ACTOR,-2201.0308, -2243.0193, 30.7806)
actor.Angle(PLAYER_ACTOR) = 235.7216

camera.SetPosition(-2203.116, -2243.4036, 30.787,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = PLAYER_ACTOR // (int)

v$ST_DIALOG[0] = "DLC541"
v$ST_DIALOG[1] = "DLC542"
v$ST_DIALOG[2] = "DLC543"
v$ST_DIALOG[3] = "DLC544"
$DIALOG_COUNT = 3                 
create_thread @DG_SYS

:DL_ADM10_CHECK3
wait 0
if
$DIALOG_END == 1
jf @DL_ADM10_CHECK3

fade 0 1000
wait 1000      
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0
gosub @RANDOM_WEAPON_ACTOR_ADD_MSN10
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0

actor.PutAtNoOffset(PLAYER_ACTOR,-2200.6013, -2242.23, 30.7885)
actor.Angle(PLAYER_ACTOR) = 141.5117

player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()  
actor.DestroyInstantly($SURVIVER[0])
$SAVESTATUS = 0
wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

$TEMP_XP = 2000
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

jump to @ADD_MSN_UDACHN10

:ADD_MSN_KOD_PROVAL10_PEOPLE_DEAD
text.ClearAll()
v$FAILED_TEXT = "DLC96"
create_thread @GM_2OVR
jump to @ADD_MSN_KOD_PROVAL10

:RANDOM_WEAPON_ACTOR_ADD_MSN10
wait 0 
0209: 10@ = random_int_in_ranges 22 35 
if and
   not 10@ == 19 
   not 10@ == 20 
   not 10@ == 21 
   not 10@ == 10 
   not 10@ == 11 
   not 10@ == 12 
   not 10@ == 13 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN10
if and
   not 10@ == 7 
   not 10@ == 8 
   not 10@ == 2 
   not 10@ == 1 
   not 10@ == 9 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN10
10@ = 25
0781: get_weapon_with_ID 10@ model_to 11@ 
if 
07DE:   model 11@ exists // versionB 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN10
Model.Load(11@)
038B: load_requested_models 
if
model.Available(11@)
jf @RANDOM_WEAPON_ACTOR_ADD_MSN10

0782: get_weapon_with_ID 10@ weapon_group_to 2@
2@ += 1

if
not 2@ == 4
then
0209: 12@ = random_int_in_ranges 60 120
else
0209: 12@ = random_int_in_ranges 30 60
end

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 2@ weapon 1@ ammo 4@ model 3@
if
4@ < 1
then 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 10@, 12@)
else
005A: 4@ += 12@  // (int)
017B: set_actor PLAYER_ACTOR weapon 1@ ammo_to 4@
end

return

:ADD_MSN_UDACHN10 
$ONMISSION = 0   
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
if
MCS_FLAG == 0
then      
0318: set_latest_mission_passed 'DLC531B'
else
0318: set_latest_mission_passed 'DLC531C'
end 
$ADDITIONAL_MISSIONS_PASSED += 1
0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"                            
if and
0@ == 0
$ADDITIONAL_MISSIONS_PASSED >= 20
$CHEATED_FLAG == 0
$CARL_STORY == 1
    then
    v$ACHIEVEMNT = "ACH104"
    $ACHIEVEMENT_GOT_FLAG = 1                                    
    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"
    end
gosub @ADD_MSN_PROPUSK_KOD10
return

:ADD_MSN_KOD_PROVAL10    
$ONMISSION = 0  
mission_cleanup
marker.Disable(MISSION_CHECKPOINT)
marker.Disable(MISSION_MARKER)
WITCH_CRY_FLAG = 0
SPEC_INF_ONLY_ONE_INSTANSE_SPAWN_FLAG = 0
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0   
return

:ADD_MSN_UDALENIE_KOD10 
$ONMISSION = 0
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
return