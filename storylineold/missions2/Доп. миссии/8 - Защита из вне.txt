//-------------Additioanl Mission 8---------------
// Ќазвание: ѕрикрой мен€

:DL_ADM8
thread 'DL_ADM8'
gosub @ADD_MSN_KOD_MISSI8 
if 
wasted_or_busted
Jf @ADD_MSN_PROPUSK_KOD8 
gosub @ADD_MSN_KOD_PROVAL8 

:ADD_MSN_PROPUSK_KOD8 
gosub @ADD_MSN_UDALENIE_KOD8
$QUEST_TYPE = -1
end_thread

:ADD_MSN_KOD_MISSI8 
increment_mission_attempts
0629: change_integer_stat 258 to 72

v$OBJECTIVE = "DUMMY"
$ONMISSION = 1
$SAVESTATUS = 1
$HUD_REMOVE = 1
$NOTSPAWN_ETC = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
$QUEST_TYPE = 2048

while not model.Available(#SADLER)
wait 0
model.Load(#SADLER)
mload
end
                   
SUPPLY_QUEST_TRUCK = Car.Create(#SADLER, -1099.4209, -1630.2701, 76.0859)
Car.Angle(SUPPLY_QUEST_TRUCK) = 272.2145
0229: set_car SUPPLY_QUEST_TRUCK primary_color_to 21 secondary_color_to 0
03CB: set_rendering_origin_at -353.2496 -1044.7535 59.3641

while not model.Available(159)
wait 0
model.Load(159)
mload
end

$SURVIVER[0] = actor.Create(26,159,-1104.4891, -1628.6603, 76.3739)
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 176.1723
actor.SetWalkStyle($SURVIVER[0],"GANG1")
0446: set_actor $SURVIVER[0] dismemberment_possible 0
077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype 0 // see ped.dat
060B: set_actor $SURVIVER[0] decision_maker_to $DEC_PEOPLE
actor.Health($SURVIVER[0]) = $FRIEND_HEALTH

actor.PutAtNoOffset(PLAYER_ACTOR,-1104.4824, -1631.5311, 76.3739)
actor.Angle(PLAYER_ACTOR) = 1.1374

camera.SetPosition(-1101.9457, -1632.4492, 77.3739,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)

v$ST_DIALOG[0] = "DLC500"
v$ST_DIALOG[1] = "DLC501"
v$ST_DIALOG[2] = "DLC502"
v$ST_DIALOG[3] = "DLC503"
v$ST_DIALOG[4] = "DLC504"
v$ST_DIALOG[5] = "DLC505"
v$ST_DIALOG[6] = "DLC506"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS

:DL_ADM8_CHECK1
wait 0
if
$DIALOG_END == 1
jf @DL_ADM8_CHECK1

fade 0 1000
wait 1000

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
actor.PutAtNoOffset(PLAYER_ACTOR,-1100.1503, -1633.6344, 76.3672)
actor.Angle(PLAYER_ACTOR) = 271.4188
actor.PutAtNoOffset($SURVIVER[0],-1096.7378, -1630.1914, 76.3672)
actor.Angle($SURVIVER[0]) = 92.5271
0657: car SUPPLY_QUEST_TRUCK open_door 0
car.LockInCurrentPosition(SUPPLY_QUEST_TRUCK,true)
car.DoorStatus(SUPPLY_QUEST_TRUCK,4)

UNPRESS_STAY_IN_CAR = -123 //ахахаха, ебучий гой смешно побежал вперед, ржака бл€ть

:DL_ADM8_CAR_START
wait 0
04ED: load_animation "INT_HOUSE"
if
04EE:   animation "INT_HOUSE" loaded
jf @DL_ADM8_CAR_START

077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype $ZOMBIE_SET_PEDTYPE // see ped.dat

06AB: set_actor $SURVIVER[0] all_weapons_hidden 1
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()
  
0812: AS_actor $SURVIVER[0] perform_animation "WASH_UP" IFP "INT_HOUSE" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 999999  // versionB

0568: set_actor $SURVIVER[0] untargetable 1
$WAIT_TIME = 360000

//////////////////////////////////////////
0A97: 10@ = car SUPPLY_QUEST_TRUCK struct
0A8E: 2@ = 10@ + 0x45C 
0A8C: write_memory 2@ size 2 value $WAIT_TIME virtual_protect 1
//////////////////////////////////////////

008D: $WAIT_TIME = integer $WAIT_TIME to_float
0069: $WAIT_TIME *= $SAVE_TIME2 // (float) 
008C: $WAIT_TIME = float $WAIT_TIME to_integer
03C3: set_timer_to $WAIT_TIME type 1 GXT 'DLC508' // global_variable  // Time

$SAVESTATUS = 0
wait 1000
fade 1 1000
$HUD_REMOVE = 0    
wait 1000
MISSION_MARKER = marker.CreateAboveActor($SURVIVER[0])
07E0: set_marker MISSION_MARKER type_to 1

$PEOPLE_DONT_ATTACK = 1
$NOT_ALLOW_COMMANDS = 1
0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP
07B3: set_group PLAYER_GROUP give_command 2

v$OBJECTIVE = "DLC507"
$ZOMBIES_HORDE = 1
$MISSION14_ACTIVE = 2
$SALVATION_FLAG = 1
0ED9: set_char_ignore_damage_anims $SURVIVER[0] 1

:DL_ADM8_CHECK2
wait 0
0812: AS_actor $SURVIVER[0] perform_animation "WASH_UP" IFP "INT_HOUSE" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 999999  // versionB
if
not car.Wrecked(SUPPLY_QUEST_TRUCK)
jf @DL_ADM8_CAR_WRECKED 
0@ = actor.Health($SURVIVER[0])
if
not 0@ == 0
jf @DL_ADM8_PEOPLE_DEAD
if
$WAIT_TIME < 1
jf @DL_ADM8_CHECK2

v$OBJECTIVE = "DUMMY"

06C9: remove_actor $SURVIVER[0] from_group
$PEOPLE_DONT_ATTACK = 0
$NOT_ALLOW_COMMANDS = 0
07B3: set_group PLAYER_GROUP give_command 1
$ZOMBIES_HORDE = 0
$MISSION14_ACTIVE = 0
$SALVATION_FLAG = 0
         
014F: stop_timer $WAIT_TIME
marker.Disable(MISSION_MARKER)
player.CanMove(PLAYER_CHAR) = false
fade 0 1000
$HUD_REMOVE = 1
wait 1000
       
08A6: set_car SUPPLY_QUEST_TRUCK door 0 rotation_to 0.0
car.LockInCurrentPosition(SUPPLY_QUEST_TRUCK,false)
$SAVESTATUS = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1

actor.PutAtNoOffset(PLAYER_ACTOR,-1099.3632, -1632.7827, 76.3739)
actor.Angle(PLAYER_ACTOR) = 355.3929

camera.SetPosition(-1100.6836, -1634.6217, 76.3672,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0792: disembark_instantly_actor $SURVIVER[0]
animation.Release("INT_HOUSE")

036A: put_actor $SURVIVER[0] in_car SUPPLY_QUEST_TRUCK
text.ClearAll()

wait 1000
fade 1 1000
wait 1000                       
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 9999999 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 9999999 ms
0707: start_scene_skip_to @DL_ADM8_SKIP1  
52@ = 3500
00BC: show_text_highpriority GXT 'DLC510' time 52@ flag 1
0967: actor $SURVIVER[0] move_mouth 52@ ms 
wait 52@
0968: actor $SURVIVER[0] stop_mouth 

text.ClearAll()
52@ = 2000
//00BC: show_text_highpriority GXT 'DLC511' time 52@ flag 1
0384: show_text_1string GXT 'DLC511' string v$NAME3 time 52@ 1
0967: actor PLAYER_ACTOR move_mouth 52@ ms 
wait 52@
0968: actor PLAYER_ACTOR stop_mouth 

:DL_ADM8_SKIP1
0701: end_scene_skip
text.ClearAll()
0968: actor PLAYER_ACTOR stop_mouth 
0968: actor $SURVIVER[0] stop_mouth 
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms
05D6: clear_scmpath
05D7: add_point_to_scmpath -1087.9474 -1629.7042 76.3672
05D7: add_point_to_scmpath -964.7985 -1720.173 77.5703
07E7: AS_assign_scmpath_to_actor $SURVIVER[0] in_car SUPPLY_QUEST_TRUCK speed 70.0 flags 1 0 2
00AE: set_car SUPPLY_QUEST_TRUCK traffic_behaviour_to 2
0423: set_car SUPPLY_QUEST_TRUCK improved_handling_to 2.0 // (float) 
03AB: set_car SUPPLY_QUEST_TRUCK strong 1 
00AE: unknown_set_car SUPPLY_QUEST_TRUCK to_ignore_traffic_lights 2
wait 2000
fade 0 1000
wait 1000
player.CanMove(PLAYER_CHAR) = true
random_int 0@ 750 1000
0109: player PLAYER_CHAR money += 0@
cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
actor.PutAtNoOffset(PLAYER_ACTOR,-1100.1592, -1632.9694, 76.3739)
actor.Angle(PLAYER_ACTOR) = 262.8864
camera.Restore_WithJumpCut()
actor.DestroyInstantly($SURVIVER[0])
car.Destroy(SUPPLY_QUEST_TRUCK)
$SAVESTATUS = 0 
$NOTSPAWN_ETC = 0 
wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

$TEMP_XP = 2000
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

jump to @ADD_MSN_UDACHN8

:DL_ADM8_CAR_WRECKED
text.ClearAll()
v$FAILED_TEXT = "DLC153"
create_thread @GM_2OVR
jump to @ADD_MSN_KOD_PROVAL8

:DL_ADM8_PEOPLE_DEAD
text.ClearAll()
v$FAILED_TEXT = "DLC96"
0792: disembark_instantly_actor $SURVIVER[0]
05BE: AS_actor $SURVIVER[0] die
create_thread @GM_2OVR 
jump to @ADD_MSN_KOD_PROVAL8

:ADD_MSN_UDACHN8 
$ONMISSION = 0   
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
if
MCS_FLAG == 0
then      
0318: set_latest_mission_passed 'DLC512B'
else
0318: set_latest_mission_passed 'DLC512C'
end 
$ADDITIONAL_MISSIONS_PASSED += 1
0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"                            
if and
0@ == 0
$ADDITIONAL_MISSIONS_PASSED >= 20
$CHEATED_FLAG == 0
$CARL_STORY == 1
    then
    v$ACHIEVEMNT = "ACH104"
    $ACHIEVEMENT_GOT_FLAG = 1                                    
    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"
    end
gosub @ADD_MSN_PROPUSK_KOD8
return

:ADD_MSN_KOD_PROVAL8    
$ONMISSION = 0
$NOTSPAWN_ETC = 0
$ZOMBIES_HORDE = 0
$MISSION14_ACTIVE = 0
$SALVATION_FLAG = 0 
$PEOPLE_DONT_ATTACK = 0
$NOT_ALLOW_COMMANDS = 0
07B3: set_group PLAYER_GROUP give_command 1 
mission_cleanup
marker.Disable(MISSION_MARKER)
014F: stop_timer $WAIT_TIME
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0   
return

:ADD_MSN_UDALENIE_KOD8 
$ONMISSION = 0
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
return