//-------------Additioanl Mission 12---------------
// Название: Из точки А в точку Б

:DL_AD12
thread 'DL_AD12'
gosub @ADD_MSN_KOD_MISSI12 
if 
wasted_or_busted
Jf @ADD_MSN_PROPUSK_KOD12 
gosub @ADD_MSN_KOD_PROVAL12 

:ADD_MSN_PROPUSK_KOD12 
gosub @ADD_MSN_UDALENIE_KOD12
$QUEST_TYPE = -1
$ZOMBIE_DONT_ATTACK_PLAYER = 0
end_thread

:ADD_MSN_KOD_MISSI12 
increment_mission_attempts
0629: change_integer_stat 258 to 76

$ONMISSION = 1
$NOTSPAWN_ETC = 1
$SAVESTATUS = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
v$OBJECTIVE = "DUMMY"
$QUEST_TYPE = 2048

while not model.Available(24)
wait 0
model.Load(24)
mload
end

03CB: set_rendering_origin_at -2668.6038 1350.9042 55.4297

$SURVIVER[0] = actor.Create(26,24,-2668.6038, 1350.9042, 55.4297)
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 91.5232
actor.SetWalkStyle($SURVIVER[0],"GANG1")
0568: set_actor $SURVIVER[0] untargetable 1
0ED9: set_char_ignore_damage_anims $SURVIVER[0] 1

actor.PutAtNoOffset(PLAYER_ACTOR,-2670.3015, 1350.8512, 55.4297)
actor.Angle(PLAYER_ACTOR) = 267.8194

camera.SetPosition(-2672.8657, 1352.4596, 55.4297,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)

v$ST_DIALOG[0] = "DLC561"
v$ST_DIALOG[1] = "DLC562"
v$ST_DIALOG[2] = "DLC563"
v$ST_DIALOG[3] = "DLC564"
v$ST_DIALOG[4] = "DLC565"
v$ST_DIALOG[5] = "DLC566"
v$ST_DIALOG[6] = "DLC567"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS

:DL_ADM12_CHECK1
wait 0
if
$DIALOG_END == 1
jf @DL_ADM12_CHECK1

fade 0 1000
wait 1000

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0

actor.PutAtNoOffset(PLAYER_ACTOR,-2672.7358, 1352.1444, 55.4297)
actor.Angle(PLAYER_ACTOR) = 0.2534

player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()
  
0446: set_actor $SURVIVER[0] dismemberment_possible 0
077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype 0 // see ped.dat
060B: set_actor $SURVIVER[0] decision_maker_to $DEC_PEOPLE
008B: 0@ = $FRIEND_HEALTH // (int)
0@ *= 5
actor.Health($SURVIVER[0]) = 0@
actor.WeaponAccuracy($SURVIVER[0]) = 100
07DD: set_actor $SURVIVER[0] attack_rate 999 // previously known as temper_to

0781: get_weapon_with_ID 4 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo($SURVIVER[0], 4, 1)

$SURVIVOR_MELEE_ID[0] = 4
$SURVIVOR_WEP_ID[0] = 78
create_thread @MEL_SRV $SURVIVER[0] 0

0781: get_weapon_with_ID 78 model_to 1@

while not model.Available(1@)
wait 0
model.Load(1@)
mload
end

actor.GiveWeaponAndAmmo($SURVIVER[0],78,99999)

gosub @ADD_MSN_MOST2_COORD_CHECK_FIRST
$SAVESTATUS = 0
07CC: set_player PLAYER_CHAR can_enter_exit_vehicles 0
$ZOMBIE_DONT_ATTACK_PLAYER = 1
05D3: AS_actor $SURVIVER[0] goto_point -2674.1379 2096.4509 55.4219 mode 7 time 9999999 ms // versionA

100@ = 0

wait 1000
fade 1 1000
$HUD_REMOVE = 0    
wait 1000
$ZOMBIES_HORDE = 1
v$OBJECTIVE = "DLC554"
018A: MISSION_CHECKPOINT = create_checkpoint_at -2674.1379 2096.4509 55.4219

32@ = 0

:DL_ADM12_CHECK2
wait 0
gosub @ADD_MSN_MOST2_COORD_CHECK
07CC: set_player PLAYER_CHAR can_enter_exit_vehicles 0
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL12_PEOPLE_DEAD      
if
00EC:   actor PLAYER_ACTOR sphere 0 near_point -2674.1379 2096.4509 radius 75.0 75.0
jf @DL_ADM12_CHECK2
$SPAWN_TANK_FLAG = 1

:DL_ADM12_CHECK3
wait 0
gosub @ADD_MSN_MOST2_COORD_CHECK
07CC: set_player PLAYER_CHAR can_enter_exit_vehicles 0
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL12_PEOPLE_DEAD      
if and
00EC:   actor PLAYER_ACTOR sphere 1 near_point -2674.1379 2096.4509 radius 2.0 2.0
00EC:   actor $SURVIVER[0] sphere 0 near_point -2674.1379 2096.4509 radius 5.0 5.0
jf @DL_ADM12_CHECK3

v$OBJECTIVE = "DUMMY"
marker.Disable(MISSION_CHECKPOINT)
player.CanMove(PLAYER_CHAR) = false

fade 0 1000
$HUD_REMOVE = 1
wait 1000
$SAVESTATUS = 1
06C9: remove_actor $SURVIVER[0] from_group

cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1

actor.PutAtNoOffset($SURVIVER[0],-2669.7444, 2159.7871, 55.4297)
actor.Angle($SURVIVER[0]) = 96.5927

actor.PutAtNoOffset(PLAYER_ACTOR,-2672.0012, 2159.6104, 55.4297)
actor.Angle(PLAYER_ACTOR) = 270.5911

camera.SetPosition(-2674.8557, 2161.5034, 55.4297,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = PLAYER_ACTOR // (int)

v$ST_DIALOG[0] = "DLC568"
v$ST_DIALOG[1] = "DLC569"
v$ST_DIALOG[2] = "DLC570"
v$ST_DIALOG[3] = "DLC571"
v$ST_DIALOG[4] = "DLC572"
$DIALOG_COUNT = 4                 
create_thread @DG_SYS

:DL_ADM12_CHECK4
wait 0
if
$DIALOG_END == 1
jf @DL_ADM12_CHECK4

fade 0 1000
wait 1000      
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0
gosub @RANDOM_WEAPON_ACTOR_ADD_MSN12
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms
cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
actor.PutAtNoOffset(PLAYER_ACTOR,-2672.3672, 2163.5676, 55.4297)
actor.Angle(PLAYER_ACTOR) = 2.3986
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()  
actor.DestroyInstantly($SURVIVER[0])
$SAVESTATUS = 0
wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

$TEMP_XP = 2500
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

jump to @ADD_MSN_UDACHN12

:ADD_MSN_KOD_PROVAL12_PEOPLE_DEAD
text.ClearAll()
v$FAILED_TEXT= "DLC96"
create_thread @GM_2OVR
jump to @ADD_MSN_KOD_PROVAL12

:ADD_MSN_UDACHN12 
$ONMISSION = 0   
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
if
MCS_FLAG == 0
then      
0318: set_latest_mission_passed 'DLC560B'
else
0318: set_latest_mission_passed 'DLC560C'
end 
$ADDITIONAL_MISSIONS_PASSED += 1
0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"                            
if and
0@ == 0
$ADDITIONAL_MISSIONS_PASSED >= 20
$CHEATED_FLAG == 0
$CARL_STORY == 1
    then
    v$ACHIEVEMNT = "ACH104"
    $ACHIEVEMENT_GOT_FLAG = 1                                    
    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"
    end
gosub @ADD_MSN_PROPUSK_KOD12
return

:ADD_MSN_KOD_PROVAL12    
$ONMISSION = 0
$ZOMBIES_HORDE = 0
$ZOMBIE_SPAWN_ON_COORD = 0
$SPECIAL_INFECTED_SPAWN_OZC_FLAG = 0  
mission_cleanup
marker.Disable(MISSION_CHECKPOINT)
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0   
return

:ADD_MSN_UDALENIE_KOD12 
$ONMISSION = 0
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
return

:RANDOM_WEAPON_ACTOR_ADD_MSN12
wait 0 
0209: 10@ = random_int_in_ranges 22 35 
if and
   not 10@ == 19 
   not 10@ == 20 
   not 10@ == 21 
   not 10@ == 10 
   not 10@ == 11 
   not 10@ == 12 
   not 10@ == 13 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN12
if and
   not 10@ == 7 
   not 10@ == 8 
   not 10@ == 2 
   not 10@ == 1 
   not 10@ == 9 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN12
0781: get_weapon_with_ID 10@ model_to 11@ 
if 
07DE:   model 11@ exists // versionB 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN12
Model.Load(11@)
038B: load_requested_models 
if
model.Available(11@)
jf @RANDOM_WEAPON_ACTOR_ADD_MSN12

0782: get_weapon_with_ID 10@ weapon_group_to 2@
2@ += 1

if
not 2@ == 4
then
0209: 12@ = random_int_in_ranges 600 1200
else
0209: 12@ = random_int_in_ranges 300 600
end

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 2@ weapon 1@ ammo 4@ model 3@
if
4@ < 1
then 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 10@, 12@)
else
005A: 4@ += 12@  // (int)
017B: set_actor PLAYER_ACTOR weapon 1@ ammo_to 4@
end

return

:ADD_MSN_MOST2_COORD_CHECK
gosub @ADD_MSN_MOST2_DO_PRIKOL_THINGS
0208: 9@ = random_float_in_ranges 40.0 -40.0 
0208: 10@ = random_float_in_ranges 120.0 160.0
04C4: store_coords_to 6@ 7@ 8@ from_actor PLAYER_ACTOR with_offset 9@ 10@ 0.0
02C1: store_to 9@ 10@ 11@ car_path_coords_closest_to 6@ 7@ 8@ 
Actor.StorePos(PLAYER_ACTOR, 6@, 7@, 8@)
050A: 17@ = distance_between_XYZ 6@ 7@ 8@ and_XYZ 9@ 10@ 11@
0092: 18@ = float 17@ to_integer
if and
18@ >= 90 
not 18@ >= 180 
jf @ADD_MSN_MOST2_COORD_CHECK_RETURN 
if and
8AE2: not 30@ = random_vehicle_near_point 9@ 10@ 11@ in_radius 10.0 find_next 0 pass_wrecked 1 //IF and SET
8AE1: not 31@ = random_actor_near_point 9@ 10@ 11@ in_radius 5.0 find_next 0 pass_deads 0 //IF and SET
jf @ADD_MSN_MOST2_COORD_CHECK_RETURN
$ZOMBIE_SPAWN_ON_COORD = 1
$SPECIAL_INFECTED_SPAWN_OZC_FLAG = 1 
$ZOMBIE_COORD_MAX = 1 
0088: $ZOMBIE_X[0] = 9@ // (float)
0088: $ZOMBIE_Y[0] = 10@ // (float)
0088: $ZOMBIE_Z[0] = 11@ // (float) 
return

:ADD_MSN_MOST2_COORD_CHECK_RETURN
return

:ADD_MSN_MOST2_COORD_CHECK_FIRST
wait 0
0208: 9@ = random_float_in_ranges 40.0 -40.0 
0208: 10@ = random_float_in_ranges 120.0 160.0
04C4: store_coords_to 6@ 7@ 8@ from_actor PLAYER_ACTOR with_offset 9@ 10@ 0.0
02C1: store_to 9@ 10@ 11@ car_path_coords_closest_to 6@ 7@ 8@ 
Actor.StorePos(PLAYER_ACTOR, 6@, 7@, 8@)
050A: 17@ = distance_between_XYZ 6@ 7@ 8@ and_XYZ 9@ 10@ 11@
0092: 18@ = float 17@ to_integer
if and
18@ >= 90 
not 18@ >= 180 
jf @ADD_MSN_MOST2_COORD_CHECK_FIRST
if and
8AE2: not 30@ = random_vehicle_near_point 9@ 10@ 11@ in_radius 10.0 find_next 0 pass_wrecked 1 //IF and SET
8AE1: not 31@ = random_actor_near_point 9@ 10@ 11@ in_radius 5.0 find_next 0 pass_deads 0 //IF and SET
jf @ADD_MSN_MOST2_COORD_CHECK_FIRST
$ZOMBIE_SPAWN_ON_COORD = 1
$SPECIAL_INFECTED_SPAWN_OZC_FLAG = 1  
$ZOMBIE_COORD_MAX = 1 
0088: $ZOMBIE_X[0] = 9@ // (float)
0088: $ZOMBIE_Y[0] = 10@ // (float)
0088: $ZOMBIE_Z[0] = 11@ // (float) 
return

:ADD_MSN_MOST2_DO_PRIKOL_THINGS

if
0E45: frame_mod 900
then
$SPAWN_WITCH_FLAG = 1
end

if
0E45: frame_mod 1600
then
$SPAWN_HUNTER_FLAG = 1
end

if and
0E45: frame_mod 30
100@ == 0
then
0EFF: get_char_simplest_active_task $SURVIVER[0] id_to 2@ pointer_to 3@
if
not 2@ == 900
    then
    05D3: AS_actor $SURVIVER[0] goto_point -2674.1379 2096.4509 55.4219 mode 7 time 9999999 ms // versionA
    end
end

25@ = 0

for 24@ = 0 to 19

if
056D:   actor $ZOMBIE[24@] defined
    then    
    if
    0104:   actor $ZOMBIE[24@] near_actor $SURVIVER[0] radius 15.0 15.0 15.0 sphere 0
        then
        25@ += 1
        end    
    end

end

if and
100@ == 0
25@ > 5
32@ > 10000
    then
    32@ = 0
    100@ = 1
    0687: clear_actor $SURVIVER[0] task
    0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP
    end
  
if
100@ == 1
    then
    if or
    25@ < 5
    32@ > 10000
        then
        32@ = 0
        100@ = 0
        0687: clear_actor $SURVIVER[0] task
        06C9: remove_actor $SURVIVER[0] from_group
        05D3: AS_actor $SURVIVER[0] goto_point -2674.1379 2096.4509 55.4219 mode 7 time 9999999 ms // versionA
        end
    end

return