//-------------Additioanl Mission 9---------------
// Название: Потерянный друг

:DL_ADM9
thread 'DL_ADM9'
gosub @ADD_MSN_KOD_MISSI9 
if 
wasted_or_busted
Jf @ADD_MSN_PROPUSK_KOD9 
gosub @ADD_MSN_KOD_PROVAL9 

:ADD_MSN_PROPUSK_KOD9 
gosub @ADD_MSN_UDALENIE_KOD9
$QUEST_TYPE = -1
end_thread

:ADD_MSN_KOD_MISSI9 
increment_mission_attempts
0629: change_integer_stat 258 to 73

v$OBJECTIVE = "DUMMY"
$ONMISSION = 1
$SAVESTATUS = 1
$HUD_REMOVE = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
$QUEST_TYPE = 2048

while not model.Available(133)
wait 0
model.Load(133)
mload
end

03CB: set_rendering_origin_at -937.967 -496.9721 25.9609

$SURVIVER[0] = actor.Create(26,133,-937.967, -496.9721, 25.9609)
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 157.204
actor.SetWalkStyle($SURVIVER[0],"GANG1")
0446: set_actor $SURVIVER[0] dismemberment_possible 0
077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype 0 // see ped.dat
060B: set_actor $SURVIVER[0] decision_maker_to $DEC_PEOPLE
actor.Health($SURVIVER[0]) = $FRIEND_HEALTH
actor.WeaponAccuracy($SURVIVER[0]) = 100
07DD: set_actor $SURVIVER[0] attack_rate 999 // previously known as temper_to

actor.PutAtNoOffset(PLAYER_ACTOR,-938.631, -498.5707, 25.9609)
actor.Angle(PLAYER_ACTOR) = 337.678

camera.SetPosition(-942.0804, -501.487, 25.9609,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)

v$ST_DIALOG[0] = "DLC514"
v$ST_DIALOG[1] = "DLC515"
v$ST_DIALOG[2] = "DLC516"
v$ST_DIALOG[3] = "DLC517"
v$ST_DIALOG[4] = "DLC518"
v$ST_DIALOG[5] = "DLC519"
v$ST_DIALOG[6] = "DLC520"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS

:DL_ADM9_CHECK1
wait 0
if
$DIALOG_END == 1
jf @DL_ADM9_CHECK1

fade 0 1000
wait 1000

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
actor.PutAtNoOffset(PLAYER_ACTOR,-939.89, -500.049, 25.9609)
actor.Angle(PLAYER_ACTOR) = 256.4194
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()

0781: get_weapon_with_ID 15 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo($SURVIVER[0], 15, 1)

$SURVIVOR_MELEE_ID[0] = 15
$SURVIVOR_WEP_ID[0] = 30
create_thread @MEL_SRV $SURVIVER[0] 0

0781: get_weapon_with_ID 30 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

actor.GiveWeaponAndAmmo($SURVIVER[0],30,99999)

$SAVESTATUS = 0
0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP
wait 1000
fade 1 1000
$HUD_REMOVE = 0    
wait 1000
v$OBJECTIVE = "DLC521"
018A: MISSION_CHECKPOINT = create_checkpoint_at -1039.4131 -590.4639 32.0078

:DL_ADM9_CHECK2
wait 0
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL9_PEOPLE_DEAD
if and
00EC:   actor PLAYER_ACTOR sphere 1 near_point -1039.4131 -590.4639 radius 2.0 2.0
00EC:   actor $SURVIVER[0] sphere 0 near_point -1039.4131 -590.4639 radius 5.0 5.0
jf @DL_ADM9_CHECK2          
marker.Disable(MISSION_CHECKPOINT)
v$OBJECTIVE = "DLC522"

while not model.Available(160)
wait 0
model.Load(160)
mload
end

gosub @RANDOM_POS_ADD_MSN9
actor.StorePos($SURVIVER[1],$ACTOR_X,$ACTOR_Y,$ACTOR_Z)
actor.SetWalkStyle($SURVIVER[1],"GANG1")
0446: set_actor $SURVIVER[1] dismemberment_possible 0
077A: set_actor $SURVIVER[1] acquaintance 0 to_actors_pedtype 0 // see ped.dat
060B: set_actor $SURVIVER[1] decision_maker_to $DEC_PEOPLE
actor.Health($SURVIVER[1]) = $FRIEND_HEALTH
actor.WeaponAccuracy($SURVIVER[1]) = 100
07DD: set_actor $SURVIVER[1] attack_rate 999 // previously known as temper_to
05C5: AS_actor $SURVIVER[1] cower -1 ms
077A: set_actor $SURVIVER[1] acquaintance 0 to_actors_pedtype 20 // see ped.dat
0568: set_actor $SURVIVER[1] untargetable 1

:DL_ADM9_CHECK3
wait 0   
05C5: AS_actor $SURVIVER[1] cower -1 ms
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL9_PEOPLE_DEAD
if
not actor.Dead($SURVIVER[1])
jf @ADD_MSN_KOD_PROVAL9_PEOPLE_DEAD  
if
00FE:   actor PLAYER_ACTOR sphere 0 in_sphere $ACTOR_X $ACTOR_Y $ACTOR_Z radius 5.0 5.0 2.0
jf @DL_ADM9_CHECK3

v$OBJECTIVE = "DUMMY"

077A: set_actor $SURVIVER[1] acquaintance 4 to_actors_pedtype 20 // see ped.dat

0781: get_weapon_with_ID 15 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo($SURVIVER[1], 15, 1)

$SURVIVOR_MELEE_ID[1] = 15
$SURVIVOR_WEP_ID[1] = 30
create_thread @MEL_SRV $SURVIVER[1] 1

0781: get_weapon_with_ID 30 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

actor.GiveWeaponAndAmmo($SURVIVER[1],30,99999)

05C5: AS_actor $SURVIVER[1] cower 100 ms 
0631: put_actor $SURVIVER[1] in_group PLAYER_GROUP

TANK_DELETE_FLAG = 1 
wait 100 
TANK_DELETE_FLAG = 0 
text.ShowH('DLC523',3000,1)
$TANK_X = -1027.873
$TANK_Y = -668.037
$TANK_Z = 32.0078
$SPAWN_TANK_ON_COORD = 1
$SPAWN_TANK_FLAG = 1
wait 2900
v$OBJECTIVE = "DLC524"
018A: MISSION_CHECKPOINT = create_checkpoint_at -934.4771 -505.9077 25.9609
$SPAWN_TANK_ON_COORD = 0
wait 2000
text.ShowH('DLC530',2000,1)

:DL_ADM9_CHECK4
wait 0
if and
not actor.Dead($SURVIVER[0])
not actor.Dead($SURVIVER[1])
jf @ADD_MSN_KOD_PROVAL9_PEOPLE_DEAD
if and
00EC:   actor PLAYER_ACTOR sphere 1 near_point -934.4771 -505.9077 radius 2.0 2.0
00EC:   actor $SURVIVER[0] sphere 0 near_point -934.4771 -505.9077 radius 5.0 5.0
00EC:   actor $SURVIVER[1] sphere 0 near_point -934.4771 -505.9077 radius 5.0 5.0
jf @DL_ADM9_CHECK4

v$OBJECTIVE = "DUMMY"          
marker.Disable(MISSION_CHECKPOINT)
player.CanMove(PLAYER_CHAR) = false

fade 0 1000
$HUD_REMOVE = 1
wait 1000
$SAVESTATUS = 1

06C9: remove_actor $SURVIVER[0] from_group
06C9: remove_actor $SURVIVER[1] from_group

actor.PutAtNoOffset($SURVIVER[0],-937.5917, -496.79, 25.9609)
actor.Angle($SURVIVER[0]) = 162.0428

actor.PutAtNoOffset($SURVIVER[1],-939.2946, -496.3331, 25.9609)
actor.Angle($SURVIVER[1]) = 157.8694

actor.PutAtNoOffset(PLAYER_ACTOR,-939.2803, -498.671, 25.9609)
actor.Angle(PLAYER_ACTOR) = 339.2833

cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1

camera.SetPosition(-936.1918, -501.7843, 25.9609,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)
  
0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor $SURVIVER[1] rotate_to_actor PLAYER_ACTOR

0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor $SURVIVER[1] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = $SURVIVER[1] // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = PLAYER_ACTOR // (int)

v$ST_DIALOG[0] = "DLC525"
v$ST_DIALOG[1] = "DLC526"
v$ST_DIALOG[2] = "DLC527"
v$ST_DIALOG[3] = "DLC528"
v$ST_DIALOG[4] = "DLC529"
$DIALOG_COUNT = 4                 
create_thread @DG_SYS

:DL_ADM9_CHECK5
wait 0
if
$DIALOG_END == 1
jf @DL_ADM9_CHECK5

fade 0 1000
wait 1000

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor $SURVIVER[1] look_at_actor PLAYER_ACTOR 1 ms

05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0

actor.DestroyInstantly($SURVIVER[0])
actor.DestroyInstantly($SURVIVER[1])

actor.PutAtNoOffset(PLAYER_ACTOR,-940.6646, -500.6303, 25.9609)
actor.Angle(PLAYER_ACTOR) = 255.6146

player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()

032B: 321@ = create_weapon_pickup #FLAME group 3 ammo 2000 at -937.6027 -497.9399 25.9609

$SAVESTATUS = 0
wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

$TEMP_XP = 2000
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

jump to @ADD_MSN_UDACHN9

:RANDOM_POS_ADD_MSN9
random_int 0@ 0 4
if and
not 0@ == 0
not 0@ == 1
jf @RANDOM_POS_ADD_MSN9_1
if and
not 0@ == 1
not 0@ == 2
jf @RANDOM_POS_ADD_MSN9_2
if and
not 0@ == 3
not 0@ == 4
jf @RANDOM_POS_ADD_MSN9_3

:RANDOM_POS_ADD_MSN9_1
$SURVIVER[1] = actor.Create(26,160,-1099.6603, -754.3664, 32.0078)
create_thread @PED_BKP $SURVIVER[1] 371
actor.Angle($SURVIVER[1]) = 273.9959
return

:RANDOM_POS_ADD_MSN9_2
$SURVIVER[1] = actor.Create(26,160,-996.1178, -721.185, 35.9375)
create_thread @PED_BKP $SURVIVER[1] 371
actor.Angle($SURVIVER[1]) = 357.6566
return

:RANDOM_POS_ADD_MSN9_3
$SURVIVER[1] = actor.Create(26,160,-1053.7368, -698.6085, 32.3516)
create_thread @PED_BKP $SURVIVER[1] 371
actor.Angle($SURVIVER[1]) = 253.6058
return

:ADD_MSN_KOD_PROVAL9_PEOPLE_DEAD
text.ClearAll()
v$FAILED_TEXT = "DLC96"
create_thread @GM_2OVR 
jump to @ADD_MSN_KOD_PROVAL9

:ADD_MSN_UDACHN9 
$ONMISSION = 0   
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
if
MCS_FLAG == 0
then      
0318: set_latest_mission_passed 'DLC513B'
else
0318: set_latest_mission_passed 'DLC513C'
end 
$ADDITIONAL_MISSIONS_PASSED += 1
0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"                            
if and
0@ == 0
$ADDITIONAL_MISSIONS_PASSED >= 20
$CHEATED_FLAG == 0
$CARL_STORY == 1
    then
    v$ACHIEVEMNT = "ACH104"
    $ACHIEVEMENT_GOT_FLAG = 1                                    
    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"
    end
gosub @ADD_MSN_PROPUSK_KOD9
return

:ADD_MSN_KOD_PROVAL9    
$ONMISSION = 0  
mission_cleanup
marker.Disable(MISSION_CHECKPOINT)
marker.Disable(MISSION_MARKER)
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0   
return

:ADD_MSN_UDALENIE_KOD9 
$ONMISSION = 0
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
return