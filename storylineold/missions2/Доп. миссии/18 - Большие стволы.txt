:DL_AD18
thread 'DL_AD18'
gosub @ADD_MSN_KOD_MISSI18 
if 
wasted_or_busted
Jf @ADD_MSN_PROPUSK_KOD18 
gosub @ADD_MSN_KOD_PROVAL18 

:ADD_MSN_PROPUSK_KOD18 
gosub @ADD_MSN_UDALENIE_KOD18
$QUEST_TYPE = -1
end_thread

:ADD_MSN_KOD_MISSI18 
increment_mission_attempts
0629: change_integer_stat 258 to 82

v$OBJECTIVE = "DUMMY"
$ONMISSION = 1
$SAVESTATUS = 1
$HUD_REMOVE = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
07FB: set_interior 'AMMUN2' access 0
$QUEST_TYPE = 2048
ENEX_OFF = 1

03CB: set_rendering_origin_at 2570.0178 1082.2036 10.8203

while not model.Available(180)
wait 0
model.Load(180)
mload
end

$SURVIVER[0] = actor.Create(26,180,2570.0178, 1082.2036, 10.8203)
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 177.165
actor.SetWalkStyle($SURVIVER[0],"GANG1")

actor.PutAtNoOffset(PLAYER_ACTOR,2570.2559, 1079.683, 10.8203)
actor.Angle(PLAYER_ACTOR) = 0.5073

camera.SetPosition(2567.9626, 1077.7574, 10.6719,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)
0568: set_actor $SURVIVER[0] untargetable 1

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)

v$ST_DIALOG[0] = "DLC634"
v$ST_DIALOG[1] = "DLC635"
v$ST_DIALOG[2] = "DLC636"
v$ST_DIALOG[3] = "DLC637"
v$ST_DIALOG[4] = "DLC638"
v$ST_DIALOG[5] = "DLC639"
v$ST_DIALOG[6] = "DLC640"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS

:DL_ADM18_CHECK1
wait 0
if
$DIALOG_END == 1
jf @DL_ADM18_CHECK1

fade 0 1000
wait 1000

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0

actor.PutAtNoOffset(PLAYER_ACTOR,2570.8345, 1079.1057, 10.8203)
actor.Angle(PLAYER_ACTOR) = 90.0749
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()
  
0446: set_actor $SURVIVER[0] dismemberment_possible 0
077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype 0 // see ped.dat
060B: set_actor $SURVIVER[0] decision_maker_to $DEC_PEOPLE
actor.Health($SURVIVER[0]) = $FRIEND_HEALTH
actor.WeaponAccuracy($SURVIVER[0]) = 100
07DD: set_actor $SURVIVER[0] attack_rate 999 // previously known as temper_to

0781: get_weapon_with_ID 3 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo($SURVIVER[0], 3, 1)

$SURVIVOR_MELEE_ID[0] = 3
$SURVIVOR_WEP_ID[0] = 28
create_thread @MEL_SRV $SURVIVER[0] 0

0781: get_weapon_with_ID 28 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

actor.GiveWeaponAndAmmo($SURVIVER[0],28,99999)

$SAVESTATUS = 0
0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP
wait 1000
fade 1 1000
$HUD_REMOVE = 0    
wait 1000
v$OBJECTIVE = "DLC641"
018A: MISSION_CHECKPOINT = create_checkpoint_at 2157.4927 942.9228 10.8203

:DL_ADM18_CHECK2
wait 0
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL18_PEOPLE_DEAD
if and
00EC:   actor PLAYER_ACTOR sphere 1 near_point 2155.78 943.1948 radius 2.0 2.0
00EC:   actor $SURVIVER[0] sphere 0 near_point 2155.78 943.1948 radius 10.0 10.0
not actor.Driving(PLAYER_ACTOR)
not actor.Driving($SURVIVER[0])
jf @DL_ADM18_CHECK2

v$OBJECTIVE = "DUMMY"
marker.Disable(MISSION_CHECKPOINT)
077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype $ZOMBIE_SET_PEDTYPE // see ped.dat
06C9: remove_actor $SURVIVER[0] from_group
0568: set_actor $SURVIVER[0] untargetable 1

$WAIT_TIME = 240000
008D: $WAIT_TIME = integer $WAIT_TIME to_float
0069: $WAIT_TIME *= $SAVE_TIME2 // (float) 
008C: $WAIT_TIME = float $WAIT_TIME to_integer
03C3: set_timer_to $WAIT_TIME type 1 GXT 'MSN608' // global_variable  // Time
$ZOMBIES_HORDE = 1
$MISSION14_ACTIVE = 2
$SALVATION_FLAG = 1
0ED9: set_char_ignore_damage_anims $SURVIVER[0] 1

animation.Load("BOMBER")

0615: define_AS_pack_begin 51@
05D3: AS_actor -1 goto_point 2158.144 943.1116 10.8203 mode 6 time 99999 ms // versionA
05D4: AS_actor -1 rotate_angle 272.5177
0812: AS_actor -1 perform_animation "BOM_Plant_Loop" IFP "BOMBER" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 999999  // versionB
0616: define_AS_pack_end 51@
0618: assign_actor $SURVIVER[0] to_AS_pack 51@
061B: remove_references_to_AS_pack 51@

06AB: set_actor $SURVIVER[0] all_weapons_hidden 1

text.ShowH('DLC642',3000,1)

//////////////////////////////////////////////////////////

$SIREN_X = 2158.144
$SIREN_Y = 943.1116
$SIREN_Z = 10.8203 
$SIREN_VOLUME_MULTIPER = 200.0

$SOUND_EFFECT1 = Audiostream.Load("AUDIO\SOUNDS\AlarmBanditBase.mp3")

Audiostream.PerformAction($SOUND_EFFECT1, PLAY)
Audiostream.Volume($SOUND_EFFECT1) = VERO_TIPA_VOLUME
Audiostream.Loop($SOUND_EFFECT1, 1)

//////////////////////////////////////////////////////////

32@ = 0

while 32@ < 3000
wait 0
gosub @SIREN_VOLUME_ADD_MSN18
end 

marker.CreateAboveActor(MISSION_MARKER,$SURVIVER[0])
07E0: set_marker MISSION_MARKER type_to 1
$PEOPLE_DONT_ATTACK = 1
$NOT_ALLOW_COMMANDS = 1
0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP
07B3: set_group PLAYER_GROUP give_command 2
0812: AS_actor $SURVIVER[0] perform_animation "BOM_Plant_Loop" IFP "BOMBER" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 9999999  // versionB

v$OBJECTIVE = "DLC643"

:DL_ADM18_CHECK3
wait 0
0812: AS_actor $SURVIVER[0] perform_animation "BOM_Plant_Loop" IFP "BOMBER" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 9999999  // versionB
gosub @SIREN_VOLUME_ADD_MSN18
0@ = actor.Health($SURVIVER[0])
if
not 0@ == 0
jf @ADD_MSN_KOD_PROVAL18_PEOPLE_DEAD2
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL18_PEOPLE_DEAD
if
$WAIT_TIME < 1
jf @DL_ADM18_CHECK3

v$OBJECTIVE = "DUMMY"
marker.Disable(MISSION_MARKER)
014F: stop_timer $WAIT_TIME

077A: set_actor $SURVIVER[0] acquaintance 4 to_actors_pedtype 20 // see ped.dat

06AB: set_actor $SURVIVER[0] all_weapons_hidden 0

player.CanMove(PLAYER_CHAR) = false

0668: AS_actor $SURVIVER[0] rotate_and_shoot_at 2150.5676 943.1245 11.6719 4000 ms
05D3: AS_actor PLAYER_ACTOR goto_point 2158.9731 943.1725 10.8203 mode 6 time 99999 ms // versionA

text.ClearAll()
text.ShowH('DLC644',3000,1)

fade 0 1000
$HUD_REMOVE = 1
wait 1000
$SAVESTATUS = 1

06C9: remove_actor $SURVIVER[0] from_group
$PEOPLE_DONT_ATTACK = 0
$NOT_ALLOW_COMMANDS = 0
07B3: set_group PLAYER_GROUP give_command 1
$ZOMBIES_HORDE = 0
$MISSION14_ACTIVE = 0
$SALVATION_FLAG = 0

Audiostream.PerformAction($SOUND_EFFECT1, STOP)
Audiostream.Release($SOUND_EFFECT1)
$SIREN_X = 0.0

wait 1000

actor.PutAtNoOffset($SURVIVER[0],2153.8931, 949.5304, 10.8203)
actor.Angle($SURVIVER[0]) = 184.4608

actor.PutAtNoOffset(PLAYER_ACTOR,2153.739, 946.704, 10.8203)
actor.Angle(PLAYER_ACTOR) = 0.9097
           
0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR

camera.SetPosition(2151.7771, 943.2421, 10.68,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
player.CanMove(PLAYER_CHAR) = false

wait 1000
06AB: set_actor $SURVIVER[0] all_weapons_hidden 1
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 999999 ms
0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[2] = PLAYER_ACTOR // (int)

v$ST_DIALOG[0] = "DLC645"
v$ST_DIALOG[1] = "DLC646"
v$ST_DIALOG[2] = "DLC647"
$DIALOG_COUNT = 2                 
create_thread @DG_SYS

:DL_ADM18_CHECK4
wait 0
if
$DIALOG_END == 1
jf @DL_ADM18_CHECK4

fade 0 1000
wait 1000
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0
camera.Restore_WithJumpCut()
cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
player.CanMove(PLAYER_CHAR) = true  
actor.DestroyInstantly($SURVIVER[0])
$SAVESTATUS = 0
ENEX_OFF = 0
gosub @RANDOM_WEAPON_ACTOR_ADD_MSN18
wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

$TEMP_XP = 3000
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

jump to @ADD_MSN_UDACHN18

:ADD_MSN_KOD_PROVAL18_PEOPLE_DEAD
text.ClearAll()
v$FAILED_TEXT = "DLC96"
create_thread @GM_2OVR
jump to @ADD_MSN_KOD_PROVAL18

:ADD_MSN_KOD_PROVAL18_PEOPLE_DEAD2
text.ClearAll()
0792: disembark_instantly_actor $SURVIVER[0]
05BE: AS_actor $SURVIVER[0] die
v$FAILED_TEXT = "DLC96"
create_thread @GM_2OVR
jump to @ADD_MSN_KOD_PROVAL18

:ADD_MSN_UDACHN18 
$ONMISSION = 0   
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
if
MCS_FLAG == 0
then      
0318: set_latest_mission_passed 'DLC648B'
else
0318: set_latest_mission_passed 'DLC648C'
end 
$ADDITIONAL_MISSIONS_PASSED += 1
0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"                            
if and
0@ == 0
$ADDITIONAL_MISSIONS_PASSED >= 20
$CHEATED_FLAG == 0
$CARL_STORY == 1
    then
    v$ACHIEVEMNT = "ACH104"
    $ACHIEVEMENT_GOT_FLAG = 1                                    
    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"
    end
07FB: set_interior 'AMMUN2' access 1
gosub @ADD_MSN_PROPUSK_KOD18
return

:ADD_MSN_KOD_PROVAL18    
$ONMISSION = 0
ENEX_OFF = 0  
$ZOMBIES_HORDE = 0
$MISSION14_ACTIVE = 0
$SALVATION_FLAG = 0
mission_cleanup
marker.Disable(MISSION_CHECKPOINT)
marker.Disable(MISSION_MARKER)
014F: stop_timer $WAIT_TIME  
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
07FB: set_interior 'AMMUN2' access 1   
return

:ADD_MSN_UDALENIE_KOD18 
$ONMISSION = 0
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
return

:RANDOM_WEAPON_ACTOR_ADD_MSN18
wait 0 
0209: 10@ = random_int_in_ranges 22 35 
if and
   not 10@ == 19 
   not 10@ == 20 
   not 10@ == 21 
   not 10@ == 10 
   not 10@ == 11 
   not 10@ == 12 
   not 10@ == 13 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN18
if and
   not 10@ == 7 
   not 10@ == 8 
   not 10@ == 2 
   not 10@ == 1 
   not 10@ == 9 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN18
0781: get_weapon_with_ID 10@ model_to 11@ 
if 
07DE:   model 11@ exists // versionB 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN18
Model.Load(11@)
038B: load_requested_models 
if
model.Available(11@)
jf @RANDOM_WEAPON_ACTOR_ADD_MSN18

0782: get_weapon_with_ID 10@ weapon_group_to 2@
2@ += 1

if
not 2@ == 4
then
0209: 12@ = random_int_in_ranges 120 240
else
0209: 12@ = random_int_in_ranges 60 120
end

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 2@ weapon 1@ ammo 4@ model 3@
if
4@ < 1
then 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 10@, 12@)
else
005A: 4@ += 12@  // (int)
017B: set_actor PLAYER_ACTOR weapon 1@ ammo_to 4@
end

return

:SIREN_VOLUME_ADD_MSN18

actor.StorePos(PLAYER_ACTOR,25@,26@,27@)
050A: 100@ = distance_between_XYZ 25@ 26@ 27@ and_XYZ $SIREN_X $SIREN_Y $SIREN_Z

0077: 100@ /= $SIREN_VOLUME_MULTIPER // (float)

2@ = 1.0

if
100@ > 0.9
    then
    100@ = 0.9
    end
    
0063: 2@ -= 100@  // (float)                   

0086: $SOUNDS_VOLUME22 = $SOUNDS_VOLUME // (float)
006D: $SOUNDS_VOLUME22 *= 2@ // (float)

if or
$SIMULATING_FLAG == 1
not $GAME_SPEED >= 1.0
then
$SOUNDS_VOLUME22 = 0.0
end

Audiostream.Volume($SOUND_EFFECT1) = $SOUNDS_VOLUME22          
                                                                 
return