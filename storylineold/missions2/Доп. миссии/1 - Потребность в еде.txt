//-------------Additioanl Mission 1---------------
// Название: Потребность в еде

:DL_ADM1
thread 'DL_ADM1'
gosub @ADD_MSN_KOD_MISSI1 
if 
wasted_or_busted
Jf @ADD_MSN_PROPUSK_KOD1 
gosub @ADD_MSN_KOD_PROVAL1 

:ADD_MSN_PROPUSK_KOD1 
gosub @ADD_MSN_UDALENIE_KOD1
$QUEST_TYPE = -1
end_thread

:ADD_MSN_KOD_MISSI1 
increment_mission_attempts
0629: change_integer_stat 258 to 65

$ONMISSION = 1   
$NOTSPAWN_ETC = 1
ENEX_OFF = 1
$SAVESTATUS = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
v$OBJECTIVE = "DUMMY"
$QUEST_TYPE = 2048

while not model.Available(#FAM2)
wait 0
model.Load(#FAM2)
mload
end

$SURVIVER[0] = actor.Create(26,#FAM2,2248.165, -1647.9325, 15.4779)
$SURVIVOR_MELEE_ID[0] = -1
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 314.2973
actor.Health($SURVIVER[0]) = $FRIEND_HEALTH

camera.SetPosition(2249.2766, -1648.4374, 15.4776,0.0,0.0,0.0)
03CB: set_rendering_origin_at 2249.2766 -1648.4374 15.4776

actor.PutAtNoOffset(PLAYER_ACTOR,2251.9221, -1648.9899, 15.477)
actor.Angle(PLAYER_ACTOR) = 75.3903                   
0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

0936: set_camera 2253.777832 -1651.936279 15.206261 position_to 2252.994141 -1651.196533 15.217731 time 60000 smooth_transition 1
0920: point_camera 2253.052490 -1651.248047 15.216743 transverse_to 2252.267578 -1650.509521 15.229957 time 60000 smooth_transition 1

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 999999 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 999999 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[6] = PLAYER_ACTOR // (int)

if
MCS_FLAG == 0
then
v$ST_DIALOG[0] = "DLC25"
v$ST_DIALOG[1] = "DLC26"
v$ST_DIALOG[2] = "DLC27"
v$ST_DIALOG[3] = "DLC28"
v$ST_DIALOG[4] = "DLC29"
v$ST_DIALOG[5] = "DLC30"
v$ST_DIALOG[6] = "DLC31"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS
else
v$ST_DIALOG[0] = "DLC25B"
v$ST_DIALOG[1] = "DLC26B"
v$ST_DIALOG[2] = "DLC27"
v$ST_DIALOG[3] = "DLC28"
v$ST_DIALOG[4] = "DLC29"
v$ST_DIALOG[5] = "DLC30"
v$ST_DIALOG[6] = "DLC31"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS
end

repeat
wait 0
until $DIALOG_END == 1

fade 0 1000
wait 1000

0936: set_camera 2253.777832 -1651.936279 15.206261 position_to 2252.994141 -1651.196533 15.217731 time 1 smooth_transition 1
0920: point_camera 2253.052490 -1651.248047 15.216743 transverse_to 2252.267578 -1650.509521 15.229957 time 1 smooth_transition 1

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()

0781: get_weapon_with_ID 32 model_to 1@

while not model.Available(1@)
wait 0
model.Load(1@)
mload
end

actor.GiveWeaponAndAmmo($SURVIVER[0],32,9999)

actor.SetWalkStyle($SURVIVER[0],"GANG1")
0446: set_actor $SURVIVER[0] dismemberment_possible 0
077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype 0 // see ped.dat
060B: set_actor $SURVIVER[0] decision_maker_to $DEC_PEOPLE
actor.WeaponAccuracy($SURVIVER[0]) = 100
07DD: set_actor $SURVIVER[0] attack_rate 999 // previously known as temper_to
0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP

$SAVESTATUS = 0
$NOTSPAWN = 0
cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0

wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000
v$OBJECTIVE = "DLC33"
03BC: MISSION_SPHERE = create_sphere_at 2292.0371 -1724.2112 13.5469 radius 2.0
018A: MISSION_MARKER = create_checkpoint_at 2292.0371 -1724.2112 13.5469

repeat
wait 0

if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL1_FAMGRV_DEAD

if and
00EC:   actor PLAYER_ACTOR sphere 0 near_point 2292.0371 -1724.2112 radius 3.0 3.0
00EC:   actor $SURVIVER[0] sphere 0 near_point 2292.0371 -1724.2112 radius 3.0 3.0
not actor.Driving(PLAYER_ACTOR)
not actor.Driving($SURVIVER[0])
    then
    break
    end

until false

marker.Disable(MISSION_MARKER)
03BD: destroy_sphere MISSION_SPHERE

$ZOMBIE_SPAWN_ON_COORD = 1
$SPECIAL_INFECTED_SPAWN_OZC_FLAG = 1  
$ZOMBIE_COORD_MAX = 1  
    
$ZOMBIE_X[0] = 2296.1094
$ZOMBIE_Y[0] = -1699.9199
$ZOMBIE_Z[0] = 14.5469

v$OBJECTIVE = "DUMMY"

077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype 20 // see ped.dat
0746: set_acquaintance 0 of_actors_pedtype 20 to_actors_pedtype 0
0746: set_acquaintance 4 of_actors_pedtype 20 to_actors_pedtype 26
06C9: remove_actor $SURVIVER[0] from_group
0568: set_actor $SURVIVER[0] untargetable 1

$WAIT_TIME = 180000
008D: $WAIT_TIME = integer $WAIT_TIME to_float
0069: $WAIT_TIME *= $SAVE_TIME2 // (float) 
008C: $WAIT_TIME = float $WAIT_TIME to_integer
if
$TIME > 0
then
014F: stop_timer $TIME
end
03C3: set_timer_to $WAIT_TIME type 1 GXT 'MSN608' // global_variable  // Time

//////////////////////////////////////////////////////////

$SIREN_X = 2292.1787
$SIREN_Y = -1722.6868
$SIREN_Z = 13.5469 
$SIREN_VOLUME_MULTIPER = 200.0

$SOUND_EFFECT1 = Audiostream.Load("AUDIO\SOUNDS\AlarmBanditBase.mp3")

Audiostream.PerformAction($SOUND_EFFECT1, PLAY)
Audiostream.Volume($SOUND_EFFECT1) = VERO_TIPA_VOLUME
Audiostream.Loop($SOUND_EFFECT1, 1) 

//////////////////////////////////////////////////////////

MQ_ALLOW_HORDE = 1  
$ZOMBIES_HORDE = 1
$MISSION14_ACTIVE = 2
$SALVATION_FLAG = 1
0ED9: set_char_ignore_damage_anims $SURVIVER[0] 1

repeat
wait 0
$ZOMBIES_HORDE = 1
gosub @SIREN_VOLUME_ADD_MSN1
animation.Load("BOMBER")
until animation.Available("BOMBER")

0615: define_AS_pack_begin 51@
05D3: AS_actor -1 goto_point 2292.1394 -1723.2825 13.5469 mode 6 time 99999 ms // versionA
05D4: AS_actor -1 rotate_angle 359.963
0812: AS_actor -1 perform_animation "BOM_Plant_Loop" IFP "BOMBER" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 999999  // versionB
0616: define_AS_pack_end 51@
0618: assign_actor $SURVIVER[0] to_AS_pack 51@
061B: remove_references_to_AS_pack 51@
06AB: set_actor $SURVIVER[0] all_weapons_hidden 1

text.ShowH('DLC35',3000,1)

MAIN_QUEST_MUSIC_FLAG = 1

wait 1000

while true
wait 0
$ZOMBIES_HORDE = 1
$END_FADING_MUSIC_FAST = 1

if and
$MUSIC_FADE_ENDED[0] == 1
$MUSIC_FADE_ENDED[1] == 1
$MUSIC_FADE_ENDED[2] == 1
$MUSIC_FADE_ENDED[3] == 1
$MUSIC_FADE_ENDED[4] == 1
then
$END_FADING_MUSIC_FAST = 0
break
end

end

$MUSIC_MISSION = Audiostream.Load("modloader\ZAOLDSTORYFILES\m2\15.mp3")
Audiostream.PerformAction($MUSIC_MISSION, PLAY)
Audiostream.Loop($MUSIC_MISSION, 1)
Audiostream.Volume($MUSIC_MISSION) = $MUSIC_VOLUME
250A: SET_AUDIO_STREAM_TYPE $MUSIC_MISSION type 2
2504: SET_AUDIO_STREAM_VOLUME_WITH_TRANSITION $MUSIC_MISSION volume $MUSIC_VOLUME time 1000 

32@ = 0
while 32@ < 3000
wait 0
$ZOMBIES_HORDE = 1
gosub @SIREN_VOLUME_ADD_MSN1
end

marker.CreateAboveActor(MISSION_MARKER,$SURVIVER[0])
07E0: set_marker MISSION_MARKER type_to 1
$PEOPLE_DONT_ATTACK = 1
$NOT_ALLOW_COMMANDS = 1
0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP
07B3: set_group PLAYER_GROUP give_command 2
0812: AS_actor $SURVIVER[0] perform_animation "BOM_Plant_Loop" IFP "BOMBER" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 9999999  // versionB

if
MCS_FLAG == 0
then
v$OBJECTIVE = "DLC36"
else
v$OBJECTIVE = "DLC36B"
end

repeat
wait 0
$ZOMBIES_HORDE = 1
0812: AS_actor $SURVIVER[0] perform_animation "BOM_Plant_Loop" IFP "BOMBER" framedelta 4.0 loopA 1 lockX 0 lockY 0 lockF 0 time 9999999  // versionB

gosub @SIREN_VOLUME_ADD_MSN1

0@ = actor.Health($SURVIVER[0])
if
not 0@ == 0
jf @ADD_MSN_KOD_PROVAL1_FAMGRV_DEAD2
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL1_FAMGRV_DEAD

until $WAIT_TIME < 1

v$OBJECTIVE = "DUMMY"

marker.Disable(MISSION_MARKER)
014F: stop_timer $WAIT_TIME
if
$TIME > 0
then
03C3: set_timer_to $TIME type 1 GXT 'BB_19' // global_variable
end

06AB: set_actor $SURVIVER[0] all_weapons_hidden 0

player.CanMove(PLAYER_CHAR) = false
0668: AS_actor $SURVIVER[0] rotate_and_shoot_at 2291.9468 -1730.1123 13.3828 4000 ms
05D3: AS_actor PLAYER_ACTOR goto_point 2293.4504 -1723.859 13.5469 mode 6 time 99999 ms // versionA

text.ClearAll()
text.ShowH('DLC37',3000,1)

fade 0 1000
$HUD_REMOVE = 1
wait 1000

$ZOMBIES_HORDE = 0
MQ_ALLOW_HORDE = 0

Audiostream.PerformAction($SOUND_EFFECT1, STOP)
Audiostream.Release($SOUND_EFFECT1)
$SIREN_X = 0.0

$SAVESTATUS = 1

06C9: remove_actor $SURVIVER[0] from_group
$PEOPLE_DONT_ATTACK = 0
$NOT_ALLOW_COMMANDS = 0
07B3: set_group PLAYER_GROUP give_command 1
$ZOMBIES_HORDE = 0
$MISSION14_ACTIVE = 0
$SALVATION_FLAG = 0

gosub @MUSIC_MISSI2_OFF
MAIN_QUEST_MUSIC_FLAG = 0

$ZOMBIE_SPAWN_ON_COORD = 0
$SPECIAL_INFECTED_SPAWN_OZC_FLAG = 0

wait 1000
actor.PutAtNoOffset($SURVIVER[0],2285.803, -1724.1523, 13.5469)
actor.Angle($SURVIVER[0]) = 270.4443
actor.PutAtNoOffset(PLAYER_ACTOR,2289.3625, -1724.0342, 13.5469)
actor.Angle(PLAYER_ACTOR) = 89.6087               
0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR

0936: set_camera 2291.468018 -1726.665649 13.875596 position_to 2289.850342 -1726.749268 13.764594 time 65000 smooth_transition 1
0920: point_camera 2290.636230 -1726.118286 13.783596 transverse_to 2289.316650 -1725.913208 13.637180 time 65000 smooth_transition 1

cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
player.CanMove(PLAYER_CHAR) = false
wait 1000

06AB: set_actor $SURVIVER[0] all_weapons_hidden 1
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 999999 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 999999 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = PLAYER_ACTOR // (int)

v$ST_DIALOG[0] = "DLC38"
v$ST_DIALOG[1] = "DLC39"
v$ST_DIALOG[2] = "DLC40"
v$ST_DIALOG[3] = "DLC41"
$DIALOG_COUNT = 3                 
$BANKA += 10
create_thread @DG_SYS

repeat
wait 0
until $DIALOG_END == 1

fade 0 1000
wait 1000

0936: set_camera 2291.468018 -1726.665649 13.875596 position_to 2289.850342 -1726.749268 13.764594 time 1 smooth_transition 1
0920: point_camera 2290.636230 -1726.118286 13.783596 transverse_to 2289.316650 -1725.913208 13.637180 time 1 smooth_transition 1

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

camera.Restore_WithJumpCut()
cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
player.CanMove(PLAYER_CHAR) = true  
actor.DestroyInstantly($SURVIVER[0])

$SAVESTATUS = 0
$NOTSPAWN_ETC = 0
ENEX_OFF = 0

wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

$TEMP_XP = 1500
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

jump to @ADD_MSN_UDACHN1

:ADD_MSN_KOD_PROVAL1_FAMGRV_DEAD
text.ClearAll()
v$FAILED_TEXT = "DLC34"
create_thread @GM_2OVR
jump to @ADD_MSN_KOD_PROVAL1

:ADD_MSN_KOD_PROVAL1_FAMGRV_DEAD2
text.ClearAll()
0792: disembark_instantly_actor $SURVIVER[0]
05BE: AS_actor $SURVIVER[0] die
v$FAILED_TEXT = "DLC34"
create_thread @GM_2OVR
jump to @ADD_MSN_KOD_PROVAL1

:ADD_MSN_UDACHN1 
$ONMISSION = 0   
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
if
MCS_FLAG == 0
then      
0318: set_latest_mission_passed 'DLC24B'
else
0318: set_latest_mission_passed 'DLC24C'
end 
$ADDITIONAL_MISSIONS_PASSED += 1
0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"                            
if and
0@ == 0
$ADDITIONAL_MISSIONS_PASSED >= 20
$CHEATED_FLAG == 0
$CARL_STORY == 1
    then
    v$ACHIEVEMNT = "ACH104"
    $ACHIEVEMENT_GOT_FLAG = 1                                    
    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"
    end
gosub @ADD_MSN_PROPUSK_KOD1
return

:ADD_MSN_KOD_PROVAL1    
$ONMISSION = 0  
$ZOMBIE_SPAWN_ON_COORD = 0
$SPECIAL_INFECTED_SPAWN_OZC_FLAG = 0
$ZOMBIES_HORDE = 0
$MISSION14_ACTIVE = 0
$SALVATION_FLAG = 0
$NOTSPAWN_ETC = 0
ENEX_OFF = 0
014F: stop_timer $WAIT_TIME
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0

if
$SIREN_X > 0.0
then
Audiostream.PerformAction($SOUND_EFFECT1, STOP)
Audiostream.Release($SOUND_EFFECT1)
$SIREN_X = 0.0
end

gosub @MUSIC_MISSI2_OFF
MAIN_QUEST_MUSIC_FLAG = 0   
return

:ADD_MSN_UDALENIE_KOD1 
$ONMISSION = 0
mission_cleanup
marker.Disable(MISSION_MARKER)
03BD: destroy_sphere MISSION_SPHERE
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
return

:MUSIC_MISSI2_OFF

/////////////////////Плавное затухание/////////////////////
//
//$MUSIC_FADE_NUMBER += 1
//if
//$MUSIC_FADE_NUMBER > 4
//    then
//    $MUSIC_FADE_NUMBER = 0 
//    end    
//
//0084: $MUSIC_FADE1[$MUSIC_FADE_NUMBER] = $MUSIC_MISSION // (int)
//$MUSIC_FADE_END_VOLUME[$MUSIC_FADE_NUMBER] = 0.0
//
//$MUSIC_FADE_TIME[$MUSIC_FADE_NUMBER] = 1000
//$MUSIC_FADE_FLAG[$MUSIC_FADE_NUMBER] = 0  
//$MUSIC_FADE_COUNT[$MUSIC_FADE_NUMBER] = 1 
//create_thread @MUS_FAD
//
//wait 1
//
//while $MUSIC_FADE_ENDED[$MUSIC_FADE_NUMBER] == 0
//wait 0
//end 
2504: SET_AUDIO_STREAM_VOLUME_WITH_TRANSITION $MUSIC_MISSION volume 0.0 time 1000
wait 500
audiostream.Release($MUSIC_MISSION)

//////////////////////////////////////////////////////////

return

:SIREN_VOLUME_ADD_MSN1

actor.StorePos(PLAYER_ACTOR,25@,26@,27@)
050A: 100@ = distance_between_XYZ 25@ 26@ 27@ and_XYZ $SIREN_X $SIREN_Y $SIREN_Z

0077: 100@ /= $SIREN_VOLUME_MULTIPER // (float)

2@ = 1.0

if
100@ > 0.9
    then
    100@ = 0.9
    end
    
0063: 2@ -= 100@  // (float)                   

0086: $SOUNDS_VOLUME22 = $SOUNDS_VOLUME // (float)
006D: $SOUNDS_VOLUME22 *= 2@ // (float)

if or
$SIMULATING_FLAG == 1
not $GAME_SPEED >= 1.0
then
$SOUNDS_VOLUME22 = 0.0
end

Audiostream.Volume($SOUND_EFFECT1) = $SOUNDS_VOLUME22          
                                                                 
return