//-------------Additioanl Mission 14---------------
// Название: Коробки с едой

:DL_AD14
thread 'DL_AD14'
gosub @ADD_MSN_KOD_MISSI14 
if 
wasted_or_busted
Jf @ADD_MSN_PROPUSK_KOD14 
gosub @ADD_MSN_KOD_PROVAL14 

:ADD_MSN_PROPUSK_KOD14 
gosub @ADD_MSN_UDALENIE_KOD14
$QUEST_TYPE = -1
end_thread

:ADD_MSN_KOD_MISSI14 
increment_mission_attempts
0629: change_integer_stat 258 to 78

v$OBJECTIVE = "DUMMY"
player.CanMove(PLAYER_CHAR) = false
$ONMISSION = 1
$SAVESTATUS = 1
$HUD_REMOVE = 1
$NOTSPAWN_ETC = 1

$CAMP_DEAD_FLAG[12] = 1
$QUEST_TYPE = 2048

cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1

03CB: set_rendering_origin_at -2668.6038 1350.9042 55.4297

while not model.Available(206)
wait 0
model.Load(206)
mload
end

$SURVIVER[0] = actor.Create(26,206,-692.0804, 958.8719, 12.2141)
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 179.9636
actor.SetWalkStyle($SURVIVER[0],"GANG1")

actor.PutAtNoOffset(PLAYER_ACTOR,-691.8884, 955.425, 12.2111)
actor.Angle(PLAYER_ACTOR) = 0.6465
                         
camera.SetPosition(-693.4418, 954.4711, 12.2354,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

while not model.Available(#BURRITO)
wait 0
model.Load(#BURRITO)
mload
end

SUPPLY_QUEST_TRUCK = car.Create(#BURRITO,-681.4728, 965.134, 12.2559)
car.Angle(SUPPLY_QUEST_TRUCK) = 88.8128  
 
0229: set_car SUPPLY_QUEST_TRUCK primary_color_to 27 secondary_color_to 0
0A97: 11@ = car SUPPLY_QUEST_TRUCK struct 
11@ += 9
0A8D: 12@ = read_memory 11@ size 1 virtual_protect 0
08BD: set 12@ bit 2
0A8C: write_memory 11@ size 1 value 12@ virtual_protect 0 
/////////////////////
0A97: 11@ = car SUPPLY_QUEST_TRUCK struct 
11@ += 4 
0A8D: 12@ = read_memory 11@ size 1 virtual_protect 0
08BD: set 12@ bit 1 
0A8C: write_memory 11@ size 1 value 12@ virtual_protect 0 
0A97: 22@ = car SUPPLY_QUEST_TRUCK struct 
0A8E: 23@ = 22@ + 5 // int 
0A8C: write_memory 23@ size 4 value 50.0 virtual_protect 0 
08C3: clear 12@ bit 1
08C3: clear 12@ bit 2
0A97: 11@ = car SUPPLY_QUEST_TRUCK struct 
11@ += 18
0A8D: 12@ = read_memory 11@ size 1 virtual_protect 0
08BD: set 12@ bit 2
0A8C: write_memory 11@ size 1 value 12@ virtual_protect 0 
08C3: clear 12@ bit 2
       
0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)

v$ST_DIALOG[0] = "DLC587"
v$ST_DIALOG[1] = "DLC588"
v$ST_DIALOG[2] = "DLC589"
v$ST_DIALOG[3] = "DLC590"
v$ST_DIALOG[4] = "DLC591"
v$ST_DIALOG[5] = "DLC592"
v$ST_DIALOG[6] = "DLC593"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS

:DL_ADM14_CHECK1
wait 0
if
$DIALOG_END == 1
jf @DL_ADM14_CHECK1

fade 0 1000
wait 1000
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms
cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
actor.PutAtNoOffset(PLAYER_ACTOR,-695.923, 954.3542, 12.2742)
actor.Angle(PLAYER_ACTOR) = 358.0354
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()  
actor.DestroyInstantly($SURVIVER[0])

while not model.Available(#BANDIT1)
wait 0
model.Load(#BANDIT1)
mload
end

$ARMY69_2[0] = actor.Create(25,#BANDIT1,-855.989, 1536.8192, 25.7377)
actor.Angle($ARMY69_2[0]) = 270.3445

while not model.Available(#BANDIT2)
wait 0
model.Load(#BANDIT2)
mload
end

$ARMY69_2[1] = actor.Create(25,#BANDIT2,-855.9911, 1516.3511, 25.7377)
actor.Angle($ARMY69_2[1]) = 269.1955

while not model.Available(#BANDIT3)
wait 0
model.Load(#BANDIT3)
mload
end

$ARMY69_2[2] = actor.Create(25,#BANDIT3,-856.1793, 1517.7963, 22.587)
actor.Angle($ARMY69_2[2]) = 269.5001

while not model.Available(#BANDIT4)
wait 0
model.Load(#BANDIT4)
mload
end

$ARMY69_2[3] = actor.Create(25,#BANDIT4,-861.3867, 1536.8483, 22.587)
actor.Angle($ARMY69_2[3]) = 2.6568

for 0@ = 0 to 3
Actor.SetWalkStyle($ARMY69_2[0@], "GANG2")
actor.Health($ARMY69_2[0@]) = $ADD_MSN_BANDIT_HEALTH
Actor.WeaponAccuracy($ARMY69_2[0@]) = $ADD_MSN_BANDIT_ACCURACY
060B: set_actor $ARMY69_2[0@] decision_maker_to $DEC_PEOPLE 
0446: set_actor $ARMY69_2[0@] dismemberment_possible 0   
05E2: AS_actor $ARMY69_2[0@] kill_actor PLAYER_ACTOR
0350: set_actor $ARMY69_2[0@] maintain_position_when_attacked 1
create_thread @PED_BKP $ARMY69_2[0@] 371
create_thread @ANT_PRE $ARMY69_2[0@]

0781: get_weapon_with_ID 30 model_to 1@

while not model.Available(1@)
wait 0
model.Load(1@)
mload
end

actor.GiveWeaponAndAmmo($ARMY69_2[0@],30,9999)
07DD: set_actor $ARMY69_2[0@] attack_rate 999 // previously known as temper_to
end

32@ = 0
$SAVESTATUS = 0

wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

MISSION_MARKER = marker.CreateAboveCar(SUPPLY_QUEST_TRUCK)
07E0: set_marker MISSION_MARKER type_to 1
$HUD_REMOVE = 0 
v$OBJECTIVE = "DLC431"
 
:DL_ADM14_CHECK2
wait 0
if
not car.Wrecked(SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_CAR_DESTROYED
if
actor.InCar(PLAYER_ACTOR,SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_CHECK2
text.ClearAll()
marker.Disable(MISSION_MARKER)
v$OBJECTIVE = "DLC594"
018A: MISSION_CHECKPOINT = create_checkpoint_at -860.4283 1540.9232 22.6388
  
:DL_ADM14_CHECK33
wait 0
gosub @DL_ADM14_AIM_SYSTEM
if
not car.Wrecked(SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_CAR_DESTROYED
if
actor.InCar(PLAYER_ACTOR,SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_OUT_OF_CAR1
if
00EC:   actor PLAYER_ACTOR sphere 0 near_point -860.4283 1540.9232 radius 90.0 90.0
jf @DL_ADM14_CHECK33
     
:DL_ADM14_CHECK3
wait 0
gosub @DL_ADM14_AIM_SYSTEM
if
not car.Wrecked(SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_CAR_DESTROYED
if
actor.InCar(PLAYER_ACTOR,SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_OUT_OF_CAR1
if
00EC:   actor PLAYER_ACTOR sphere 1 near_point -860.4283 1540.9232 radius 2.0 2.0
jf @DL_ADM14_CHECK3
if
actor.Stopped(PLAYER_ACTOR)
jf @DL_ADM14_CHECK3
v$OBJECTIVE = "DUMMY"
marker.Disable(MISSION_CHECKPOINT)
player.CanMove(PLAYER_CHAR) = false

fade 0 1000
$HUD_REMOVE = 1
wait 1000

while not model.Available(1271)
wait 0
model.Load(1271)
mload
end
    
28@ = object.Create(1271,0.0,0.0,0.0)
0681: attach_object 28@ to_car SUPPLY_QUEST_TRUCK with_offset 0.0 -0.28 -0.15 rotation 0.0 0.0 0.0
0382: set_object 28@ collision_detection 0
//model.Destroy(1271)

wait 2000
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()
wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000
v$OBJECTIVE = "DLC595"
018A: MISSION_CHECKPOINT = create_checkpoint_at -701.7225 964.3483 12.3616

:DL_ADM14_CHECK4
wait 0
if
not car.Wrecked(SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_CAR_DESTROYED
if
actor.InCar(PLAYER_ACTOR,SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_OUT_OF_CAR2
if
00EC:   actor PLAYER_ACTOR sphere 1 near_point -701.7225 964.3483 radius 2.0 2.0
jf @DL_ADM14_CHECK4
if
actor.Stopped(PLAYER_ACTOR)
jf @DL_ADM14_CHECK4

v$OBJECTIVE = "DUMMY"
marker.Disable(MISSION_CHECKPOINT)
player.CanMove(PLAYER_CHAR) = false
fade 0 1000
$HUD_REMOVE = 1
wait 1000
$SAVESTATUS = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1

while not model.Available(206)
wait 0
model.Load(206)
mload
end
                     
$SURVIVER[0] = actor.Create(26,206,-692.0804, 958.8719, 12.2141)
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 179.9636
actor.SetWalkStyle($SURVIVER[0],"GANG1")
//model.Destroy(206)
actor.PutAtNoOffset($SURVIVER[0],-692.6191, 959.2413, 12.2225)
actor.Angle($SURVIVER[0]) = 179.2888

0362: remove_actor PLAYER_ACTOR from_car_and_place_at -692.5255 956.4743 12.2211

actor.PutAtNoOffset(PLAYER_ACTOR,-692.5255, 956.4743, 12.2211)
actor.Angle(PLAYER_ACTOR) = 359.6584

camera.SetPosition(-694.0704, 953.6746, 12.2452,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[2] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = PLAYER_ACTOR // (int)

v$ST_DIALOG[0] = "DLC596"
v$ST_DIALOG[1] = "DLC599"
v$ST_DIALOG[2] = "DLC600"
v$ST_DIALOG[3] = "DLC597"
v$ST_DIALOG[4] = "DLC598"
v$ST_DIALOG[5] = "DLC601"
$DIALOG_COUNT = 5                 
create_thread @DG_SYS

:DL_ADM14_CHECK5
wait 0
if
$DIALOG_END == 1
jf @DL_ADM14_CHECK5

fade 0 1000
wait 1000      
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0
gosub @RANDOM_WEAPON_ACTOR_ADD_MSN14
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms
cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
actor.PutAtNoOffset(PLAYER_ACTOR,-698.06, 954.6105, 12.3076)
actor.Angle(PLAYER_ACTOR) = 268.0597
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()  
actor.DestroyInstantly($SURVIVER[0])
car.Destroy(SUPPLY_QUEST_TRUCK)
object.Destroy(28@)
$SAVESTATUS = 0
$NOTSPAWN_ETC = 0
$CAMP_DEAD_FLAG[12] = 0
wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

$TEMP_XP = 2500
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

jump to @ADD_MSN_UDACHN14

:DL_ADM14_CAR_DESTROYED
$CAMP_DEAD_FLAG[12] = 0
text.ClearAll()
v$FAILED_TEXT = "DLC432"
create_thread @GM_2OVR
jump to @ADD_MSN_KOD_PROVAL14

:DL_ADM14_OUT_OF_CAR1
marker.Disable(MISSION_CHECKPOINT)
v$OBJECTIVE = "DLC971" 
MISSION_MARKER = Marker.CreateAboveCar(SUPPLY_QUEST_TRUCK)
07E0: set_marker MISSION_MARKER type_to 1
 
:DL_ADM14_OUT_OF_CAR1_CHECK
wait 0
gosub @DL_ADM14_AIM_SYSTEM
if
not car.Wrecked(SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_CAR_DESTROYED
if
actor.InCar(PLAYER_ACTOR,SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_OUT_OF_CAR1_CHECK
Marker.Disable(MISSION_MARKER)
v$OBJECTIVE = "DLC594"
018A: MISSION_CHECKPOINT = create_checkpoint_at -860.4283 1540.9232 22.6388
jump to @DL_ADM14_CHECK3

:DL_ADM14_OUT_OF_CAR2
marker.Disable(MISSION_CHECKPOINT)
v$OBJECTIVE = "DLC971" 
MISSION_MARKER = Marker.CreateAboveCar(SUPPLY_QUEST_TRUCK)
07E0: set_marker MISSION_MARKER type_to 1
 
:DL_ADM14_OUT_OF_CAR2_CHECK
wait 0
if
not car.Wrecked(SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_CAR_DESTROYED
if
actor.InCar(PLAYER_ACTOR,SUPPLY_QUEST_TRUCK)
jf @DL_ADM14_OUT_OF_CAR2_CHECK
Marker.Disable(MISSION_MARKER)
018A: MISSION_CHECKPOINT = create_checkpoint_at -701.7225 964.3483 12.3616
v$OBJECTIVE = "DLC595"
jump to @DL_ADM14_CHECK4

:RANDOM_WEAPON_ACTOR_ADD_MSN14
wait 0 
0209: 10@ = random_int_in_ranges 22 35 
if and
   not 10@ == 19 
   not 10@ == 20 
   not 10@ == 21 
   not 10@ == 10 
   not 10@ == 11 
   not 10@ == 12 
   not 10@ == 13 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN14
if and
   not 10@ == 7 
   not 10@ == 8 
   not 10@ == 2 
   not 10@ == 1 
   not 10@ == 9 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN14
0781: get_weapon_with_ID 10@ model_to 11@ 
if 
07DE:   model 11@ exists // versionB 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN14
Model.Load(11@)
038B: load_requested_models 
if
model.Available(11@)
jf @RANDOM_WEAPON_ACTOR_ADD_MSN14

0782: get_weapon_with_ID 10@ weapon_group_to 2@
2@ += 1

if
not 2@ == 4
then
0209: 12@ = random_int_in_ranges 60 120
else
0209: 12@ = random_int_in_ranges 30 60
end

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 2@ weapon 1@ ammo 4@ model 3@
if
4@ < 1
then 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 10@, 12@)
else
005A: 4@ += 12@  // (int)
017B: set_actor PLAYER_ACTOR weapon 1@ ammo_to 4@
end

return

:ADD_MSN_UDACHN14 
$ONMISSION = 0   
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
if
MCS_FLAG == 0
then      
0318: set_latest_mission_passed 'DLC602B'
else
0318: set_latest_mission_passed 'DLC602C'
end  
$ADDITIONAL_MISSIONS_PASSED += 1
0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"                            
if and
0@ == 0
$ADDITIONAL_MISSIONS_PASSED >= 20
$CHEATED_FLAG == 0
$CARL_STORY == 1
    then
    v$ACHIEVEMNT = "ACH104"
    $ACHIEVEMENT_GOT_FLAG = 1                                    
    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"
    end
gosub @ADD_MSN_PROPUSK_KOD14
return

:ADD_MSN_KOD_PROVAL14    
$ONMISSION = 0  
mission_cleanup
marker.Disable(MISSION_CHECKPOINT)
marker.Disable(MISSION_MARKER)
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0   
return

:ADD_MSN_UDALENIE_KOD14 
$ONMISSION = 0
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
return

:DL_ADM14_AIM_SYSTEM

if
32@ > 500
then
32@ = 0
random_int 0@ 0 4
062E: get_actor $ARMY69_2[0@] task 1506 status_store_to 1@ // ret 7 if not found
if and
0184:   actor $ARMY69_2[0@] health > 0
0364:   actor $ARMY69_2[0@] spotted_actor PLAYER_ACTOR
1@ == 7
    then
    if
    82E0: not  actor $ARMY69_2[0@] firing_weapon
        then
        05E2: AS_actor $ARMY69_2[0@] kill_actor PLAYER_ACTOR
        end
    end
end

return