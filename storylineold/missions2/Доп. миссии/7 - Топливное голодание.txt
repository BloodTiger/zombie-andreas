//-------------Additioanl Mission 7---------------
// Название: Бензозаправка

:DL_ADM7
thread 'DL_ADM7'
gosub @ADD_MSN_KOD_MISSI7 
if 
wasted_or_busted
Jf @ADD_MSN_PROPUSK_KOD7 
gosub @ADD_MSN_KOD_PROVAL7 

:ADD_MSN_PROPUSK_KOD7 
gosub @ADD_MSN_UDALENIE_KOD7
$QUEST_TYPE = -1
end_thread

:ADD_MSN_KOD_MISSI7 
increment_mission_attempts
0629: change_integer_stat 258 to 71

v$OBJECTIVE = "DUMMY"
$ONMISSION = 1
$SAVESTATUS = 1
$HUD_REMOVE = 1
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1
$QUEST_TYPE = 2048

while not model.Available(230)
wait 0
model.Load(230)
mload
end

03CB: set_rendering_origin_at -353.2496 -1044.7535 59.3641

$SURVIVER[0] = actor.Create(26,230,-353.2496, -1044.7535, 59.3641)
create_thread @PED_BKP $SURVIVER[0] 371
actor.Angle($SURVIVER[0]) = 92.3374
actor.SetWalkStyle($SURVIVER[0],"GANG1")
//model.Destroy(230)
actor.PutAtNoOffset(PLAYER_ACTOR,-355.1354, -1044.9352, 59.3796)
actor.Angle(PLAYER_ACTOR) = 270.8269
camera.SetPosition(-357.9261, -1042.4452, 60.3976,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[2] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[3] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[4] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[5] = PLAYER_ACTOR // (int)
0084: $DIALOG_ACTOR[6] = $SURVIVER[0] // (int)

v$ST_DIALOG[0] = "DLC486"
v$ST_DIALOG[1] = "DLC487"
v$ST_DIALOG[2] = "DLC488"
v$ST_DIALOG[3] = "DLC489"
v$ST_DIALOG[4] = "DLC490"
v$ST_DIALOG[5] = "DLC491"
v$ST_DIALOG[6] = "DLC492"
$DIALOG_COUNT = 6                 
create_thread @DG_SYS

:DL_ADM7_CHECK1
wait 0
if
$DIALOG_END == 1
jf @DL_ADM7_CHECK1

fade 0 1000
wait 1000

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms

cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0

actor.PutAtNoOffset(PLAYER_ACTOR,-357.31, -1044.9719, 59.3777)
actor.Angle(PLAYER_ACTOR) = 185.0772
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()
  
0446: set_actor $SURVIVER[0] dismemberment_possible 0
077A: set_actor $SURVIVER[0] acquaintance 0 to_actors_pedtype 0 // see ped.dat
060B: set_actor $SURVIVER[0] decision_maker_to $DEC_PEOPLE
actor.Health($SURVIVER[0]) = $FRIEND_HEALTH
actor.WeaponAccuracy($SURVIVER[0]) = 100
07DD: set_actor $SURVIVER[0] attack_rate 999 // previously known as temper_to

0781: get_weapon_with_ID 5 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end
 
Actor.GiveWeaponAndAmmo($SURVIVER[0], 5, 1)

$SURVIVOR_MELEE_ID[0] = 5
$SURVIVOR_WEP_ID[0] = 28
create_thread @MEL_SRV $SURVIVER[0] 0

0781: get_weapon_with_ID 28 model_to 0@

while not model.Available(0@)
wait 0
model.Load(0@)
mload
end

actor.GiveWeaponAndAmmo($SURVIVER[0],28,99999)

$SAVESTATUS = 0
0631: put_actor $SURVIVER[0] in_group PLAYER_GROUP
wait 1000
fade 1 1000
$HUD_REMOVE = 0    
wait 1000
v$OBJECTIVE = "DLC493"
018A: MISSION_CHECKPOINT = create_checkpoint_at -92.1566 -1168.796 2.4503
03BC: MISSION_SPHERE = create_sphere_at -92.1566 -1168.796 2.4503 radius 2.0

:DL_ADM7_CHECK2
wait 0
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL7_PEOPLE_DEAD
if and
00EC:   actor PLAYER_ACTOR sphere 0 near_point -92.1566 -1168.796 radius 2.0 2.0
00EC:   actor $SURVIVER[0] sphere 0 near_point -92.1566 -1168.796 radius 5.0 5.0
not actor.Driving(PLAYER_ACTOR)
not actor.Driving($SURVIVER[0])
jf @DL_ADM7_CHECK2
v$OBJECTIVE = "DUMMY"
          
marker.Disable(MISSION_CHECKPOINT)
03BD: destroy_sphere MISSION_SPHERE
04ED: load_animation "BOMBER"
  
00BC: show_text_highpriority GXT 'DLC494' time 4000 flag 1
 
0615: define_AS_pack_begin 19@ 
05D3: AS_actor -1 goto_point -86.4451 -1164.5571 2.2466 mode 6 time 30000 ms // versionA 
0605: actor -1 perform_animation "BOM_PLANT" IFP "BOMBER" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
0616: define_AS_pack_end 19@ 
0618: assign_actor $SURVIVER[0] to_AS_pack 19@
 
061B: remove_references_to_AS_pack 19@ 
0615: define_AS_pack_begin 18@ 
05D3: AS_actor -1 goto_point -91.2602 -1175.9753 2.195 mode 6 time 30000 ms // versionA 
0605: actor -1 perform_animation "BOM_PLANT" IFP "BOMBER" framedelta 4.0 loop 0 lockX 0 lockY 0 lockF 0 time -1 
0616: define_AS_pack_end 18@ 
0618: assign_actor PLAYER_ACTOR to_AS_pack 18@ 
061B: remove_references_to_AS_pack 18@ 

wait 6000 
00BC: show_text_highpriority GXT 'DLC495' time 3000 flag 1 
wait 3000 
v$OBJECTIVE = "DLC496"
018A: MISSION_CHECKPOINT = create_checkpoint_at -357.7512 -1043.5785 59.3886

:DL_ADM7_CHECK3
wait 0
if
not actor.Dead($SURVIVER[0])
jf @ADD_MSN_KOD_PROVAL7_PEOPLE_DEAD
if and
00EC:   actor PLAYER_ACTOR sphere 1 near_point -357.7512 -1043.5785 radius 2.0 2.0
00EC:   actor $SURVIVER[0] sphere 0 near_point -357.7512 -1043.5785 radius 5.0 5.0
jf @DL_ADM7_CHECK3

v$OBJECTIVE = "DUMMY"
marker.Disable(MISSION_CHECKPOINT)
player.CanMove(PLAYER_CHAR) = false
 
fade 0 1000
$HUD_REMOVE = 1
wait 1000
$SAVESTATUS = 1
    
cinema.Widescreen(1)
BLACK_POLOSKI_ON = 1 
06C9: remove_actor $SURVIVER[0] from_group

actor.PutAtNoOffset($SURVIVER[0],-353.2496, -1044.7535, 59.3641)
actor.Angle($SURVIVER[0]) = 92.33741

actor.PutAtNoOffset(PLAYER_ACTOR,-355.1354, -1044.9352, 59.3796)
actor.Angle(PLAYER_ACTOR) = 270.8269

camera.SetPosition(-357.9261, -1042.4452, 60.3976,0.0,0.0,0.0)
camera.OnPed($SURVIVER[0],15,2)

0639: AS_actor $SURVIVER[0] rotate_to_actor PLAYER_ACTOR
0639: AS_actor PLAYER_ACTOR rotate_to_actor $SURVIVER[0]

06AB: set_actor PLAYER_ACTOR all_weapons_hidden 1

wait 1000
fade 1 1000
wait 1000

05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR -1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] -1 ms

0084: $DIALOG_ACTOR[0] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[1] = $SURVIVER[0] // (int)
0084: $DIALOG_ACTOR[2] = PLAYER_ACTOR // (int)

v$ST_DIALOG[0] = "DLC497"
v$ST_DIALOG[1] = "DLC498"
v$ST_DIALOG[2] = "DLC499"
$DIALOG_COUNT = 2                 
create_thread @DG_SYS

:DL_ADM7_CHECK4
wait 0
if
$DIALOG_END == 1
jf @DL_ADM7_CHECK4

fade 0 1000
wait 1000   
06AB: set_actor PLAYER_ACTOR all_weapons_hidden 0
05BF: AS_actor $SURVIVER[0] look_at_actor PLAYER_ACTOR 1 ms
05BF: AS_actor PLAYER_ACTOR look_at_actor $SURVIVER[0] 1 ms
cinema.Widescreen(0)
BLACK_POLOSKI_ON = 0
actor.PutAtNoOffset(PLAYER_ACTOR,-357.31, -1044.9719, 59.3777)
actor.Angle(PLAYER_ACTOR) = 185.0772
player.CanMove(PLAYER_CHAR) = true
camera.Restore_WithJumpCut()
actor.DestroyInstantly($SURVIVER[0])
gosub @RANDOM_WEAPON_ACTOR_ADD_MSN7
$SAVESTATUS = 0
wait 1000
fade 1 1000
$HUD_REMOVE = 0
wait 1000

$TEMP_XP = 2000
008D: $TEMP_XP = integer $TEMP_XP to_float
0069: $TEMP_XP *= $XP_COEFF // (float)
if
PERK_CHARISMA == 2
then
$TEMP_XP *= CHARISMA_LEVEL2_XP_COEFF
end
008C: $TEMP_XP = float $TEMP_XP to_integer
0058: $PLAYER_XP += $TEMP_XP // (int)

0629: change_integer_stat 258 to 0

jump to @ADD_MSN_UDACHN7

:RANDOM_WEAPON_ACTOR_ADD_MSN7
wait 0 
0209: 10@ = random_int_in_ranges 22 35 
if and
   not 10@ == 19 
   not 10@ == 20 
   not 10@ == 21 
   not 10@ == 10 
   not 10@ == 11 
   not 10@ == 12 
   not 10@ == 13 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN7
if and
   not 10@ == 7 
   not 10@ == 8 
   not 10@ == 2 
   not 10@ == 1 
   not 10@ == 9 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN7
0781: get_weapon_with_ID 10@ model_to 11@ 
if 
07DE:   model 11@ exists // versionB 
jf @RANDOM_WEAPON_ACTOR_ADD_MSN7
Model.Load(11@)
038B: load_requested_models 
if
model.Available(11@)
jf @RANDOM_WEAPON_ACTOR_ADD_MSN7

0782: get_weapon_with_ID 10@ weapon_group_to 2@
2@ += 1

if
not 2@ == 4
then
0209: 12@ = random_int_in_ranges 60 120
else
0209: 12@ = random_int_in_ranges 30 60
end

04B8: get_weapon_data_from_actor PLAYER_ACTOR slot 2@ weapon 1@ ammo 4@ model 3@
if
4@ < 1
then 
Actor.GiveWeaponAndAmmo(PLAYER_ACTOR, 10@, 12@)
else
005A: 4@ += 12@  // (int)
017B: set_actor PLAYER_ACTOR weapon 1@ ammo_to 4@
end

return

:ADD_MSN_KOD_PROVAL7_PEOPLE_DEAD
text.ClearAll()
v$FAILED_TEXT = "DLC96"
create_thread @GM_2OVR 
jump to @ADD_MSN_KOD_PROVAL7

:ADD_MSN_UDACHN7 
$ONMISSION = 0   
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
if
MCS_FLAG == 0
then      
0318: set_latest_mission_passed 'DLC485B'
else
0318: set_latest_mission_passed 'DLC485C'
end 
$ADDITIONAL_MISSIONS_PASSED += 1
0AF0: 0@ = get_int_from_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"                            
if and
0@ == 0
$ADDITIONAL_MISSIONS_PASSED >= 20
$CHEATED_FLAG == 0
$CARL_STORY == 1
    then
    v$ACHIEVEMNT = "ACH104"
    $ACHIEVEMENT_GOT_FLAG = 1                                    
    0AF1: write_int 1 to_ini_file "saves/ZACM.set" section "SCR" key "ACHIEVEMENT104_GOT"
    end
gosub @ADD_MSN_PROPUSK_KOD7
return

:ADD_MSN_KOD_PROVAL7    
$ONMISSION = 0  
mission_cleanup
marker.Disable(MISSION_CHECKPOINT)
03BD: destroy_sphere MISSION_SPHERE
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0   
return

:ADD_MSN_UDALENIE_KOD7 
$ONMISSION = 0
mission_cleanup
06D0: enable_emergency_traffic 0 
06D7: enable_train_traffic 0 
096A: enable_flying_helis 0 
099E: enable_police_patrols 0 
06C8: enable_riot 0 
0923: enable_air_traffic 0 
03DE: set_pedestrians_density_multiplier_to 0.0 
01EB: set_traffic_density_multiplier_to 0.0
return