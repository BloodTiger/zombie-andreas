:NO_WAY
thread 'NO_WAY'

if
0@ > 0
then

repeat
wait 0
until $HUD_REMOVE == 0

end

repeat
wait 250

if and
0E45: frame_mod 60
$ACTIVE_INTERIOR == 0
not $INT_ENTERING_FLAG == 1
8867: not  unknown_in_burglary_interior 0
then
actor.StorePos(PLAYER_ACTOR,0@,1@,2@)
07EF: get_town_number_from_point 0@ 1@ 2@ store_to 3@
0843: get_zone_at 0@ 1@ 2@ nameA_to 4@s // 8-byte string
if and
not 3@ == 1
09E7:   player PLAYER_CHAR not_frozen
    then
    if and
    8D49: not 6@ = compare_strings 4@s "RED" // IF and SET
    8D49: not 6@ = compare_strings 4@s "RIH" // IF and SET
    8D49: not 6@ = compare_strings 4@s "RIH4" // IF and SET
    8D49: not 6@ = compare_strings 4@s "VIN" // IF and SET
    8D49: not 6@ = compare_strings 4@s "MUL" // IF and SET
    8D49: not 6@ = compare_strings 4@s "LA" // IF and SET
    8D49: not 6@ = compare_strings 4@s "MULINT" // IF and SET
    8583: not  player PLAYER_CHAR in_zone 'RED'   
        then
        
        player.CanMove(PLAYER_CHAR,false)
        0221: set_player PLAYER_CHAR apply_brakes_to_car 1        
        
//        0AD1: show_formatted_text_highpriority "%s, %d" time 2000 4@s 3@
//        0AF5: write_string 4@s to_ini_file "debuf\NOWAY.ini" section "ZONE" key "ZONE"
//        wait 2000
        
        random_int 0@ 1 6
        0AD3: v$ST_DIALOG[0] = format "NOWAY%d" 0@    
        $DIALOG_COUNT = 0
        $DIALOG_NOT_SKIP = 1
        create_thread @DG_SYS2
        
        repeat
        wait 0     
        until $DIALOG_END == 1
            
        fade 0 500
        $HUD_REMOVE = 1
        wait 500
        
        9@ = 0.0
        
        while not model.Available(#MALE01)
        wait 0
        model.Load(#MALE01)
        mload
        end
        
        20@ = actor.Create(5,#MALE01,0.0,0.0,0.0)   
        0337: set_actor 20@ visibility 0
        0619: enable_actor 20@ collision_detection 0
        0489: set_actor 20@ muted 1
                
        repeat
        wait 0
        9@ += 100.0
        actor.StorePos(PLAYER_ACTOR,0@,1@,2@)
        0E27: get_angle_from_two_coords 0@ 1@ and 1595.4697 -1481.8032 to 3@
        0EF0: get_coord_from_angled_distance 0@ 1@ angle 3@ dist 9@ store_to 5@ 6@
        07EF: get_town_number_from_point 5@ 6@ 0.0 store_to 7@
        
        actor.PutAtNoOffset(20@,5@,6@,5.0)
        if and
        0154:   actor 20@ in_zone 'RED'  // Easter Basin
        not 7@ == 1
        then
        9@ += 300.0
        actor.StorePos(PLAYER_ACTOR,0@,1@,2@)
        0E27: get_angle_from_two_coords 0@ 1@ and 1595.4697 -1481.8032 to 3@
        0EF0: get_coord_from_angled_distance 0@ 1@ angle 3@ dist 9@ store_to 5@ 6@
        07EF: get_town_number_from_point 5@ 6@ 0.0 store_to 7@
        break
        end
        
        until 7@ == 1
        
        actor.DestroyInstantly(20@)
        random_int 24@ 0 2
        06F8: get_nearest_route_for 5@ 6@ 0.0 in_direction 24@ store_to 10@ 11@ 12@ Z_angle_to 13@
        
        if
        not actor.Driving(PLAYER_ACTOR)
        then
        actor.PutAt(PLAYER_ACTOR,10@,11@,12@)
        actor.Angle(PLAYER_ACTOR) = 3@
        04C4: store_coords_to 5@ 6@ 7@ from_actor PLAYER_ACTOR with_offset 0.0 3.0 0.0
        07CD: AS_actor PLAYER_ACTOR walk_to 5@ 6@ 7@ stop_with_angle 3@ within_radius 1.0 
        else
        0@ = actor.CurrentCar(PLAYER_ACTOR)
        car.PutAt(0@,10@,11@,12@)
        car.Angle(0@) = 13@       
        end
        
        camera.Restore_WithJumpCut()
        player.CanMove(PLAYER_CHAR,true)
        0221: set_player PLAYER_CHAR apply_brakes_to_car 0    
        wait 1000
        create_thread @3D_REFR
        fade 1 500
        $HUD_REMOVE = 0
        wait 500
        
        wait 2000
        end
    end
end

until false

end_thread